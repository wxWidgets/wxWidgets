dnl Process this file with autoconf to produce a configure script.
AC_REVISION($Id$)dnl

AC_PREREQ(2.58)

dnl ---------------------------------------------------------------------------
dnl
dnl Top-level configure.in for wxWidgets by Robert Roebling, Phil Blecker,
dnl Vadim Zeitlin and Ron Lee
dnl
dnl This script is under the wxWidgets licence.
dnl
dnl Version: $Id$
dnl ---------------------------------------------------------------------------

dnl ---------------------------------------------------------------------------
dnl initialization
dnl ---------------------------------------------------------------------------

AC_INIT([wxWidgets], [2.5.3], [wx-dev@lists.wxwidgets.org])

dnl the file passed to AC_CONFIG_SRCDIR should be specific to our package
AC_CONFIG_SRCDIR([wx-config.in])

dnl sets build, host, target variables and the same with _alias
AC_CANONICAL_BUILD
AC_CANONICAL_HOST
AC_CANONICAL_TARGET


dnl When making releases do:
dnl
dnl wx_release_number += 1
dnl
dnl ..and update WX_CURRENT, WX_REVISION and WX_AGE according to the
dnl following rules:
dnl
dnl    If any changes have been made to the public interface, that is if any
dnl    exported class, method, global or global type has been added, removed
dnl    or changed in any way, then do:  WX_CURRENT += 1
dnl
dnl    If source changes have been made that *do not* alter the public
dnl    interface then do:  WX_REVISION += 1
dnl    If WX_CURRENT was incremented (as above) instead do:  WX_REVISION = 0
dnl
dnl    If any public interface was added, do:  WX_AGE += 1
dnl    If any public interface was removed (or altered in a way effectively
dnl    removing the previous definition), instead do:  WX_AGE = 0
dnl
dnl When the major or minor version numbers are incremented, all the above
dnl variables should be reset to 0.
dnl
dnl The resulting library name will be of the form:
dnl   libwx_$(TOOLKIT)-$(WX_RELEASE).so.$(WX_CURRENT).$(WX_REVISION).$(WX_AGE)

wx_major_version_number=2
wx_minor_version_number=5
wx_release_number=3
wx_subrelease_number=2

WX_RELEASE=$wx_major_version_number.$wx_minor_version_number
WX_VERSION=$WX_RELEASE.$wx_release_number
WX_SUBVERSION=$WX_VERSION.$wx_subrelease_number

WX_MSW_VERSION=$wx_major_version_number$wx_minor_version_number$wx_release_number

WX_CURRENT=4
WX_REVISION=0
WX_AGE=1


dnl ------------------------------------------------------------------------
dnl Check platform (host system)
dnl ------------------------------------------------------------------------

dnl OS (assume Unix)
USE_UNIX=1
USE_OS2=0
USE_WIN32=0
USE_DOS=0
USE_BEOS=0
USE_MAC=0

dnl Unix kind
USE_AIX=
USE_BSD=                            dnl any BSD
USE_DARWIN=                         dnl a.k.a. Mac OS X
USE_FREEBSD=
USE_GNU=                            dnl GNU/Hurd
USE_HPUX=
USE_LINUX=
USE_NETBSD=
USE_OPENBSD=
USE_OSF=                            dnl OSF/1 (obsolete?)
USE_SGI=
USE_SOLARIS=                        dnl Solaris ("SunOS" >= 5)
USE_SUN=                            dnl SunOS or Solaris
USE_SUNOS=                          dnl old/real SunOS (obsolete)
USE_SVR4=                           dnl SysV R4
USE_SYSV=                           dnl any System V
USE_VMS=
USE_ULTRIX=
USE_UNIXWARE=

dnl hardware platform
USE_ALPHA=

dnl on some platforms xxx_r() functions are declared inside "#ifdef
dnl _REENTRANT" and it's easier to just define this symbol for these platforms
dnl than checking it during run-time
NEEDS_D_REENTRANT_FOR_R_FUNCS=0

dnl the list of all available toolkits
dnl
dnl update NUM_TOOLKITS calculation below when adding a new toolkit here!
ALL_TOOLKITS="COCOA GTK MAC MGL MICROWIN MOTIF MSW PM X11"

dnl NB: these wxUSE_XXX constants have value of 0 or 1 unlike all the other ones
dnl     which are either yes or no
DEFAULT_wxUSE_COCOA=0
DEFAULT_wxUSE_GTK=0
DEFAULT_wxUSE_MAC=0
DEFAULT_wxUSE_MGL=0
DEFAULT_wxUSE_MICROWIN=0
DEFAULT_wxUSE_MOTIF=0
DEFAULT_wxUSE_MSW=0
DEFAULT_wxUSE_PM=0
DEFAULT_wxUSE_X11=0

dnl these are the values which are really default for the given platform -
dnl they're not cached and are only used if no --with-toolkit was given *and*
dnl nothing was found in the cache
DEFAULT_DEFAULT_wxUSE_COCOA=0
DEFAULT_DEFAULT_wxUSE_GTK=0
DEFAULT_DEFAULT_wxUSE_MAC=0
DEFAULT_DEFAULT_wxUSE_MGL=0
DEFAULT_DEFAULT_wxUSE_MICROWIN=0
DEFAULT_DEFAULT_wxUSE_MOTIF=0
DEFAULT_DEFAULT_wxUSE_MSW=0
DEFAULT_DEFAULT_wxUSE_PM=0
DEFAULT_DEFAULT_wxUSE_X11=0

PROGRAM_EXT=
SO_SUFFIX=so
SAMPLES_RPATH_FLAG=
SAMPLES_RPATH_POSTLINK=

dnl to support a new system, you need to add its canonical name (as determined
dnl by config.sub or specified by the configure command line) to this "case"
dnl and also define the shared library flags below - search for
dnl SHARED_LIB_SETUP to find the exact place
case "${host}" in
  *-hp-hpux* )
    USE_HPUX=1
    DEFAULT_DEFAULT_wxUSE_MOTIF=1
    NEEDS_D_REENTRANT_FOR_R_FUNCS=1
    SO_SUFFIX=sl
    AC_DEFINE(__HPUX__)
  ;;
  *-*-linux* )
    USE_LINUX=1
    AC_DEFINE(__LINUX__)
    TMP=`uname -m`
    if test "x$TMP" = "xalpha"; then
      USE_ALPHA=1
      AC_DEFINE(__ALPHA__)
    fi
    DEFAULT_DEFAULT_wxUSE_GTK=1
  ;;
  *-*-gnu* | *-*-k*bsd*-gnu )
    USE_GNU=1
    TMP=`uname -m`
    if test "x$TMP" = "xalpha"; then
      USE_ALPHA=1
      AC_DEFINE(__ALPHA__)
    fi
    DEFAULT_DEFAULT_wxUSE_GTK=1
  ;;
  *-*-irix5* | *-*-irix6* )
    USE_SGI=1
    USE_SVR4=1
    AC_DEFINE(__SGI__)
    AC_DEFINE(__SVR4__)
    DEFAULT_DEFAULT_wxUSE_MOTIF=1
  ;;
  *-*-solaris2* )
    USE_SUN=1
    USE_SOLARIS=1
    USE_SVR4=1
    AC_DEFINE(__SUN__)
    AC_DEFINE(__SOLARIS__)
    AC_DEFINE(__SVR4__)
    DEFAULT_DEFAULT_wxUSE_MOTIF=1
    NEEDS_D_REENTRANT_FOR_R_FUNCS=1
  ;;
  *-*-sunos4* )
    USE_SUN=1
    USE_SUNOS=1
    USE_BSD=1
    AC_DEFINE(__SUN__)
    AC_DEFINE(__SUNOS__)
    AC_DEFINE(__BSD__)
    DEFAULT_DEFAULT_wxUSE_MOTIF=1
  ;;
  *-*-freebsd*)
    USE_BSD=1
    USE_FREEBSD=1
    AC_DEFINE(__FREEBSD__)
    AC_DEFINE(__BSD__)
    DEFAULT_DEFAULT_wxUSE_GTK=1
  ;;
  *-*-openbsd*)
    USE_BSD=1
    USE_OPENBSD=1
    AC_DEFINE(__OPENBSD__)
    AC_DEFINE(__BSD__)
    DEFAULT_DEFAULT_wxUSE_GTK=1
  ;;
  *-*-netbsd*)
    USE_BSD=1
    USE_NETBSD=1
    AC_DEFINE(__NETBSD__)
    AC_DEFINE(__BSD__)
    DEFAULT_DEFAULT_wxUSE_GTK=1
  ;;
  *-*-osf* )
    USE_ALPHA=1
    USE_OSF=1
    AC_DEFINE(__ALPHA__)
    AC_DEFINE(__OSF__)
    DEFAULT_DEFAULT_wxUSE_MOTIF=1
    NEEDS_D_REENTRANT_FOR_R_FUNCS=1
  ;;
  *-*-dgux5* )
    USE_ALPHA=1
    USE_SVR4=1
    AC_DEFINE(__ALPHA__)
    AC_DEFINE(__SVR4__)
    DEFAULT_DEFAULT_wxUSE_MOTIF=1
  ;;
  *-*-sysv5* )
    USE_SYSV=1
    USE_SVR4=1
    AC_DEFINE(__SYSV__)
    AC_DEFINE(__SVR4__)
    DEFAULT_DEFAULT_wxUSE_MOTIF=1
  ;;
  *-*-aix* )
    USE_AIX=1
    USE_SYSV=1
    USE_SVR4=1
    dnl quoting from http://www-1.ibm.com/servers/esdd/articles/gnu.html:
    dnl
    dnl     Both archive libraries and shared libraries on AIX have an .a
    dnl     extension. This will explain why you can't link with an .so and
    dnl     why it works with the name changed to .a.
    SO_SUFFIX=a
    AC_DEFINE(__AIX__)
    AC_DEFINE(__SYSV__)
    AC_DEFINE(__SVR4__)
    DEFAULT_DEFAULT_wxUSE_MOTIF=1
  ;;

  *-*-*UnixWare*)
    USE_SYSV=1
    USE_SVR4=1
    USE_UNIXWARE=1
    AC_DEFINE(__UNIXWARE__)
  ;;

  *-*-cygwin* | *-*-mingw32* )
    dnl MBN: some of the defines have been moved after toolkit detection
    dnl      because for wxMotif/wxGTK/wxX11 to build on Cygwin
    dnl      USE_UNIX  must be set and not USE_WIN32
    SO_SUFFIX=dll
    PROGRAM_EXT=".exe"
    RESCOMP=windres
    DEFAULT_DEFAULT_wxUSE_MSW=1
  ;;

  *-pc-msdosdjgpp )
    USE_UNIX=0
    USE_DOS=1
    AC_DEFINE(__DOS__)
    PROGRAM_EXT=".exe"
    DEFAULT_DEFAULT_wxUSE_MGL=1
    dnl DJGPP needs explicit -lstdc++ for some reason (VS: mayb some versions only?)
    LIBS="$LIBS -lstdc++"
  ;;

  *-pc-os2_emx | *-pc-os2-emx )
    AC_DEFINE(__EMX__)
    USE_OS2=1
    AC_DEFINE(__OS2__)
    PROGRAM_EXT=".exe"
    DEFAULT_DEFAULT_wxUSE_PM=1
    dnl "c++" wrapper is not always available, so always use plain gcc.
    CXX=gcc
    LDFLAGS="$LDFLAGS -Zcrtdll"
    dnl
    dnl Some special code that's automatically added by autoconf-2.57 for OS/2
    dnl and hopefully also by autoconf-2.58 and newer on all other platforms.
    dnl For now however, we still need it to make sure the configure script
    dnl works on OS/2 no matter what platform it is generated on.
    ac_executable_extensions=".exe"
    export ac_executable_extensions
    dnl This strange code is necessary to deal with handling of
    dnl backslashes by ksh and pdksh's sh variant.
    ac_save_IFS="$IFS"
    IFS='\\'
    ac_TEMP_PATH=
    for ac_dir in $PATH; do
      IFS=$ac_save_IFS
      if test -z "$ac_TEMP_PATH"; then
        ac_TEMP_PATH="$ac_dir"
      else
        ac_TEMP_PATH="$ac_TEMP_PATH/$ac_dir"
      fi
    done
    export PATH="$ac_TEMP_PATH"
    unset ac_TEMP_PATH
  ;;

  powerpc-*-darwin* )
    dnl PowerPC Darwin based distributions (including Mac OS X)
    USE_BSD=1
    USE_DARWIN=1
    SO_SUFFIX=dylib
    AC_DEFINE(__BSD__)
    AC_DEFINE(__DARWIN__)
    AC_DEFINE(__POWERPC__)
    AC_DEFINE(TARGET_CARBON)
    DEFAULT_DEFAULT_wxUSE_MAC=1
  ;;

  *-*-beos* )
    dnl leave USE_UNIX on - BeOS is sufficiently Unix-like for this
    USE_BEOS=1
    AC_DEFINE(__BEOS__)
  ;;

  *)
    AC_MSG_ERROR(unknown system type ${host}.)
esac

dnl ---------------------------------------------------------------------------
dnl command line options for configure
dnl ---------------------------------------------------------------------------

dnl the default values for all options - we collect them all here to simplify
dnl modification of the default values (for example, if the defaults for some
dnl platform should be changed, it can be done here too)
dnl
dnl NB: see also DEFAULT_wxUSE<toolkit> variables defined above

WX_ARG_CACHE_INIT

dnl useful to test the compilation with minimum options, define as 0 for normal
dnl usage
DEBUG_CONFIGURE=0
if test $DEBUG_CONFIGURE = 1; then
  DEFAULT_wxUSE_UNIVERSAL=no
  DEFAULT_wxUSE_STL=no

  DEFAULT_wxUSE_NANOX=no

  DEFAULT_wxUSE_THREADS=yes

  DEFAULT_wxUSE_SHARED=yes
  DEFAULT_wxUSE_OPTIMISE=no
  DEFAULT_wxUSE_PROFILE=no
  DEFAULT_wxUSE_NO_DEPS=no
  DEFAULT_wxUSE_NO_RTTI=no
  DEFAULT_wxUSE_NO_EXCEPTIONS=no
  DEFAULT_wxUSE_RPATH=yes
  DEFAULT_wxUSE_PERMISSIVE=no
  DEFAULT_wxUSE_DEBUG_FLAG=yes
  DEFAULT_wxUSE_DEBUG_INFO=yes
  DEFAULT_wxUSE_DEBUG_GDB=yes
  DEFAULT_wxUSE_MEM_TRACING=no
  DEFAULT_wxUSE_DEBUG_CONTEXT=no
  DEFAULT_wxUSE_DMALLOC=no
  DEFAULT_wxUSE_APPLE_IEEE=no

  DEFAULT_wxUSE_EXCEPTIONS=no
  DEFAULT_wxUSE_LOG=yes
  DEFAULT_wxUSE_LOGWINDOW=no
  DEFAULT_wxUSE_LOGGUI=no
  DEFAULT_wxUSE_LOGDIALOG=no

  DEFAULT_wxUSE_GUI=yes
  DEFAULT_wxUSE_CONTROLS=no

  DEFAULT_wxUSE_REGEX=no
  DEFAULT_wxUSE_XML=no
  DEFAULT_wxUSE_EXPAT=no
  DEFAULT_wxUSE_ZLIB=no
  DEFAULT_wxUSE_LIBPNG=no
  DEFAULT_wxUSE_LIBJPEG=no
  DEFAULT_wxUSE_LIBTIFF=no
  DEFAULT_wxUSE_LIBXPM=no
  DEFAULT_wxUSE_LIBMSPACK=no
  DEFAULT_wxUSE_LIBSDL=no
  DEFAULT_wxUSE_LIBGNOMEPRINT=no
  DEFAULT_wxUSE_ODBC=no
  DEFAULT_wxUSE_OPENGL=no

  DEFAULT_wxUSE_ON_FATAL_EXCEPTION=no
  DEFAULT_wxUSE_SNGLINST_CHECKER=no
  DEFAULT_wxUSE_STD_IOSTREAM=no
  DEFAULT_wxUSE_CMDLINE_PARSER=no
  DEFAULT_wxUSE_DATETIME=no
  DEFAULT_wxUSE_TIMER=no
  DEFAULT_wxUSE_STOPWATCH=no
  DEFAULT_wxUSE_FILE=no
  DEFAULT_wxUSE_FFILE=no
  DEFAULT_wxUSE_TEXTBUFFER=no
  DEFAULT_wxUSE_TEXTFILE=no
  DEFAULT_wxUSE_SOUND=no
  DEFAULT_wxUSE_MEDIACTRL=no  
  DEFAULT_wxUSE_DIRECTSHOW=no  
  DEFAULT_wxUSE_INTL=no
  DEFAULT_wxUSE_CONFIG=no
  DEFAULT_wxUSE_FONTMAP=no
  DEFAULT_wxUSE_STREAMS=no
  DEFAULT_wxUSE_SOCKETS=no
  DEFAULT_wxUSE_OLE=no
  DEFAULT_wxUSE_DATAOBJ=no
  DEFAULT_wxUSE_DIALUP_MANAGER=no
  DEFAULT_wxUSE_JOYSTICK=no
  DEFAULT_wxUSE_DYNLIB_CLASS=no
  DEFAULT_wxUSE_DYNAMIC_LOADER=no
  DEFAULT_wxUSE_LONGLONG=no
  DEFAULT_wxUSE_GEOMETRY=no

  DEFAULT_wxUSE_AFM_FOR_POSTSCRIPT=no
  DEFAULT_wxUSE_NORMALIZED_PS_FONTS=no
  DEFAULT_wxUSE_POSTSCRIPT=no

  DEFAULT_wxUSE_CLIPBOARD=no
  DEFAULT_wxUSE_TOOLTIPS=no
  DEFAULT_wxUSE_DRAG_AND_DROP=no
  DEFAULT_wxUSE_DRAGIMAGE=no
  DEFAULT_wxUSE_SPLINES=no

  DEFAULT_wxUSE_MDI=no
  DEFAULT_wxUSE_MDI_ARCHITECTURE=no
  DEFAULT_wxUSE_DOC_VIEW_ARCHITECTURE=no
  DEFAULT_wxUSE_PRINTING_ARCHITECTURE=no

  DEFAULT_wxUSE_RESOURCES=no
  DEFAULT_wxUSE_CONSTRAINTS=no
  DEFAULT_wxUSE_IPC=no
  DEFAULT_wxUSE_HELP=no
  DEFAULT_wxUSE_MS_HTML_HELP=no
  DEFAULT_wxUSE_WXHTML_HELP=no
  DEFAULT_wxUSE_TREELAYOUT=no
  DEFAULT_wxUSE_METAFILE=no
  DEFAULT_wxUSE_MIMETYPE=no
  DEFAULT_wxUSE_SYSTEM_OPTIONS=no
  DEFAULT_wxUSE_PROTOCOL=no
  DEFAULT_wxUSE_PROTOCOL_HTTP=no
  DEFAULT_wxUSE_PROTOCOL_FTP=no
  DEFAULT_wxUSE_PROTOCOL_FILE=no
  DEFAULT_wxUSE_URL=no

  DEFAULT_wxUSE_COMMONDLGS=no
  DEFAULT_wxUSE_CHOICEDLG=no
  DEFAULT_wxUSE_COLOURDLG=no
  DEFAULT_wxUSE_DIRDLG=no
  DEFAULT_wxUSE_FILEDLG=no
  DEFAULT_wxUSE_FINDREPLDLG=no
  DEFAULT_wxUSE_FONTDLG=no
  DEFAULT_wxUSE_MSGDLG=no
  DEFAULT_wxUSE_NUMBERDLG=no
  DEFAULT_wxUSE_TEXTDLG=no
  DEFAULT_wxUSE_SPLASH=no
  DEFAULT_wxUSE_STARTUP_TIPS=no
  DEFAULT_wxUSE_PROGRESSDLG=no
  DEFAULT_wxUSE_WIZARDDLG=no

  DEFAULT_wxUSE_MENUS=no
  DEFAULT_wxUSE_MINIFRAME=no
  DEFAULT_wxUSE_HTML=no
  DEFAULT_wxUSE_XRC=no
  DEFAULT_wxUSE_WEBKIT=no
  DEFAULT_wxUSE_FILESYSTEM=no
  DEFAULT_wxUSE_FS_INET=no
  DEFAULT_wxUSE_FS_ZIP=no
  DEFAULT_wxUSE_BUSYINFO=no
  DEFAULT_wxUSE_ZIPSTREAM=no
  DEFAULT_wxUSE_VALIDATORS=no

  DEFAULT_wxUSE_ACCEL=no
  DEFAULT_wxUSE_BUTTON=no
  DEFAULT_wxUSE_BMPBUTTON=no
  DEFAULT_wxUSE_CALCTRL=no
  DEFAULT_wxUSE_CARET=no
  DEFAULT_wxUSE_CHECKBOX=no
  DEFAULT_wxUSE_CHECKLST=no
  DEFAULT_wxUSE_CHOICE=no
  DEFAULT_wxUSE_CHOICEBOOK=no
  DEFAULT_wxUSE_COMBOBOX=no
  DEFAULT_wxUSE_DISPLAY=no
  DEFAULT_wxUSE_GAUGE=no
  DEFAULT_wxUSE_GRID=no
  DEFAULT_wxUSE_IMAGLIST=no
  DEFAULT_wxUSE_LISTBOOK=no
  DEFAULT_wxUSE_LISTBOX=no
  DEFAULT_wxUSE_LISTCTRL=no
  DEFAULT_wxUSE_NOTEBOOK=no
  DEFAULT_wxUSE_RADIOBOX=no
  DEFAULT_wxUSE_RADIOBTN=no
  DEFAULT_wxUSE_SASH=no
  DEFAULT_wxUSE_SCROLLBAR=no
  DEFAULT_wxUSE_SLIDER=no
  DEFAULT_wxUSE_SPINBTN=no
  DEFAULT_wxUSE_SPINCTRL=no
  DEFAULT_wxUSE_SPLITTER=no
  DEFAULT_wxUSE_STATBMP=no
  DEFAULT_wxUSE_STATBOX=no
  DEFAULT_wxUSE_STATLINE=no
  DEFAULT_wxUSE_STATTEXT=no
  DEFAULT_wxUSE_STATUSBAR=yes
  DEFAULT_wxUSE_TABDIALOG=no
  DEFAULT_wxUSE_TEXTCTRL=no
  DEFAULT_wxUSE_TOGGLEBTN=no
  DEFAULT_wxUSE_TOOLBAR=no
  DEFAULT_wxUSE_TOOLBAR_NATIVE=no
  DEFAULT_wxUSE_TOOLBAR_SIMPLE=no
  DEFAULT_wxUSE_TREECTRL=no
  DEFAULT_wxUSE_POPUPWIN=no
  DEFAULT_wxUSE_TIPWINDOW=no

  DEFAULT_wxUSE_UNICODE=no
  DEFAULT_wxUSE_UNICODE_MSLU=no
  DEFAULT_wxUSE_WCSRTOMBS=no

  DEFAULT_wxUSE_PALETTE=no
  DEFAULT_wxUSE_IMAGE=no
  DEFAULT_wxUSE_GIF=no
  DEFAULT_wxUSE_PCX=no
  DEFAULT_wxUSE_PNM=no
  DEFAULT_wxUSE_IFF=no
  DEFAULT_wxUSE_XPM=no
  DEFAULT_wxUSE_ICO_CUR=no
  DEFAULT_wxUSE_ACCESSIBILITY=no
  
  DEFAULT_wxUSE_MONOLITHIC=no
  DEFAULT_wxUSE_PLUGINS=no
  DEFAULT_wxUSE_OFFICIAL_BUILD=no
else
  DEFAULT_wxUSE_UNIVERSAL=no
  DEFAULT_wxUSE_STL=no

  DEFAULT_wxUSE_NANOX=no

  DEFAULT_wxUSE_THREADS=yes

  DEFAULT_wxUSE_SHARED=yes
  DEFAULT_wxUSE_OPTIMISE=yes
  DEFAULT_wxUSE_PROFILE=no
  DEFAULT_wxUSE_NO_DEPS=no
  DEFAULT_wxUSE_NO_RTTI=no
  DEFAULT_wxUSE_NO_EXCEPTIONS=no
  DEFAULT_wxUSE_RPATH=yes
  DEFAULT_wxUSE_PERMISSIVE=no
  DEFAULT_wxUSE_DEBUG_FLAG=no
  DEFAULT_wxUSE_DEBUG_INFO=no
  DEFAULT_wxUSE_DEBUG_GDB=no
  DEFAULT_wxUSE_MEM_TRACING=no
  DEFAULT_wxUSE_DEBUG_CONTEXT=no
  DEFAULT_wxUSE_DMALLOC=no
  DEFAULT_wxUSE_APPLE_IEEE=yes

  DEFAULT_wxUSE_EXCEPTIONS=yes
  DEFAULT_wxUSE_LOG=yes
  DEFAULT_wxUSE_LOGWINDOW=yes
  DEFAULT_wxUSE_LOGGUI=yes
  DEFAULT_wxUSE_LOGDIALOG=yes

  DEFAULT_wxUSE_GUI=yes

  DEFAULT_wxUSE_REGEX=yes
  DEFAULT_wxUSE_XML=yes
  DEFAULT_wxUSE_EXPAT=yes
  DEFAULT_wxUSE_ZLIB=yes
  DEFAULT_wxUSE_LIBPNG=yes
  DEFAULT_wxUSE_LIBJPEG=yes
  DEFAULT_wxUSE_LIBTIFF=yes
  DEFAULT_wxUSE_LIBXPM=yes
  DEFAULT_wxUSE_LIBMSPACK=yes
  DEFAULT_wxUSE_LIBSDL=no
  DEFAULT_wxUSE_LIBGNOMEPRINT=no
  DEFAULT_wxUSE_ODBC=no
  DEFAULT_wxUSE_OPENGL=no

  DEFAULT_wxUSE_ON_FATAL_EXCEPTION=yes
  DEFAULT_wxUSE_SNGLINST_CHECKER=yes
  DEFAULT_wxUSE_STD_IOSTREAM=no
  DEFAULT_wxUSE_CMDLINE_PARSER=yes
  DEFAULT_wxUSE_DATETIME=yes
  DEFAULT_wxUSE_TIMER=yes
  DEFAULT_wxUSE_STOPWATCH=yes
  DEFAULT_wxUSE_FILE=yes
  DEFAULT_wxUSE_FFILE=yes
  DEFAULT_wxUSE_TEXTBUFFER=yes
  DEFAULT_wxUSE_TEXTFILE=yes
  DEFAULT_wxUSE_SOUND=yes
  DEFAULT_wxUSE_MEDIACTRL=no
  DEFAULT_wxUSE_DIRECTSHOW=no  
  DEFAULT_wxUSE_INTL=yes
  DEFAULT_wxUSE_CONFIG=yes
  DEFAULT_wxUSE_FONTMAP=yes
  DEFAULT_wxUSE_STREAMS=yes
  DEFAULT_wxUSE_SOCKETS=yes
  DEFAULT_wxUSE_OLE=yes
  DEFAULT_wxUSE_DATAOBJ=yes
  DEFAULT_wxUSE_DIALUP_MANAGER=yes
  DEFAULT_wxUSE_JOYSTICK=yes
  DEFAULT_wxUSE_DYNLIB_CLASS=yes
  DEFAULT_wxUSE_DYNAMIC_LOADER=yes
  DEFAULT_wxUSE_LONGLONG=yes
  DEFAULT_wxUSE_GEOMETRY=yes

  DEFAULT_wxUSE_AFM_FOR_POSTSCRIPT=yes
  DEFAULT_wxUSE_NORMALIZED_PS_FONTS=yes
  DEFAULT_wxUSE_POSTSCRIPT=yes

  DEFAULT_wxUSE_CLIPBOARD=yes
  DEFAULT_wxUSE_TOOLTIPS=yes
  DEFAULT_wxUSE_DRAG_AND_DROP=yes
  DEFAULT_wxUSE_DRAGIMAGE=yes
  DEFAULT_wxUSE_SPLINES=yes

  DEFAULT_wxUSE_MDI=yes
  DEFAULT_wxUSE_MDI_ARCHITECTURE=yes
  DEFAULT_wxUSE_DOC_VIEW_ARCHITECTURE=yes
  DEFAULT_wxUSE_PRINTING_ARCHITECTURE=yes

  DEFAULT_wxUSE_RESOURCES=no
  DEFAULT_wxUSE_CONSTRAINTS=yes
  DEFAULT_wxUSE_IPC=yes
  DEFAULT_wxUSE_HELP=yes
  DEFAULT_wxUSE_MS_HTML_HELP=yes
  DEFAULT_wxUSE_WXHTML_HELP=yes
  DEFAULT_wxUSE_TREELAYOUT=yes
  DEFAULT_wxUSE_METAFILE=yes
  DEFAULT_wxUSE_MIMETYPE=yes
  DEFAULT_wxUSE_SYSTEM_OPTIONS=yes
  DEFAULT_wxUSE_PROTOCOL=yes
  DEFAULT_wxUSE_PROTOCOL_HTTP=yes
  DEFAULT_wxUSE_PROTOCOL_FTP=yes
  DEFAULT_wxUSE_PROTOCOL_FILE=yes
  DEFAULT_wxUSE_URL=yes

  DEFAULT_wxUSE_COMMONDLGS=yes
  DEFAULT_wxUSE_CHOICEDLG=yes
  DEFAULT_wxUSE_COLOURDLG=yes
  DEFAULT_wxUSE_DIRDLG=yes
  DEFAULT_wxUSE_FILEDLG=yes
  DEFAULT_wxUSE_FINDREPLDLG=yes
  DEFAULT_wxUSE_FONTDLG=yes
  DEFAULT_wxUSE_MSGDLG=yes
  DEFAULT_wxUSE_NUMBERDLG=yes
  DEFAULT_wxUSE_TEXTDLG=yes
  DEFAULT_wxUSE_SPLASH=yes
  DEFAULT_wxUSE_STARTUP_TIPS=yes
  DEFAULT_wxUSE_PROGRESSDLG=yes
  DEFAULT_wxUSE_WIZARDDLG=yes

  DEFAULT_wxUSE_MENUS=yes
  DEFAULT_wxUSE_MINIFRAME=yes
  DEFAULT_wxUSE_HTML=yes
  DEFAULT_wxUSE_XRC=yes
  DEFAULT_wxUSE_WEBKIT=yes
  DEFAULT_wxUSE_FILESYSTEM=yes
  DEFAULT_wxUSE_FS_INET=yes
  DEFAULT_wxUSE_FS_ZIP=yes
  DEFAULT_wxUSE_BUSYINFO=yes
  DEFAULT_wxUSE_ZIPSTREAM=yes
  DEFAULT_wxUSE_VALIDATORS=yes

  DEFAULT_wxUSE_ACCEL=yes
  DEFAULT_wxUSE_BUTTON=yes
  DEFAULT_wxUSE_BMPBUTTON=yes
  DEFAULT_wxUSE_CALCTRL=yes
  DEFAULT_wxUSE_CARET=yes
  DEFAULT_wxUSE_CHECKBOX=yes
  DEFAULT_wxUSE_CHECKLST=yes
  DEFAULT_wxUSE_CHOICE=yes
  DEFAULT_wxUSE_CHOICEBOOK=yes
  DEFAULT_wxUSE_COMBOBOX=yes
  DEFAULT_wxUSE_DISPLAY=yes
  DEFAULT_wxUSE_GAUGE=yes
  DEFAULT_wxUSE_GRID=yes
  DEFAULT_wxUSE_IMAGLIST=yes
  DEFAULT_wxUSE_LISTBOOK=yes
  DEFAULT_wxUSE_LISTBOX=yes
  DEFAULT_wxUSE_LISTCTRL=yes
  DEFAULT_wxUSE_NOTEBOOK=yes
  DEFAULT_wxUSE_RADIOBOX=yes
  DEFAULT_wxUSE_RADIOBTN=yes
  DEFAULT_wxUSE_SASH=yes
  DEFAULT_wxUSE_SCROLLBAR=yes
  DEFAULT_wxUSE_SLIDER=yes
  DEFAULT_wxUSE_SPINBTN=yes
  DEFAULT_wxUSE_SPINCTRL=yes
  DEFAULT_wxUSE_SPLITTER=yes
  DEFAULT_wxUSE_STATBMP=yes
  DEFAULT_wxUSE_STATBOX=yes
  DEFAULT_wxUSE_STATLINE=yes
  DEFAULT_wxUSE_STATTEXT=yes
  DEFAULT_wxUSE_STATUSBAR=yes
  DEFAULT_wxUSE_TABDIALOG=no
  DEFAULT_wxUSE_TEXTCTRL=yes
  DEFAULT_wxUSE_TOGGLEBTN=yes
  DEFAULT_wxUSE_TOOLBAR=yes
  DEFAULT_wxUSE_TOOLBAR_NATIVE=yes
  DEFAULT_wxUSE_TOOLBAR_SIMPLE=yes
  DEFAULT_wxUSE_TREECTRL=yes
  DEFAULT_wxUSE_POPUPWIN=yes
  DEFAULT_wxUSE_TIPWINDOW=yes

  DEFAULT_wxUSE_UNICODE=no
  DEFAULT_wxUSE_UNICODE_MSLU=yes
  DEFAULT_wxUSE_WCSRTOMBS=no

  DEFAULT_wxUSE_PALETTE=yes
  DEFAULT_wxUSE_IMAGE=yes
  DEFAULT_wxUSE_GIF=yes
  DEFAULT_wxUSE_PCX=yes
  DEFAULT_wxUSE_IFF=no
  DEFAULT_wxUSE_PNM=yes
  DEFAULT_wxUSE_XPM=yes
  DEFAULT_wxUSE_ICO_CUR=yes
  DEFAULT_wxUSE_ACCESSIBILITY=no
  
  DEFAULT_wxUSE_MONOLITHIC=no
  DEFAULT_wxUSE_PLUGINS=no
  DEFAULT_wxUSE_OFFICIAL_BUILD=no

  dnl Appliable only when --with-gtk was used:
  DEFAULT_wxUSE_GTK2=yes
fi

dnl WX_ARG_WITH should be used to select whether an external package will be
dnl used or not, to configure compile-time features of this package itself,
dnl use WX_ARG_ENABLE instead

dnl ============================
dnl external package dependecies
dnl ============================

dnl these options use AC_ARG_WITH and not WX_ARG_WITH on purpose - we cache
dnl these values manually
for toolkit in `echo $ALL_TOOLKITS`; do
  LINE=`grep "wxUSE_$toolkit=" ${wx_arg_cache_file}`
  if test "x$LINE" != "x" ; then
    has_toolkit_in_cache=1
    eval "DEFAULT_$LINE"
    eval "CACHE_$toolkit=1"
  fi
done

dnl ---------------------------------------------------------------------------
dnl --disable-gui will build only non-GUI part of wxWidgets: check for this
dnl first to disable many other switches if it's given
dnl
dnl NB: this is still in testing stage, don't use if you don't know what you're
dnl     doing
dnl ---------------------------------------------------------------------------

WX_ARG_ENABLE(gui,         [  --enable-gui            use GUI classes], wxUSE_GUI)
WX_ARG_ENABLE(monolithic,  [  --enable-monolithic     build wxWidgets as single library], wxUSE_MONOLITHIC)
WX_ARG_ENABLE(plugins,     [  --enable-plugins        build parts of wxWidgets as loadable components], wxUSE_PLUGINS)

if test "$wxUSE_GUI" = "yes"; then

WX_ARG_ENABLE(universal,   [  --enable-universal      use wxWidgets GUI controls instead of native ones], wxUSE_UNIVERSAL)
AC_ARG_WITH(gtk,           [  --with-gtk              use GTK+], [wxUSE_GTK="$withval" CACHE_GTK=1 TOOLKIT_GIVEN=1])
AC_ARG_WITH(motif,         [  --with-motif            use Motif/Lesstif], [wxUSE_MOTIF="$withval" CACHE_MOTIF=1 TOOLKIT_GIVEN=1])
AC_ARG_WITH(mac,           [  --with-mac              use Mac OS X], [wxUSE_MAC="$withval" CACHE_MAC=1 TOOLKIT_GIVEN=1])
AC_ARG_WITH(cocoa,         [  --with-cocoa            use Cocoa], [wxUSE_COCOA="$withval" CACHE_COCOA=1 TOOLKIT_GIVEN=1])
AC_ARG_WITH(wine,          [  --with-wine             use Wine], [wxUSE_WINE="$withval" CACHE_WINE=1])
AC_ARG_WITH(msw,           [  --with-msw              use MS-Windows], [wxUSE_MSW="$withval" CACHE_MSW=1 TOOLKIT_GIVEN=1])
AC_ARG_WITH(pm,            [  --with-pm               use OS/2 Presentation Manager], [wxUSE_PM="$withval" CACHE_PM=1 TOOLKIT_GIVEN=1])
AC_ARG_WITH(mgl,           [  --with-mgl              use SciTech MGL], [wxUSE_MGL="$withval" wxUSE_UNIVERSAL="yes" CACHE_MGL=1 TOOLKIT_GIVEN=1])
AC_ARG_WITH(microwin,      [  --with-microwin         use MicroWindows], [wxUSE_MICROWIN="$withval" CACHE_MICROWIN=1 TOOLKIT_GIVEN=1])
AC_ARG_WITH(x11,           [  --with-x11              use X11], [wxUSE_X11="$withval" wxUSE_UNIVERSAL="yes" CACHE_X11=1 TOOLKIT_GIVEN=1])
WX_ARG_ENABLE(nanox,       [  --enable-nanox          use NanoX], wxUSE_NANOX)

WX_ARG_ENABLE(gtk2,        [  --disable-gtk2          use GTK+ 1.2 instead of 2.0], wxUSE_GTK2)
WX_ARG_ENABLE(gpe,         [  --enable-gpe            use GNOME PDA Environment features if possible], wxUSE_GPE)

WX_ARG_SYS_WITH(libpng,    [  --with-libpng           use libpng (PNG image format)], wxUSE_LIBPNG)
WX_ARG_SYS_WITH(libjpeg,   [  --with-libjpeg          use libjpeg (JPEG file format)], wxUSE_LIBJPEG)
WX_ARG_SYS_WITH(libtiff,   [  --with-libtiff          use libtiff (TIFF file format)], wxUSE_LIBTIFF)
WX_ARG_SYS_WITH(libxpm,    [  --with-libxpm           use libxpm (XPM file format)], wxUSE_LIBXPM)
WX_ARG_WITH(libmspack,     [  --with-libmspack        use libmspack (CHM help files loading)], wxUSE_LIBMSPACK)
WX_ARG_WITH(sdl,           [  --with-sdl              use SDL for audio on Unix], wxUSE_LIBSDL)
WX_ARG_WITH(gnomeprint,    [  --with-gnomeprint       use GNOME print for printing under Unix], wxUSE_LIBGNOMEPRINT)
WX_ARG_WITH(opengl,        [  --with-opengl           use OpenGL (or Mesa)], wxUSE_OPENGL)

fi
dnl for GUI only

WX_ARG_WITH(dmalloc,       [  --with-dmalloc          use dmalloc library (http://dmalloc.com/)], wxUSE_DMALLOC)
WX_ARG_SYS_WITH(regex,     [  --with-regex            enable support for wxRegEx class], wxUSE_REGEX)
WX_ARG_SYS_WITH(zlib,      [  --with-zlib             use zlib for LZW compression], wxUSE_ZLIB)
WX_ARG_SYS_WITH(odbc,      [  --with-odbc             use the IODBC and wxODBC classes], wxUSE_ODBC)
WX_ARG_SYS_WITH(expat,     [  --with-expat            enable XML support using expat parser], wxUSE_EXPAT)

dnl ---------------------------------------------------------------------------
dnl compile options
dnl ---------------------------------------------------------------------------

WX_ARG_ENABLE(shared,      [  --enable-shared         create shared library code], wxUSE_SHARED)
WX_ARG_ENABLE(optimise,    [  --enable-optimise       create optimised code], wxUSE_OPTIMISE)
WX_ARG_ENABLE(debug,       [  --enable-debug          same as debug_flag and debug_info], wxUSE_DEBUG)
WX_ARG_ENABLE(stl,         [  --enable-stl            use STL for containers], wxUSE_STL)
if test "$USE_OS2" = "1"; then
    WX_ARG_ENABLE(omf,     [  --enable-omf            use OMF object format], wxUSE_OMF)
fi

if test "$wxUSE_DEBUG" = "yes"; then
  DEFAULT_wxUSE_DEBUG_FLAG=yes
  DEFAULT_wxUSE_DEBUG_INFO=yes
  BUILD=debug
elif test "$wxUSE_DEBUG" = "no"; then
  DEFAULT_wxUSE_DEBUG_FLAG=no
  DEFAULT_wxUSE_DEBUG_INFO=no
  BUILD=release
fi

WX_ARG_ENABLE(debug_flag,    [  --enable-debug_flag     set __WXDEBUG__ flag (recommended for developers!)], wxUSE_DEBUG_FLAG)
WX_ARG_ENABLE(debug_info,    [  --enable-debug_info     create code with debugging information], wxUSE_DEBUG_INFO)
WX_ARG_ENABLE(debug_gdb,     [  --enable-debug_gdb      create code with extra GDB debugging information], wxUSE_DEBUG_GDB)
WX_ARG_ENABLE(debug_cntxt,   [  --enable-debug_cntxt    use wxDebugContext], wxUSE_DEBUG_CONTEXT)
WX_ARG_ENABLE(mem_tracing,   [  --enable-mem_tracing    create code with memory tracing], wxUSE_MEM_TRACING)
WX_ARG_ENABLE(profile,       [  --enable-profile        create code with profiling information], wxUSE_PROFILE)
WX_ARG_ENABLE(no_rtti,       [  --enable-no_rtti        create code without RTTI information], wxUSE_NO_RTTI)
WX_ARG_ENABLE(no_exceptions, [  --enable-no_exceptions  create code without C++ exceptions handling], wxUSE_NO_EXCEPTIONS)
WX_ARG_ENABLE(permissive,    [  --enable-permissive     compile code disregarding strict ANSI], wxUSE_PERMISSIVE)
WX_ARG_ENABLE(no_deps,       [  --enable-no_deps        create code without dependency information], wxUSE_NO_DEPS)

WX_ARG_ENABLE(compat22,      [  --enable-compat22       enable wxWidgets 2.2 compatibility], WXWIN_COMPATIBILITY_2_2)
WX_ARG_ENABLE(compat24,      [  --disable-compat24      disable wxWidgets 2.4 compatibility], WXWIN_COMPATIBILITY_2_4, disable)

WX_ARG_ENABLE(rpath,         [  --disable-rpath         disable use of rpath for uninstalled builds], wxUSE_RPATH)


dnl ---------------------------------------------------------------------------
dnl (small) optional non GUI classes
dnl ---------------------------------------------------------------------------

WX_ARG_ENABLE(intl,          [  --enable-intl           use internationalization system], wxUSE_INTL)
WX_ARG_ENABLE(config,        [  --enable-config         use wxConfig (and derived) classes], wxUSE_CONFIG)

WX_ARG_ENABLE(protocols,     [  --enable-protocols      use wxProtocol and derived classes], wxUSE_PROTOCOL)
WX_ARG_ENABLE(ftp,           [  --enable-ftp            use wxFTP (requires wxProtocol], wxUSE_PROTOCOL_FTP)
WX_ARG_ENABLE(http,          [  --enable-http           use wxHTTP (requires wxProtocol], wxUSE_PROTOCOL_HTTP)
WX_ARG_ENABLE(fileproto,     [  --enable-fileproto      use wxFileProto class (requires wxProtocol], wxUSE_PROTOCOL_FILE)
WX_ARG_ENABLE(sockets,       [  --enable-sockets        use socket/network classes], wxUSE_SOCKETS)
WX_ARG_ENABLE(ole,           [  --enable-ole            use OLE classes (Win32 only)], wxUSE_OLE)
WX_ARG_ENABLE(dataobj,       [  --enable-dataobj        use data object classes], wxUSE_DATAOBJ)

WX_ARG_ENABLE(ipc,           [  --enable-ipc            use interprocess communication (wxSocket etc.)], wxUSE_IPC)

dnl please keep the settings below in alphabetical order
WX_ARG_ENABLE(apple_ieee,    [  --enable-apple_ieee     use the Apple IEEE codec], wxUSE_APPLE_IEEE)
WX_ARG_ENABLE(catch_segvs,   [  --enable-catch_segvs    catch signals in wxApp::OnFatalException (Unix only)], wxUSE_ON_FATAL_EXCEPTION)
WX_ARG_ENABLE(cmdline,       [  --enable-cmdline        use wxCmdLineParser class], wxUSE_CMDLINE_PARSER)
WX_ARG_ENABLE(datetime,      [  --enable-datetime       use wxDateTime class], wxUSE_DATETIME)
WX_ARG_ENABLE(dialupman,     [  --enable-dialupman      use dialup network classes], wxUSE_DIALUP_MANAGER)
WX_ARG_ENABLE(dynlib,        [  --enable-dynlib         use wxLibrary class for DLL loading], wxUSE_DYNLIB_CLASS)
WX_ARG_ENABLE(dynamicloader, [  --enable-dynamicloader  use (new) wxDynamicLibrary class], wxUSE_DYNAMIC_LOADER)
WX_ARG_ENABLE(exceptions,    [  --enable-exceptions     build exception-safe library], wxUSE_EXCEPTIONS)
WX_ARG_ENABLE(ffile,         [  --enable-ffile          use wxFFile class], wxUSE_FFILE)
WX_ARG_ENABLE(file,          [  --enable-file           use wxFile class], wxUSE_FILE)
WX_ARG_ENABLE(filesystem,    [  --enable-filesystem     use virtual file systems classes], wxUSE_FILESYSTEM)
WX_ARG_ENABLE(fontmap,       [  --enable-fontmap        use font encodings conversion classes], wxUSE_FONTMAP)
WX_ARG_ENABLE(fs_inet,       [  --enable-fs_inet        use virtual HTTP/FTP filesystems], wxUSE_FS_INET)
WX_ARG_ENABLE(fs_zip,        [  --enable-fs_zip         use virtual ZIP filesystems], wxUSE_FS_ZIP)
WX_ARG_ENABLE(geometry,      [  --enable-geometry       use geometry class], wxUSE_GEOMETRY)
WX_ARG_ENABLE(log,           [  --enable-log            use logging system], wxUSE_LOG)
WX_ARG_ENABLE(longlong,      [  --enable-longlong       use wxLongLong class], wxUSE_LONGLONG)
WX_ARG_ENABLE(mimetype,      [  --enable-mimetype       use wxMimeTypesManager], wxUSE_MIMETYPE)
WX_ARG_ENABLE(mslu,          [  --enable-mslu           use MS Layer for Unicode on Windows 9x (Win32 only)], wxUSE_UNICODE_MSLU)
WX_ARG_ENABLE(snglinst,      [  --enable-snglinst       use wxSingleInstanceChecker class], wxUSE_SNGLINST_CHECKER)
WX_ARG_ENABLE(streams,       [  --enable-streams        use wxStream etc classes], wxUSE_STREAMS)
WX_ARG_ENABLE(std_iostreams, [  --enable-std_iostreams  use standard C++ stream classes], wxUSE_STD_IOSTREAM)
WX_ARG_ENABLE(stopwatch,     [  --enable-stopwatch      use wxStopWatch class], wxUSE_STOPWATCH)
WX_ARG_ENABLE(system_options,[  --enable-sysoptions     use wxSystemOptions], wxUSE_SYSTEM_OPTIONS)
WX_ARG_ENABLE(textbuf,       [  --enable-textbuf        use wxTextBuffer class], wxUSE_TEXTBUFFER)
WX_ARG_ENABLE(textfile,      [  --enable-textfile       use wxTextFile class], wxUSE_TEXTFILE)
WX_ARG_ENABLE(timer,         [  --enable-timer          use wxTimer class], wxUSE_TIMER)
WX_ARG_ENABLE(unicode,       [  --enable-unicode        compile wxString with Unicode support], wxUSE_UNICODE)
WX_ARG_ENABLE(sound,         [  --enable-sound          use wxSound class], wxUSE_SOUND)
WX_ARG_ENABLE(mediactrl,     [  --enable-mediactrl      use wxMediaCtrl class], wxUSE_MEDIACTRL)
WX_ARG_ENABLE(wxprintfv,     [  --enable-wxprintfv      use wxWidgets implementation of vprintf()], wxUSE_EXPERIMENTAL_PRINTF)
WX_ARG_ENABLE(zipstream,     [  --enable-zipstream      use wxZipInputStream], wxUSE_ZIPSTREAM)

WX_ARG_ENABLE(url,           [  --enable-url            use wxURL class], wxUSE_URL)
WX_ARG_ENABLE(protocol,      [  --enable-protocol       use wxProtocol class], wxUSE_PROTOCOL)
WX_ARG_ENABLE(protocol_http, [  --enable-protocol-http  HTTP support in wxProtocol], wxUSE_PROTOCOL_HTTP)
WX_ARG_ENABLE(protocol_ftp,  [  --enable-protocol-ftp   FTP support in wxProtocol], wxUSE_PROTOCOL_FTP)
WX_ARG_ENABLE(protocol_file, [  --enable-protocol-file  FILE support in wxProtocol], wxUSE_PROTOCOL_FILE)


dnl ---------------------------------------------------------------------------
dnl "big" options (i.e. those which change a lot of things throughout the library)
dnl ---------------------------------------------------------------------------

WX_ARG_ENABLE(threads,     [  --enable-threads        use threads], wxUSE_THREADS)

if test "$wxUSE_GUI" = "yes"; then

dnl ---------------------------------------------------------------------------
dnl "big" GUI options
dnl ---------------------------------------------------------------------------

WX_ARG_ENABLE(docview,     [  --enable-docview        use document view architecture], wxUSE_DOC_VIEW_ARCHITECTURE)
WX_ARG_ENABLE(help,        [  --enable-help           use help subsystem], wxUSE_HELP)
WX_ARG_ENABLE(mshtmlhelp,  [  --enable-mshtmlhelp     use MS HTML Help (win32)], wxUSE_MS_HTML_HELP)
WX_ARG_ENABLE(html,        [  --enable-html           use wxHTML sub-library], wxUSE_HTML)
WX_ARG_ENABLE(htmlhelp,    [  --enable-htmlhelp       use wxHTML-based help], wxUSE_WXHTML_HELP)
WX_ARG_ENABLE(xrc,         [  --enable-xrc            use XRC resources sub-library], wxUSE_XRC)
WX_ARG_ENABLE(constraints, [  --enable-constraints    use layout-constraints system], wxUSE_CONSTRAINTS)
WX_ARG_ENABLE(printarch,   [  --enable-printarch      use printing architecture], wxUSE_PRINTING_ARCHITECTURE)
WX_ARG_ENABLE(mdi,         [  --enable-mdi            use multiple document interface architecture], wxUSE_MDI)
WX_ARG_ENABLE(mdidoc,      [  --enable-mdidoc         use docview architecture with MDI], wxUSE_MDI_ARCHITECTURE)
WX_ARG_ENABLE(loggui,      [  --enable-loggui         use standard GUI logger], wxUSE_LOGGUI)
WX_ARG_ENABLE(logwin,      [  --enable-logwin         use wxLogWindow], wxUSE_LOGWINDOW)
WX_ARG_ENABLE(logdialog,   [  --enable-logdialog      use wxLogDialog], wxUSE_LOGDIALOG)
WX_ARG_ENABLE(webkit,      [  --enable-webkit         use wxWebKitCtrl (Mac)], wxUSE_WEBKIT)

dnl ---------------------------------------------------------------------------
dnl PostScript options
dnl ---------------------------------------------------------------------------
WX_ARG_ENABLE(postscript,  [  --enable-postscript     use wxPostscriptDC device context (default for gtk+)], wxUSE_POSTSCRIPT)

dnl VZ: these options seem to be always on, if someone wants to change it please do
dnl WX_ARG_ENABLE(PS-normalized,    [  --enable-PS-normalized  use normalized PS fonts], dnl             wxUSE_NORMALIZED_PS_FONTS)
dnl WX_ARG_ENABLE(afmfonts,        [  --enable-afmfonts       use Adobe Font Metric Font table], dnl             wxUSE_AFM_FOR_POSTSCRIPT)

dnl ---------------------------------------------------------------------------
dnl resources
dnl ---------------------------------------------------------------------------

WX_ARG_ENABLE(prologio,    [  --enable-prologio       not available; see contrib], wxUSE_PROLOGIO)
WX_ARG_ENABLE(resources,   [  --enable-resources      not available; see contrib], wxUSE_RESOURCES)

dnl ---------------------------------------------------------------------------
dnl IPC &c
dnl ---------------------------------------------------------------------------

WX_ARG_ENABLE(clipboard,   [  --enable-clipboard      use wxClipboard class], wxUSE_CLIPBOARD)
WX_ARG_ENABLE(dnd,         [  --enable-dnd            use Drag'n'Drop classes], wxUSE_DRAG_AND_DROP)
WX_ARG_ENABLE(metafile,    [  --enable-metafile       use win32 metafiles], wxUSE_METAFILE)

dnl WX_ARG_ENABLE(treelayout,  [  --enable-treelayout     use wxTreeLayout class], wxUSE_TREELAYOUT)

dnl ---------------------------------------------------------------------------
dnl optional GUI controls (in alphabetical order except the first one)
dnl ---------------------------------------------------------------------------

WX_ARG_ENABLE(controls,    [  --enable-controls       use all usual controls], wxUSE_CONTROLS)

dnl even with --enable-controls, some may be disabled by giving
dnl --disable-<control> later on the command line - but by default all will be
dnl used (and vice versa)
if test "$wxUSE_CONTROLS" = "yes"; then
  DEFAULT_wxUSE_ACCEL=yes
  DEFAULT_wxUSE_BMPBUTTON=yes
  DEFAULT_wxUSE_BUTTON=yes
  DEFAULT_wxUSE_CALCTRL=no
  DEFAULT_wxUSE_CARET=yes
  DEFAULT_wxUSE_COMBOBOX=yes
  DEFAULT_wxUSE_CHECKBOX=yes
  DEFAULT_wxUSE_CHECKLISTBOX=yes
  DEFAULT_wxUSE_CHOICE=yes
  DEFAULT_wxUSE_CHOICEBOOK=yes
  DEFAULT_wxUSE_GAUGE=yes
  DEFAULT_wxUSE_GRID=yes
  DEFAULT_wxUSE_IMAGLIST=yes
  DEFAULT_wxUSE_LISTBOOK=yes
  DEFAULT_wxUSE_LISTBOX=yes
  DEFAULT_wxUSE_LISTCTRL=yes
  DEFAULT_wxUSE_NOTEBOOK=yes
  DEFAULT_wxUSE_RADIOBOX=yes
  DEFAULT_wxUSE_RADIOBTN=yes
  DEFAULT_wxUSE_SASH=yes
  DEFAULT_wxUSE_SCROLLBAR=yes
  DEFAULT_wxUSE_SLIDER=yes
  DEFAULT_wxUSE_SPINBTN=yes
  DEFAULT_wxUSE_SPINCTRL=yes
  DEFAULT_wxUSE_SPLITTER=yes
  DEFAULT_wxUSE_STATBMP=yes
  DEFAULT_wxUSE_STATBOX=yes
  DEFAULT_wxUSE_STATLINE=yes
  DEFAULT_wxUSE_STATUSBAR=yes
  DEFAULT_wxUSE_TAB_DIALOG=yes
  DEFAULT_wxUSE_TOGGLEBTN=yes
  DEFAULT_wxUSE_TOOLBAR=yes
  DEFAULT_wxUSE_TOOLBAR_NATIVE=yes
  DEFAULT_wxUSE_TOOLBAR_SIMPLE=yes
  DEFAULT_wxUSE_TOOLTIPS=yes
  DEFAULT_wxUSE_TREECTRL=yes
  DEFAULT_wxUSE_POPUPWIN=yes
  DEFAULT_wxUSE_TIPWINDOW=yes
elif test "$wxUSE_CONTROLS" = "no"; then
  DEFAULT_wxUSE_ACCEL=no
  DEFAULT_wxUSE_BMPBUTTON=no
  DEFAULT_wxUSE_BUTTON=no
  DEFAULT_wxUSE_CALCTRL=no
  DEFAULT_wxUSE_CARET=no
  DEFAULT_wxUSE_COMBOBOX=no
  DEFAULT_wxUSE_CHECKBOX=no
  DEFAULT_wxUSE_CHECKLISTBOX=no
  DEFAULT_wxUSE_CHOICE=no
  DEFAULT_wxUSE_CHOICEBOOK=no
  DEFAULT_wxUSE_GAUGE=no
  DEFAULT_wxUSE_GRID=no
  DEFAULT_wxUSE_IMAGLIST=no
  DEFAULT_wxUSE_LISTBOOK=no
  DEFAULT_wxUSE_LISTBOX=no
  DEFAULT_wxUSE_LISTCTRL=no
  DEFAULT_wxUSE_NOTEBOOK=no
  DEFAULT_wxUSE_RADIOBOX=no
  DEFAULT_wxUSE_RADIOBTN=no
  DEFAULT_wxUSE_SASH=no
  DEFAULT_wxUSE_SCROLLBAR=no
  DEFAULT_wxUSE_SLIDER=no
  DEFAULT_wxUSE_SPINBTN=no
  DEFAULT_wxUSE_SPINCTRL=no
  DEFAULT_wxUSE_SPLITTER=no
  DEFAULT_wxUSE_STATBMP=no
  DEFAULT_wxUSE_STATBOX=no
  DEFAULT_wxUSE_STATLINE=no
  DEFAULT_wxUSE_STATUSBAR=no
  DEFAULT_wxUSE_TAB_DIALOG=no
  DEFAULT_wxUSE_TOGGLEBTN=no
  DEFAULT_wxUSE_TOOLBAR=no
  DEFAULT_wxUSE_TOOLBAR_NATIVE=no
  DEFAULT_wxUSE_TOOLBAR_SIMPLE=no
  DEFAULT_wxUSE_TOOLTIPS=no
  DEFAULT_wxUSE_TREECTRL=no
  DEFAULT_wxUSE_POPUPWIN=no
  DEFAULT_wxUSE_TIPWINDOW=no
fi

WX_ARG_ENABLE(accel,       [  --enable-accel          use accelerators], wxUSE_ACCEL)
WX_ARG_ENABLE(button,      [  --enable-button         use wxButton class], wxUSE_BUTTON)
WX_ARG_ENABLE(bmpbutton,   [  --enable-bmpbutton      use wxBitmapButton class], wxUSE_BMPBUTTON)
WX_ARG_ENABLE(calendar,    [  --enable-calendar       use wxCalendarCtrl class], wxUSE_CALCTRL)
WX_ARG_ENABLE(caret,       [  --enable-caret          use wxCaret class], wxUSE_CARET)
WX_ARG_ENABLE(checkbox,    [  --enable-checkbox       use wxCheckBox class], wxUSE_CHECKBOX)
WX_ARG_ENABLE(checklst,    [  --enable-checklst       use wxCheckListBox (listbox with checkboxes) class], wxUSE_CHECKLST)
WX_ARG_ENABLE(choice,      [  --enable-choice         use wxChoice class], wxUSE_CHOICE)
WX_ARG_ENABLE(choicebook,  [  --enable-choicebook     use wxChoicebook class], wxUSE_CHOICEBOOK)
WX_ARG_ENABLE(combobox,    [  --enable-combobox       use wxComboBox class], wxUSE_COMBOBOX)
WX_ARG_ENABLE(display,     [  --enable-display        use wxDisplay class], wxUSE_DISPLAY)
WX_ARG_ENABLE(gauge,       [  --enable-gauge          use wxGauge class], wxUSE_GAUGE)
WX_ARG_ENABLE(grid,        [  --enable-grid           use wxGrid class], wxUSE_GRID)
WX_ARG_ENABLE(imaglist,    [  --enable-imaglist       use wxImageList class], wxUSE_IMAGLIST)
WX_ARG_ENABLE(listbook,    [  --enable-listbook       use wxListbook class], wxUSE_LISTBOOK)
WX_ARG_ENABLE(listbox,     [  --enable-listbox        use wxListBox class], wxUSE_LISTBOX)
WX_ARG_ENABLE(listctrl,    [  --enable-listctrl       use wxListCtrl class], wxUSE_LISTCTRL)
WX_ARG_ENABLE(notebook,    [  --enable-notebook       use wxNotebook class], wxUSE_NOTEBOOK)
WX_ARG_ENABLE(radiobox,    [  --enable-radiobox       use wxRadioBox class], wxUSE_RADIOBOX)
WX_ARG_ENABLE(radiobtn,    [  --enable-radiobtn       use wxRadioButton class], wxUSE_RADIOBTN)
WX_ARG_ENABLE(sash,        [  --enable-sash           use wxSashWindow class], wxUSE_SASH)
WX_ARG_ENABLE(scrollbar,   [  --enable-scrollbar      use wxScrollBar class and scrollable windows], wxUSE_SCROLLBAR)
WX_ARG_ENABLE(slider,      [  --enable-slider         use wxSlider class], wxUSE_SLIDER)
WX_ARG_ENABLE(spinbtn,     [  --enable-spinbtn        use wxSpinButton class], wxUSE_SPINBTN)
WX_ARG_ENABLE(spinctrl,    [  --enable-spinctrl       use wxSpinCtrl class], wxUSE_SPINCTRL)
WX_ARG_ENABLE(splitter,    [  --enable-splitter       use wxSplitterWindow class], wxUSE_SPLITTER)
WX_ARG_ENABLE(statbmp,     [  --enable-statbmp        use wxStaticBitmap class], wxUSE_STATBMP)
WX_ARG_ENABLE(statbox,     [  --enable-statbox        use wxStaticBox class], wxUSE_STATBOX)
WX_ARG_ENABLE(statline,    [  --enable-statline       use wxStaticLine class], wxUSE_STATLINE)
WX_ARG_ENABLE(stattext,    [  --enable-stattext       use wxStaticText class], wxUSE_STATTEXT)
WX_ARG_ENABLE(statusbar,   [  --enable-statusbar      use wxStatusBar class], wxUSE_STATUSBAR)
WX_ARG_ENABLE(tabdialog,   [  --enable-tabdialog      use wxTabControl class], wxUSE_TABDIALOG)
WX_ARG_ENABLE(textctrl,    [  --enable-textctrl       use wxTextCtrl class], wxUSE_TEXTCTRL)
WX_ARG_ENABLE(togglebtn,   [  --enable-togglebtn      use wxToggleButton class], wxUSE_TOGGLEBTN)
WX_ARG_ENABLE(toolbar,     [  --enable-toolbar        use wxToolBar class], wxUSE_TOOLBAR)
WX_ARG_ENABLE(tbarnative,  [  --enable-tbarnative     use native wxToolBar class], wxUSE_TOOLBAR_NATIVE)
WX_ARG_ENABLE(tbarsmpl,    [  --enable-tbarsmpl       use wxToolBarSimple class], wxUSE_TOOLBAR_SIMPLE)
WX_ARG_ENABLE(treectrl,    [  --enable-treectrl       use wxTreeCtrl class], wxUSE_TREECTRL)
WX_ARG_ENABLE(tipwindow,   [  --enable-tipwindow      use wxTipWindow class], wxUSE_TIPWINDOW)
WX_ARG_ENABLE(popupwin,    [  --enable-popupwin       use wxPopUpWindow class], wxUSE_POPUPWIN)

dnl ---------------------------------------------------------------------------
dnl common dialogs
dnl ---------------------------------------------------------------------------

WX_ARG_ENABLE(commondlg,   [  --enable-commondlg      use all common dialogs], wxUSE_COMMONDLGS)
WX_ARG_ENABLE(choicedlg,   [  --enable-choicedlg      use wxChoiceDialog], wxUSE_CHOICEDLG)
WX_ARG_ENABLE(coldlg,      [  --enable-coldlg         use wxColourDialog], wxUSE_COLOURDLG)
WX_ARG_ENABLE(filedlg,     [  --enable-filedlg        use wxFileDialog], wxUSE_FILEDLG)
WX_ARG_ENABLE(finddlg,     [  --enable-finddlg        use wxFindReplaceDialog], wxUSE_FINDREPLDLG)
WX_ARG_ENABLE(fontdlg,     [  --enable-fontdlg        use wxFontDialog], wxUSE_FONTDLG)
WX_ARG_ENABLE(dirdlg,      [  --enable-dirdlg         use wxDirDialog], wxUSE_DIRDLG)
WX_ARG_ENABLE(msgdlg,      [  --enable-msgdlg         use wxMessageDialog], wxUSE_MSGDLG)
WX_ARG_ENABLE(numberdlg,   [  --enable-numberdlg      use wxNumberEntryDialog], wxUSE_NUMBERDLG)
WX_ARG_ENABLE(splash,      [  --enable-splash         use wxSplashScreen], wxUSE_SPLASH)
WX_ARG_ENABLE(textdlg,     [  --enable-textdlg        use wxTextDialog], wxUSE_TEXTDLG)
WX_ARG_ENABLE(tipdlg,      [  --enable-tipdlg         use startup tips], wxUSE_STARTUP_TIPS)
WX_ARG_ENABLE(progressdlg, [  --enable-progressdlg    use wxProgressDialog], wxUSE_PROGRESSDLG)
WX_ARG_ENABLE(wizarddlg,   [  --enable-wizarddlg      use wxWizard], wxUSE_WIZARDDLG)

dnl ---------------------------------------------------------------------------
dnl misc GUI options
dnl ---------------------------------------------------------------------------

WX_ARG_ENABLE(menus,       [  --enable-menus          use wxMenu/wxMenuBar/wxMenuItem classes], wxUSE_MENUS)
WX_ARG_ENABLE(miniframe,   [  --enable-miniframe      use wxMiniFrame class], wxUSE_MINIFRAME)
WX_ARG_ENABLE(tooltips,    [  --enable-tooltips       use wxToolTip class], wxUSE_TOOLTIPS)
WX_ARG_ENABLE(splines,     [  --enable-splines        use spline drawing code], wxUSE_SPLINES)
WX_ARG_ENABLE(validators,  [  --enable-validators     use wxValidator and derived classes], wxUSE_VALIDATORS)
WX_ARG_ENABLE(busyinfo,    [  --enable-busyinfo       use wxBusyInfo], wxUSE_BUSYINFO)
WX_ARG_ENABLE(joystick,    [  --enable-joystick       use wxJoystick (Linux only)], wxUSE_JOYSTICK)
WX_ARG_ENABLE(metafile,    [  --enable-metafiles      use wxMetaFile (Windows only)], wxUSE_METAFILE)
WX_ARG_ENABLE(dragimage,   [  --enable-dragimage      use wxDragImage], wxUSE_DRAGIMAGE)
WX_ARG_ENABLE(accessibility,[  --enable-accessibility  enable accessibility support], wxUSE_ACCESSIBILITY)

dnl ---------------------------------------------------------------------------
dnl support for image formats that do not rely on external library
dnl ---------------------------------------------------------------------------

WX_ARG_ENABLE(palette,     [  --enable-palette        use wxPalette class], wxUSE_PALETTE)
WX_ARG_ENABLE(image,       [  --enable-image          use wxImage class], wxUSE_IMAGE)
WX_ARG_ENABLE(gif,         [  --enable-gif            use gif images (GIF file format)], wxUSE_GIF)
WX_ARG_ENABLE(pcx,         [  --enable-pcx            use pcx images (PCX file format)], wxUSE_PCX)
WX_ARG_ENABLE(iff,         [  --enable-iff            use iff images (IFF file format)], wxUSE_IFF)
WX_ARG_ENABLE(pnm,         [  --enable-pnm            use pnm images (PNM file format)], wxUSE_PNM)
WX_ARG_ENABLE(xpm,         [  --enable-xpm            use xpm images (XPM file format)], wxUSE_XPM)
WX_ARG_ENABLE(ico_cur,     [  --enable-icocur         use Windows ICO and CUR formats], wxUSE_ICO_CUR)

fi

dnl ---------------------------------------------------------------------------
dnl flavour support
dnl ---------------------------------------------------------------------------

dnl Should this be --enable?  I flip-flopped a couple of times and this seems
dnl in the spirit if not the letter, we have gtk-prefix and the like in this group.
dnl It doesn't actually change anything but the output file names.
AC_ARG_WITH(flavour,       [  --with-flavour=NAME     specify a name to identify this build], [WX_FLAVOUR="$withval"])

dnl ---------------------------------------------------------------------------
dnl some win32 settings
dnl ---------------------------------------------------------------------------

WX_ARG_ENABLE(official_build,  [  --enable-official_build official build of wxWidgets (win32 DLL only)], wxUSE_OFFICIAL_BUILD)
AC_ARG_ENABLE(vendor,  [  --enable-vendor=VENDOR  vendor name (win32 DLL only)], [VENDOR="$enableval"])
if test "x$VENDOR" = "x"; then
    VENDOR="custom"
fi


dnl for GUI only

dnl cache the options values before (may be) aborting below
WX_ARG_CACHE_FLUSH

dnl check that no more than one toolkit is given and that if none are given that
dnl we have a default one

AC_MSG_CHECKING(for toolkit)

# In Wine, we need to default to MSW, not GTK or MOTIF
if test "$wxUSE_WINE" = "yes"; then
    DEFAULT_DEFAULT_wxUSE_GTK=0
    DEFAULT_DEFAULT_wxUSE_MOTIF=0
    DEFAULT_DEFAULT_wxUSE_MSW=1
    wxUSE_SHARED=no
fi

if test "$wxUSE_GUI" = "yes"; then

    if test "$USE_BEOS" = 1; then
        AC_MSG_ERROR([BeOS GUI is not supported yet, use --disable-gui])
    fi

    if test "$TOOLKIT_GIVEN" = 1; then
      dnl convert "yes" to 1 and "no" to 0
      for toolkit in `echo $ALL_TOOLKITS`; do
        var=wxUSE_$toolkit
        eval "value=\$${var}"
        eval "$var=`echo \$value | sed -e "s/yes/1/" -e "s/no/0/"`"
      done
    else
      dnl try to guess the most apropriate toolkit for this platform
      for toolkit in `echo $ALL_TOOLKITS`; do
        if test "$has_toolkit_in_cache" != 1; then
          var=DEFAULT_DEFAULT_wxUSE_$toolkit
        else
          var=DEFAULT_wxUSE_$toolkit
        fi
        eval "wxUSE_$toolkit=\$${var}"
      done
    fi

    dnl we suppose that expr is available (maybe there is a better way to do
    dnl this? what about using ALL_TOOLKITS? TODO)
    NUM_TOOLKITS=`expr ${wxUSE_COCOA:-0} + ${wxUSE_GTK:-0} + ${wxUSE_MAC:-0} \
                  + ${wxUSE_MGL:-0} + ${wxUSE_MICROWIN:-0} \
                  + ${wxUSE_MOTIF:-0} + ${wxUSE_MSW:-0} + ${wxUSE_X11:-0}`

    dnl Allow wxUSE_PM only for OS/2 with EMX.
    case "${host}" in
      *-pc-os2_emx | *-pc-os2-emx )
        NUM_TOOLKITS=`expr ${NUM_TOOLKITS} + ${wxUSE_PM:-0}`
    esac

    case "$NUM_TOOLKITS" in
      1)
      ;;
      0)
        AC_MSG_ERROR(Please specify a toolkit -- cannot determine the default for ${host})
      ;;
      *)
        AC_MSG_ERROR(Please specify at most one toolkit (maybe some are cached in configarg.cache?))
    esac

    dnl cache the wxUSE_<TOOLKIT> values too
    for toolkit in `echo $ALL_TOOLKITS`; do
      var=wxUSE_$toolkit
      eval "value=\$${var}"
      if test "x$value" != x; then
        cache_var=CACHE_$toolkit
        eval "cache=\$${cache_var}"
        if test "$cache" = 1; then
          echo "$var=$value" >> ${wx_arg_cache_file}
        fi
        if test "$value" = 1; then
          toolkit_echo=`echo $toolkit | tr "[[A-Z]]" "[[a-z]]"`
          AC_MSG_RESULT($toolkit_echo)
        fi
      fi
    done
else
    if test "x$host_alias" != "x"; then
        AC_MSG_RESULT(base ($host_alias hosted) only)
    else
        AC_MSG_RESULT(base only)
    fi
fi

dnl ---------------------------------------------------------------------------
dnl When we are using Cygwin with Motif/GTK+, we want it to appear like
dnl 'just' a POSIX platform, so the Win32 API must not be available
dnl ---------------------------------------------------------------------------
dnl (Windows-only piece)
wants_win32=0
doesnt_want_win32=0
case "${host}" in
  *-*-cygwin*)
      if test "$wxUSE_MSW" = 1 ; then
        wants_win32=1
      else
        doesnt_want_win32=1
      fi
  ;;
  *-*-mingw*)
      wants_win32=1
  ;;
esac

dnl ---------------------------------------------------------------------------
dnl Wine is a virtual platform, we need to patch things up a bit
dnl ---------------------------------------------------------------------------
if test "$wxUSE_WINE" = "yes"; then
    wants_win32=1
    dnl FIXME: we should do a better job of testing for these
    CC=winegcc
    CXX=wineg++
    RESCOMP=wrc
    LDFLAGS_GUI="-mwindows"
fi

dnl NB: The two tests bellow are *NOT* mutually exclusive! They should only
dnl     take effect on Cygwin/Mingw and not other platforms.
if test "$wants_win32" = 1 ; then
    USE_UNIX=0
    USE_WIN32=1
    AC_DEFINE(__WIN32__)
    AC_DEFINE(__WIN95__)
    AC_DEFINE(__WINDOWS__)
    AC_DEFINE(__GNUWIN32__)
    AC_DEFINE(STRICT)
    AC_DEFINE(WINVER, 0x0400)
fi
if test "$doesnt_want_win32" = 1 ; then
    USE_UNIX=1
    USE_WIN32=0
fi
dnl (end of Windows-only piece)

if test "$USE_UNIX" = 1 ; then
    wxUSE_UNIX=yes
    AC_DEFINE(__UNIX__)
fi

dnl ---------------------------------------------------------------------------
dnl Checks for programs
dnl ---------------------------------------------------------------------------

dnl flush the cache because checking for programs might abort
AC_CACHE_SAVE

dnl cross-compiling support: we're cross compiling if the build system is
dnl different from the target one (assume host and target be always the same)
if test "$build" != "$host" ; then
    if test "$USE_WIN32" = 1 -o "$USE_DOS" = 1 -o "$USE_UNIX"; then
        CC=$host_alias-gcc
        CXX=$host_alias-c++
        AR=$host_alias-ar
        RANLIB=$host_alias-ranlib
        DLLTOOL=$host_alias-dlltool
        RESCOMP=$host_alias-windres
        STRIP=$host_alias-strip
    else
        AC_MSG_ERROR($build_alias -> $host_alias cross compilation not supported yet.)
    fi
fi

dnl C-compiler checks
dnl   defines CC with the compiler to use
dnl   defines GCC with yes if using gcc
dnl   defines GCC empty if not using gcc
dnl   defines CFLAGS
dnl
dnl this magic incantation is needed to prevent AC_PROG_CC from setting the
dnl default CFLAGS (something like "-g -O2") -- we don't need this as we add
dnl -g and -O flags ourselves below
CFLAGS=${CFLAGS:=}
AC_PROG_CC

dnl is -traditional needed for correct compilations
dnl   adds -traditional for gcc if needed
AC_PROG_GCC_TRADITIONAL

AC_LANG_SAVE
AC_LANG_CPLUSPLUS

dnl C++-compiler checks
dnl   defines CXX with the compiler to use
dnl   defines GXX with yes if using gxx
dnl   defines GXX empty if not using gxx
dnl   defines CXXFLAGS
dnl
dnl see CFLAGS line above
CXXFLAGS=${CXXFLAGS:=}
AC_PROG_CXX

AC_LANG_RESTORE

dnl ranlib command
dnl   defines RANLIB with the appropriate command
AC_PROG_RANLIB

dnl ar command
dnl   defines AR with the appropriate command
AC_CHECK_PROG(AR, ar, ar)
if test "x$AR" = "x" ; then
    AC_MSG_ERROR([ar is needed to build wxWidgets])
fi

dnl install checks
dnl   defines INSTALL with the appropriate command
AC_PROG_INSTALL

dnl   make install path absolute (if not already);
dnl   will fail with (some) MSDOS paths
case ${INSTALL} in
  /* ) # Absolute
     ;;
  ?:* ) # Drive letter, considered as absolute.
     ;;
  *)
     INSTALL=`pwd`/${INSTALL} ;;
esac


case "${host}" in

    dnl The other BSD's should probably go in here too, since this is
    dnl to workaround a strange static lib BSDism.
    dnl Mac OS X install seems to ignore -p option...
    powerpc-*-darwin* )
        INSTALL_PROGRAM="cp -fp"
        INSTALL_DATA="cp -fp"
        ;;
    *)
        ;;
esac

dnl strip command
dnl   defines STRIP as strip or nothing if not found
AC_CHECK_PROG(STRIP, strip, strip, true)

dnl does make support "-include" (only GNU make does AFAIK)?
AC_CACHE_CHECK([if make is GNU make], wx_cv_prog_makeisgnu,
[
    if ( ${SHELL-sh} -c "${MAKE-make} --version" 2> /dev/null |
            egrep -s GNU > /dev/null); then
        wx_cv_prog_makeisgnu="yes"
    else
        wx_cv_prog_makeisgnu="no"
    fi
])

if test "x$wx_cv_prog_makeisgnu" = "xyes"; then
    IF_GNU_MAKE=""
else
    IF_GNU_MAKE="#"
fi

AC_SUBST(IF_GNU_MAKE)

dnl we don't need to check for VPATH support in GNU make - it does have it
if test "x$wx_cv_prog_makeisgnu" != "xyes"; then
dnl check if VPATH works
AC_CACHE_CHECK([if make supports VPATH], wx_cv_prog_makevpath, [
dnl create Makefile
cat - << EOF > confMake
check : file
	cp \$? \$@
	cp \$? final_file
EOF

if test ! -d sub ; then
  mkdir sub
fi
echo dummy > sub/file
${MAKE-make} -f confMake VPATH=sub 2>&5 > /dev/null
RESULT=$?
rm -f sub/file check final_file confMake
rmdir sub
if test "$RESULT" = 0; then
  wx_cv_prog_makevpath="yes"
else
  wx_cv_prog_makevpath="no"
fi
])

if test "x$wx_cv_prog_makevpath" != "xyes"; then
AC_MSG_ERROR([
You need a make-utility that is able to use the variable
VPATH correctly.
If your version of make does not support VPATH correctly,
please install GNU-make (possibly as gmake), and start
configure with the following command:
export MAKE=gmake; ./configure  for sh-type shells
setenv MAKE gmake; ./configure  for csh-type shells
Also please do remember to use gmake in this case every time
you are trying to compile.
])
fi dnl make without VPATH
fi dnl not GNU make

dnl needed for making link to setup.h
AC_PROG_LN_S

dnl ------------------------------------------------------------------------
dnl Platform specific tests
dnl ------------------------------------------------------------------------

case "${host}" in
  *-pc-os2_emx | *-pc-os2-emx )
      dnl ---------------------------------------------------------------------
      dnl When we are using gcc on OS/2, we want to be either using resources
      dnl (PM) or a more complete POSIX emulation for Motif/GTK+/X11.
      dnl Moreover we need to link explicitly against either stdcpp.a or
      dnl stdcxx.a (depending on compiler version), since we are using "gcc",
      dnl not "g++/c++".
      dnl ---------------------------------------------------------------------
      dnl (OS/2-only piece)
      if test "$wxUSE_MOTIF" = 1 -o "$wxUSE_GTK" = 1 -o "$wxUSE_X11" = 1; then
          dnl More complete Unix emulation for unix-like ports
          dnl by linking in POSIX/2's cExt (if available).
          AC_CHECK_LIB(cExt, drand48, LIBS="$LIBS -lcExt")
      else
          dnl Include resources for the "native" port (wxPM).
          RESPROGRAMOBJ="\${top_srcdir}/include/wx/os2/wx.res"
      fi
      AC_CACHE_CHECK([for gcc version], wx_cv_gccversion,[
          AC_TRY_COMPILE([],
              [
                  #if (__GNUC__ < 3)
                      #error old gcc
                  #endif
              ],
              [
                  AC_TRY_COMPILE([],
                      [
                          #if (__GNUC__==3) && ((__GNUC_MINOR__ < 2) || ((__GNUC_MINOR__==2) && (__GNUC_PATCHLEVEL__<2)))
                              #error intermediate gcc
                          #endif
                      ],
                      [
                         wx_cv_gccversion=Innotek
                      ],
                      [
                         wx_cv_gccversion=3
                      ]
                  )
              ],
              [
                  wx_cv_gccversion=2
              ]
          )
      ])
      if test "$wx_cv_gccversion" = "2"; then
          LIBS="$LIBS -lstdcpp"
          LDFLAGS="$LDFLAGS -Zsysv-signals"
      else
          if test "$wx_cv_gccversion" = "3"; then
              LIBS="$LIBS -lstdcxx"
              LDFLAGS="$LDFLAGS -Zsysv-signals"
          else
              LIBS="$LIBS -lstdc++"
          fi
      fi
      if test "$wxUSE_OMF" = "yes"; then
        LDFLAGS="$LDFLAGS -Zomf -Zlinker /PMTYPE:PM -Zlinker /EXEPACK"
      fi
      dnl (end of OS/2-only piece)
  ;;
  *)
      dnl ---------------------------------------------------------------------
      dnl look for strcasecmp() in string.h and then strings.h if it's not
      dnl there. Don't do this on OS/2, where "stricmp" is the function to be
      dnl used.
      dnl ---------------------------------------------------------------------
      dnl (non-OS/2-only piece)

      AC_LANG_SAVE
      AC_LANG_CPLUSPLUS

      AC_CACHE_CHECK([for strcasecmp() in string.h], ac_cv_string_strcasecmp, [
          AC_TRY_LINK([
              #include <string.h>
              ],
              [
                  strcasecmp("foo", "bar");
              ],
              ac_cv_string_strcasecmp=yes,
              ac_cv_string_strcasecmp=no
          )
      ])

      if test x"$ac_cv_string_strcasecmp" = "xyes"; then
          AC_DEFINE(HAVE_STRCASECMP_IN_STRING_H)
      else
          AC_CACHE_CHECK([for strcasecmp() in strings.h], ac_cv_strings_strcasecmp, [
              AC_TRY_LINK([
                  #include <strings.h>
                  ],
                  [
                      strcasecmp("foo", "bar");
                  ],
                  ac_cv_strings_strcasecmp=yes,
                  ac_cv_strings_strcasecmp=no
              )
          ])

          if test x"$ac_cv_strings_strcasecmp" = "xyes"; then
              AC_DEFINE(HAVE_STRCASECMP_IN_STRINGS_H)
          else
              AC_MSG_ERROR([No case-insensitive string comparison function found.])
          fi
      fi

      AC_LANG_RESTORE
      dnl (end of non-OS/2-only piece)
  ;;
esac

dnl ------------------------------------------------------------------------
dnl Check for headers
dnl ------------------------------------------------------------------------

dnl defines HAVE_STDLIB_H
AC_CHECK_HEADERS(stdlib.h)
dnl defines HAVE_MALLOC_H
AC_CHECK_HEADERS(malloc.h)
dnl defines HAVE_UNISTD_H
AC_CHECK_HEADERS(unistd.h)
dnl defines HAVE_WCHAR_H
AC_CHECK_HEADERS(wchar.h)

dnl maybe wchar_t is in wcstr.h if we don't have wchar.h?
if test "$ac_cv_header_wchar_h" != "yes"; then
    dnl defines HAVE_WCSTR_H
    AC_CHECK_HEADERS(wcstr.h)
fi

dnl defines HAVE_FNMATCH_H
AC_CHECK_HEADERS(fnmatch.h)

if test "x$ac_cv_header_fnmatch_h" = "xyes"; then
    AC_CHECK_FUNCS(fnmatch)
fi

dnl defines HAVE_LANGINFO_H (GNU libc locale parameters)
AC_CHECK_HEADERS(langinfo.h)

case "${host}" in
  *-pc-os2_emx | *-pc-os2-emx )
      dnl Explicitly link -lintl if langinfo.h is available.
      if test $ac_cv_header_langinfo_h = "yes"; then
          LIBS="$LIBS -lintl"
      fi
  ;;
esac

if test "$wxUSE_GUI" = "yes"; then
    if test "$wxUSE_UNIX" = "yes" -a "$wxUSE_PM" != 1; then
        dnl defines HAVE_X11_XKBLIB_H
        AC_CHECK_HEADERS(X11/Xlib.h)
        AC_CHECK_HEADERS([X11/XKBlib.h], [], [], 
                         [
                          #if HAVE_X11_XLIB_H
                            #include <X11/Xlib.h>
                          #endif
                         ])
    fi
fi

dnl ---------------------------------------------------------------------------
dnl Checks for compiler characteristics
dnl ---------------------------------------------------------------------------

dnl defines const to be empty if c-compiler does not support const fully
AC_C_CONST
dnl defines inline to a sensible value for the c-compiler
AC_C_INLINE

dnl check the sizes of integral types (give some reasonable default values for
dnl cross-compiling)
dnl   defines the size of certain types of variables in SIZEOF_<TYPE>
AC_CHECK_SIZEOF(char, 1)
AC_CHECK_SIZEOF(short, 2)
AC_CHECK_SIZEOF(void *, 4)
AC_CHECK_SIZEOF(int, 4)
AC_CHECK_SIZEOF(long, 4)
AC_CHECK_SIZEOF(size_t, 4)

case "${host}" in
    arm-*-linux* )
        AC_CHECK_SIZEOF(long long, 8)
    ;;
    *-hp-hpux* )
        AC_CHECK_SIZEOF(long long, 0)
        if test "$ac_cv_sizeof_long_long" != "0"; then
            dnl HPUX 10.20 headers need this define in order to use long long definitions
            CPPFLAGS="$CPPFLAGS -D_INCLUDE_LONGLONG"
        fi
    ;;
    * )
        AC_CHECK_SIZEOF(long long, 0)
esac

dnl we have to do it ourselves because SGI/Irix's stdio.h does not include
dnl wchar_t and AC_CHECK_SIZEOF only includes stdio.h
dnl Mac OS X does not provide wchar.h and wchar_t is defined by stdlib.h (GD)
AC_CACHE_CHECK([size of wchar_t], wx_cv_sizeof_wchar_t,
[
    AC_TRY_RUN(
        [
            /* DJGPP only has fake wchar_t: */
            #ifdef __DJGPP__
            #  error "fake wchar_t"
            #endif
            #ifdef HAVE_WCHAR_H
            #  ifdef __CYGWIN__
            #    include <stddef.h>
            #  endif
            #  include <wchar.h>
            #endif
            #ifdef HAVE_STDLIB_H
            #  include <stdlib.h>
            #endif
            #include <stdio.h>
            int main()
            {
                FILE *f=fopen("conftestval", "w");
                if (!f) exit(1);
                fprintf(f, "%i", sizeof(wchar_t));
                exit(0);
            }
        ],
        wx_cv_sizeof_wchar_t=`cat conftestval`,
        wx_cv_sizeof_wchar_t=0,
        [
            case "${host}" in
              *-pc-msdosdjgpp )
                wx_cv_sizeof_wchar_t=0
                ;;
              * )
                wx_cv_sizeof_wchar_t=4
                ;;
            esac
        ]
    )
])

AC_DEFINE_UNQUOTED(SIZEOF_WCHAR_T, $wx_cv_sizeof_wchar_t)

dnl checks needed to define wxVaCopy
AC_CACHE_CHECK([for va_copy],
    wx_cv_func_va_copy,
    [
        AC_LINK_IFELSE([
            #include <stdarg.h>
            void foo(char *f, ...)
            {
                va_list ap1, ap2;
                va_start(ap1, f);
                va_copy(ap2, ap1);
                va_end(ap2);
                va_end(ap1);
            }],
            wx_cv_func_va_copy=yes,
            wx_cv_func_va_copy=no
        )
    ]
)

if test $wx_cv_func_va_copy = "yes"; then
    AC_DEFINE(HAVE_VA_COPY)
else
    dnl try to understand how can we copy va_lists
    AC_CACHE_CHECK([if va_list can be copied by value],
        wx_cv_type_va_list_lvalue,
        [
            AC_RUN_IFELSE([
                #include <stdarg.h>
                int foo(char *f, ...)
                {
                    va_list ap1, ap2;
                    va_start(ap1, f);
                    ap2 = ap1;
                    if ( va_arg(ap1, int) != 17 || va_arg(ap2, int) != 17 )
                        return 1;
                    va_end(ap2);
                    va_end(ap1);
                    return 0;
                }
                int main()
                {
                    return foo("hi", 17);
                }],
                wx_cv_type_va_list_lvalue=yes,
                wx_cv_type_va_list_lvalue=no,
                dnl assume most common case for cross-compiling...
                wx_cv_type_va_list_lvalue=yes
            )
        ]
    )

    if test $wx_cv_type_va_list_lvalue != "yes"; then
        dnl we suppose that the only thing which can't be copied like this
        dnl are arrays... only experience will show whether this is really true
        AC_DEFINE(VA_LIST_IS_ARRAY)
    fi
fi

dnl check for large file support
AC_SYS_LARGEFILE

dnl we need to define _FILE_OFFSET_BITS or _LARGE_FILES on the compiler command
dnl line because otherwise the system headers risk being included before
dnl wx/defs.h which defines these constants leading to inconsistent
dnl sizeof(off_t) in different source files of the same program and linking
dnl problems
if test "x$wx_largefile" = "xyes"; then
    if test "x$ac_cv_sys_file_offset_bits" = "x64"; then
        WX_LARGEFILE_FLAGS="-D_FILE_OFFSET_BITS=64 -D_LARGE_FILES"
    else
        WX_LARGEFILE_FLAGS="-D_LARGE_FILES"
    fi
    CPPFLAGS="$CPPFLAGS $WX_LARGEFILE_FLAGS"
fi

dnl check for bytesex stuff (don't use AC_C_BIGENDIAN to allow cross-compiling)
WX_C_BIGENDIAN

dnl check for iostream (as opposed to iostream.h) standard header
WX_CPP_NEW_HEADERS(, AC_DEFINE(wxUSE_IOSTREAMH))

dnl check whether C++ compiler supports bool built-in type
WX_CPP_BOOL

dnl check whether C++ compiler supports explicit keyword
WX_CPP_EXPLICIT

dnl check whether C++ compiler supports C++ casts
AC_CXX_CONST_CAST
AC_CXX_REINTERPRET_CAST
AC_CXX_STATIC_CAST

dnl check various STL features
if test "$wxUSE_STL" = "yes"; then
    AC_LANG_PUSH(C++)

    dnl check for basic STL functionality
    AC_MSG_CHECKING([for basic STL functionality])
    AC_TRY_COMPILE([#include <string>
                    #include <functional>
                    #include <algorithm>
                    #include <vector>
                    #include <list>],
                    [std::vector<int> moo;
                     std::list<int> foo;
                     std::vector<int>::iterator it = 
                         std::find_if(moo.begin(), moo.end(),
                                      std::bind2nd(std::less<int>(), 3));],
                    [AC_MSG_RESULT([yes])],
                    [AC_MSG_RESULT([no])
                     AC_MSG_ERROR([Basic STL functionality missing])])

    dnl check if <string> declares std::wstring
    AC_MSG_CHECKING([for std::wstring in <string>])
    AC_TRY_COMPILE([#include <string>],
                   [std::wstring foo;],
                   [AC_MSG_RESULT(yes)
                    AC_DEFINE(HAVE_STD_WSTRING)],
                   [AC_MSG_RESULT(no)])

    dnl check for compliant std::string::compare
    AC_MSG_CHECKING([for compliant std::string::compare])
    AC_TRY_COMPILE([#include <string>],
                   [std::string foo, bar;
                    foo.compare(bar);
                    foo.compare(1, 1, bar);
                    foo.compare(1, 1, bar, 1, 1);
                    foo.compare("");
                    foo.compare(1, 1, "");
                    foo.compare(1, 1, "", 2);],
                   [AC_MSG_RESULT(yes)
                    AC_DEFINE(HAVE_STD_STRING_COMPARE)],
                   [AC_MSG_RESULT(no)])

    dnl check for hash_map and hash_set headers
    AC_CHECK_HEADER([hash_map],
                    [AC_MSG_CHECKING([for hash_map and hash_set])
                     AC_TRY_COMPILE([#include <hash_map>
                                     #include <hash_set>],
                                    [std::hash_map<double*, char*, std::hash<double*>, std::equal_to<double*> > test1;
                                     std::hash_set<char*, std::hash<char*>, std::equal_to<char*> > test2;],
                                    [AC_MSG_RESULT(yes)
                                     AC_DEFINE(HAVE_HASH_MAP)
                                     AC_DEFINE(HAVE_STD_HASH_MAP)],
                                    [AC_MSG_RESULT(no)])
                     ])

    AC_CHECK_HEADER([ext/hash_map],
                    [AC_MSG_CHECKING([for hash_map and hash_set])
                     AC_TRY_COMPILE([#include <ext/hash_map>
                                     #include <ext/hash_set>],
                                    [__gnu_cxx::hash_map<double*, char*, __gnu_cxx::hash<double*>, std::equal_to<double*> > test1;
                                     __gnu_cxx::hash_set<char*, __gnu_cxx::hash<char*>, std::equal_to<char*> > test2;],
                                    [AC_MSG_RESULT(yes)
                                     AC_DEFINE(HAVE_EXT_HASH_MAP)
                                     AC_DEFINE(HAVE_GNU_CXX_HASH_MAP)],
                                    [AC_MSG_RESULT(no)])
                     ])

    AC_LANG_POP
fi

dnl ---------------------------------------------------------------------------
dnl Define search path for includes and libraries: all headers and libs will be
dnl looked for in all directories of this path
dnl ---------------------------------------------------------------------------

dnl Notice that /usr/include should *not* be in this list, otherwise it breaks
dnl compilation on Solaris/AIX/... with gcc because standard (non ANSI C)
dnl headers are included instead of the "fixed" (ANSI-fied) gcc ones.
dnl
dnl Also try to put all directories which may contain X11R6 before those which
dnl may contain X11R5/4 - we want to use R6 on machines which have both!
dnl
dnl In the same vein. Motif 2.1 should be tried before Motif 1.2 for the
dnl systems which have both (AIX 4.x does)
SEARCH_INCLUDE="\
    /usr/local/include        \
                              \
    /usr/Motif-2.1/include    \
    /usr/Motif-1.2/include    \
    /usr/include/Motif1.2     \
    /opt/xpm/include/X11      \
    /opt/GBxpm/include/       \
    /opt/GBxpm/X11/include/   \
                              \
    /usr/dt/include           \
    /usr/openwin/include      \
                              \
    /usr/include/Xm           \
                              \
    /usr/X11R6/include        \
    /usr/X11R6.4/include      \
    /usr/X11R5/include        \
    /usr/X11R4/include        \
                              \
    /usr/include/X11R6        \
    /usr/include/X11R5        \
    /usr/include/X11R4        \
                              \
    /usr/local/X11R6/include  \
    /usr/local/X11R5/include  \
    /usr/local/X11R4/include  \
                              \
    /usr/local/include/X11R6  \
    /usr/local/include/X11R5  \
    /usr/local/include/X11R4  \
                              \
    /usr/X11/include          \
    /usr/include/X11          \
    /usr/local/X11/include    \
    /usr/local/include/X11    \
                              \
    /usr/X386/include         \
    /usr/x386/include         \
    /usr/XFree86/include/X11  \
                              \
    X:/XFree86/include        \
    X:/XFree86/include/X11    \
                              \
    /usr/include/gtk          \
    /usr/local/include/gtk    \
    /usr/include/glib         \
    /usr/local/include/glib   \
                              \
    /usr/include/qt           \
    /usr/local/include/qt     \
                              \
    /usr/local/arm/3.3.2/include     \
                              \
    /usr/include/windows      \
    /usr/include/wine         \
    /usr/local/include/wine   \
                              \
    /usr/unsupported/include  \
    /usr/athena/include       \
    /usr/local/x11r5/include  \
    /usr/lpp/Xamples/include  \
                              \
    /usr/openwin/share/include"

dnl prepend lib and lib32 for IRIX where the files in these directories should
dnl be found before the ones in lib64 for 32bit compilation -- of course, this
dnl probably/surely breaks 64bit compilation... IMO the only real solution is to
dnl stop using WX_PATH_FIND_LIBRARIES() at all and use AC_CHECK_LIB() instead
dnl
dnl add 64 bit versions for Linux on AMD (this is not perfect but well...)
SEARCH_LIB="/usr/lib /usr/lib32 `echo "$SEARCH_INCLUDE" | sed s/include/lib/g` /usr/lib64 /usr/X11R6/lib64"

dnl ------------------------------------------------------------------------
dnl Check for libraries
dnl ------------------------------------------------------------------------

dnl flush the cache because checking for libraries below might abort
AC_CACHE_SAVE

dnl check for glibc version
dnl
dnl VZ: I have no idea why had this check been there originally, but now
dnl     we could probably do without it by just always adding _GNU_SOURCE
if test "$USE_LINUX" = 1 -o "$USE_GNU" = 1; then
    AC_CACHE_CHECK([for glibc 2.1 or later], wx_cv_lib_glibc21,[
        AC_TRY_COMPILE([#include <features.h>],
            [
                #if (__GLIBC__ < 2) || (__GLIBC_MINOR__ < 1)
                    #error not glibc2.1
                #endif
            ],
            [
                wx_cv_lib_glibc21=yes
            ],
            [
                wx_cv_lib_glibc21=no
            ]
        )
    ])
    if test "$wx_cv_lib_glibc21" = "yes"; then
        AC_DEFINE(wxHAVE_GLIBC2)
    fi
fi

dnl we may need _GNU_SOURCE for 2 things:
dnl
dnl  1. to get PTHREAD_MUTEX_RECURSIVE with glibc 2.1+ (strictly speaking we
dnl  only need _XOPEN_SOURCE=500 but just defining this disables _BSD_SOURCE
dnl  which breaks libtiff compilation, so it is simpler to just define
dnl  _GNU_SOURCE to get everything)
dnl
dnl  2. for Unicode functions
if test "x$wx_cv_lib_glibc21" = "xyes"; then
    if test "$wxUSE_UNICODE" = "yes" -o "$wxUSE_THREADS" = "yes"; then
        AC_DEFINE(_GNU_SOURCE)
    fi
fi

dnl ---------------------------------------------------------------------------
dnl Optional libraries
dnl
dnl   --with-<lib>=sys
dnl       looks for system library and fails if not found
dnl
dnl   --with-<lib>
dnl   --with-<lib>=yes
dnl       looks for system library and, if not found, prints a warning,
dnl       falls back to the builtin wx version, and continues configuration
dnl
dnl   --with-<lib>=builtin
dnl       uses builtin wx version without searching for system library
dnl
dnl   --with-<lib>=no
dnl   --without-<lib>
dnl       do not use library (neither system nor builtin wx version)
dnl
dnl ---------------------------------------------------------------------------

dnl ------------------------------------------------------------------------
dnl Check for regex libraries
dnl ------------------------------------------------------------------------

if test "$wxUSE_REGEX" != "no"; then
    AC_DEFINE(wxUSE_REGEX)
            
    if test "$wxUSE_UNICODE" = "yes" -a "$wxUSE_REGEX" = "yes"; then
        AC_MSG_WARN([Defaulting to the the builtin regex library for Unicode build.])
        wxUSE_REGEX=builtin
    fi

    if test "$wxUSE_REGEX" = "sys" -o "$wxUSE_REGEX" = "yes" ; then
        dnl according to Unix 98 specs, regcomp() is in libc but I believe that
        dnl on some old systems it may be in libregex - check for it too?
        AC_CHECK_HEADER(regex.h, [AC_CHECK_FUNCS(regcomp)])

        if test "x$ac_cv_func_regcomp" != "xyes"; then
            if test "$wxUSE_REGEX" = "sys" ; then
                AC_MSG_ERROR([system regex library not found! Use --with-regex to use built-in version])
            else
                AC_MSG_WARN([system regex library not found, will use built-in instead])
                wxUSE_REGEX=builtin
            fi
        else
            dnl we are using the system library
            wxUSE_REGEX=sys
            dnl only the built-in supports advanced REs
            AC_DEFINE(WX_NO_REGEX_ADVANCED)
        fi
    fi
fi

dnl ------------------------------------------------------------------------
dnl Check for zlib compression library
dnl ------------------------------------------------------------------------

ZLIB_LINK=
if test "$wxUSE_ZLIB" != "no" ; then
    AC_DEFINE(wxUSE_ZLIB)

    if test "$wxUSE_ZLIB" = "sys" -o "$wxUSE_ZLIB" = "yes" ; then
        dnl don't test for zlib under Mac -- its verson there is 1.1.3 but we
        dnl should still use it because hopefully (can someone confirm this?)
        dnl Apple did fix the security problem in it and not using the system
        dnl library results in a whole bunch of warnings when linking with
        dnl Carbon framework
        if test "$USE_DARWIN" = 1; then
            system_zlib_h_ok="yes"
        else
            dnl we have troubles with ancient zlib versions (e.g. 1.0.4 is
            dnl known to not work) and although I don't know which is
            dnl the minimal required version it's safer to test for 1.1.4 as
            dnl it fixes a security problem in 1.1.3 -- and hopefully nobody
            dnl has anything more ancient (1.1.3 was released in July 1998)
            dnl anyhow
            AC_CACHE_CHECK([for zlib.h >= 1.1.4], ac_cv_header_zlib_h,
                [AC_TRY_RUN(
                    dnl zlib.h defines ZLIB_VERSION="x.y.z"
                    [
                        #include <zlib.h>
                        #include <stdio.h>

                        int main()
                        {
                            FILE *f=fopen("conftestval", "w");
                            if (!f) exit(1);
                            fprintf(f, "%s",
                                    ZLIB_VERSION[0] == '1' &&
                                    (ZLIB_VERSION[2] > '1' ||
                                    (ZLIB_VERSION[2] == '1' &&
                                     ZLIB_VERSION[4] >= '4')) ? "yes" : "no");
                            exit(0);
                        }
                    ],
                    ac_cv_header_zlib_h=`cat conftestval`,
                    ac_cv_header_zlib_h=no,
                    dnl cross-compiling: don't have an answer, try later
                    unset ac_cv_header_zlib_h
                )]
            )
            dnl If the test above did not come up with a value (e.g. cross
            dnl compiling) then this should give a definitive answer
            AC_CHECK_HEADER(zlib.h)

            system_zlib_h_ok=$ac_cv_header_zlib_h
        fi

        if test "$system_zlib_h_ok" = "yes"; then
            AC_CHECK_LIB(z, deflate, ZLIB_LINK=" -lz")
        fi

        if test "x$ZLIB_LINK" = "x" ; then
            if test "$wxUSE_ZLIB" = "sys" ; then
                AC_MSG_ERROR([zlib library not found or too old! Use --with-zlib=builtin to use built-in version])
            else
                AC_MSG_WARN([zlib library not found or too old, will use built-in instead])
                wxUSE_ZLIB=builtin
            fi
        else
            dnl we are using the system library
            wxUSE_ZLIB=sys
        fi
    fi
fi

dnl ------------------------------------------------------------------------
dnl Check for png library
dnl ------------------------------------------------------------------------

PNG_LINK=
if test "$wxUSE_LIBPNG" != "no" ; then
    AC_DEFINE(wxUSE_LIBPNG)

    if test "$wxUSE_MGL" = 1 -a "$wxUSE_LIBPNG" = "builtin" ; then
        AC_MSG_WARN([wxMGL doesn't work with builtin png library, will use MGL one instead])
        wxUSE_LIBPNG=sys
    fi

    dnl for the check below to have a chance to succeed, we must already have
    dnl libz somewhere (don't do this when bulding wxMGL since its libpng
    dnl doesn't depend on zlib)
    if test "$wxUSE_MGL" != 1 -a "$wxUSE_LIBPNG" = "sys" -a "$wxUSE_ZLIB" != "sys" ; then
        AC_MSG_WARN([system png library doesn't work without system zlib, will use built-in instead])
        wxUSE_LIBPNG=builtin
    fi

    if test "$wxUSE_MGL" != 1 ; then
        dnl Don't check for libpng when building wxMGL, libmgl contains it
        if test "$wxUSE_LIBPNG" = "sys" -o "$wxUSE_LIBPNG" = "yes" ; then
            dnl libpng version 0.9 is known to not work, if an even newer
            dnl version is required, just bump it up in the test below
            AC_CACHE_CHECK([for png.h > 0.90], ac_cv_header_png_h,
                [AC_TRY_RUN(
                    dnl png.h defines PNG_LIBPNG_VER=number
                    [
                        #include <png.h>
                        #include <stdio.h>

                        int main()
                        {
                            FILE *f=fopen("conftestval", "w");
                            if (!f) exit(1);
                            fprintf(f, "%s",
                                     PNG_LIBPNG_VER > 90 ? "yes" : "no");
                            exit(0);
                        }
                    ],
                    ac_cv_header_png_h=`cat conftestval`,
                    ac_cv_header_png_h=no,
                    dnl cross-compiling: test (later) if we have any png.h
                    unset ac_cv_header_png_h
                )]
            )
            AC_CHECK_HEADER(png.h)

            if test "$ac_cv_header_png_h" = "yes"; then
                AC_CHECK_LIB(png, png_check_sig, PNG_LINK=" -lpng -lz", , [-lz -lm])
            fi

            if test "x$PNG_LINK" = "x" ; then
                if test "$wxUSE_LIBPNG" = "sys" ; then
                    AC_MSG_ERROR([system png library not found or too old! Use --with-libpng=builtin to use built-in version])
                else
                    AC_MSG_WARN([system png library not found or too old, will use built-in instead])
                    wxUSE_LIBPNG=builtin
                fi
            else
                dnl we are using the system library
                wxUSE_LIBPNG=sys
            fi
        fi
    fi

    SAMPLES_SUBDIRS="$SAMPLES_SUBDIRS png"
fi

dnl ------------------------------------------------------------------------
dnl Check for jpeg library
dnl ------------------------------------------------------------------------

JPEG_LINK=
if test "$wxUSE_LIBJPEG" != "no" ; then
    AC_DEFINE(wxUSE_LIBJPEG)

    if test "$wxUSE_MGL" = 1 -a "$wxUSE_LIBJPEG" = "builtin" ; then
        AC_MSG_WARN([wxMGL doesn't work with builtin jpeg library, will use MGL one instead])
        wxUSE_LIBJPEG=sys
    fi

    if test "$wxUSE_MGL" != 1 ; then
        dnl Don't check for libjpeg when building wxMGL, libmgl contains it
        if test "$wxUSE_LIBJPEG" = "sys" -o "$wxUSE_LIBJPEG" = "yes" ; then
            dnl can't use AC_CHECK_HEADER as jconfig.h defines things like
            dnl HAVE_STDLIB_H which are already defined and this provokes
            dnl a compiler warning which configure considers as an error...
            AC_MSG_CHECKING(for jpeglib.h)
            AC_CACHE_VAL(ac_cv_header_jpeglib_h,
                AC_TRY_COMPILE(
                    [
                        #undef HAVE_STDLIB_H
                        #include <stdio.h>
                        #include <jpeglib.h>
                    ],
                    [
                    ],
                    ac_cv_header_jpeglib_h=yes,
                    ac_cv_header_jpeglib_h=no
                )
            )
            AC_MSG_RESULT($ac_cv_header_jpeglib_h)

            if test "$ac_cv_header_jpeglib_h" = "yes"; then
                AC_CHECK_LIB(jpeg, jpeg_read_header, JPEG_LINK=" -ljpeg")
            fi

            if test "x$JPEG_LINK" = "x" ; then
                if test "$wxUSE_LIBJPEG" = "sys" ; then
                    AC_MSG_ERROR([system jpeg library not found! Use --with-libjpeg=builtin to use built-in version])
                else
                    AC_MSG_WARN([system jpeg library not found, will use built-in instead])
                    wxUSE_LIBJPEG=builtin
                fi
            else
                dnl we are using the system library
                wxUSE_LIBJPEG=sys
            fi
        fi
    fi
fi

dnl ------------------------------------------------------------------------
dnl Check for tiff library
dnl ------------------------------------------------------------------------

TIFF_LINK=
TIFF_PREREQ_LINKS=-lm
if test "$wxUSE_LIBTIFF" != "no" ; then
    AC_DEFINE(wxUSE_LIBTIFF)

    if test "$wxUSE_LIBTIFF" = "sys" -o "$wxUSE_LIBTIFF" = "yes" ; then
        dnl libtiff may depend on libjpeg and libz so use them in the test
        dnl below or it would fail
        if test "$wxUSE_LIBJPEG" = "sys"; then
            TIFF_PREREQ_LINKS="$TIFF_PREREQ_LINKS $JPEG_LINK"
        fi
        if test "$wxUSE_ZLIB" = "sys"; then
            TIFF_PREREQ_LINKS="$TIFF_PREREQ_LINKS $ZLIB_LINK"
        fi
        AC_CHECK_HEADER(tiffio.h,
                        [
                            AC_CHECK_LIB(tiff, TIFFError,
                                         TIFF_LINK=" -ltiff",
                                         ,
                                         $TIFF_PREREQ_LINKS)
                        ]
                       )

        if test "x$TIFF_LINK" = "x" ; then
            if test "$wxUSE_LIBTIFF" = "sys" ; then
                AC_MSG_ERROR([system tiff library not found! Use --with-libtiff=builtin to use built-in version])
            else
                AC_MSG_WARN([system tiff library not found, will use built-in instead])
                wxUSE_LIBTIFF=builtin
            fi
        else
            dnl we are using the system library
            wxUSE_LIBTIFF=sys
        fi
    fi
fi

dnl ------------------------------------------------------------------------
dnl Check for expat libraries
dnl ------------------------------------------------------------------------

if test "$wxUSE_EXPAT" != "no"; then
    wxUSE_XML=yes
    AC_DEFINE(wxUSE_EXPAT)
    AC_DEFINE(wxUSE_XML)
    
    if test "$wxUSE_EXPAT" = "sys" -o "$wxUSE_EXPAT" = "yes" ; then           
        AC_CHECK_HEADER([expat.h], [found_expat_h=1])
        if test "x$found_expat_h" = "x1"; then
            dnl Expat 1.95.6 comes with broken expat.h:
            AC_CACHE_CHECK([if expat.h is valid C++ header],
                wx_cv_expat_is_not_broken,
                [
                AC_LANG_SAVE
                AC_LANG_CPLUSPLUS
                AC_TRY_COMPILE([#include <expat.h>],[],
                    wx_cv_expat_is_not_broken=yes,
                    wx_cv_expat_is_not_broken=no
                )
                AC_LANG_RESTORE
                ]
            )
            if test "$wx_cv_expat_is_not_broken" = "yes" ; then
                AC_CHECK_LIB(expat, XML_ParserCreate, EXPAT_LINK=" -lexpat")
            fi
        fi
        if test "x$EXPAT_LINK" = "x" ; then
            if test "$wxUSE_EXPAT" = "sys" ; then
                AC_MSG_ERROR([system expat library not found! Use --with-expat=builtin to use built-in version])
            else
                AC_MSG_WARN([system expat library not found, will use built-in instead])
                wxUSE_EXPAT=builtin
            fi
        else
            dnl we are using the system library
            wxUSE_EXPAT=sys
        fi
    fi
    if test "$wxUSE_EXPAT" = "builtin" ; then
        dnl Expat needs this:
        AC_CONFIG_SUBDIRS([src/expat])
    fi
fi


dnl ------------------------------------------------------------------------
dnl Check for libmspack
dnl ------------------------------------------------------------------------

if test "$wxUSE_LIBMSPACK" != "no"; then
    
    AC_CHECK_HEADER([mspack.h], [found_mspack_h=1])
    if test "x$found_mspack_h" = "x1"; then
        AC_CHECK_LIB(mspack, mspack_create_chm_decompressor,
                     MSPACK_LINK=" -lmspack")
    fi
    if test "x$MSPACK_LINK" = "x" ; then
        wxUSE_LIBMSPACK=no
    else
        AC_DEFINE(wxUSE_LIBMSPACK)
    fi
fi


dnl ----------------------------------------------------------------
dnl search for toolkit (widget sets)
dnl ----------------------------------------------------------------

AFMINSTALL=
WIN32INSTALL=

TOOLKIT=
TOOLKIT_INCLUDE=
WIDGET_SET=

dnl are we building for a win32 target environment?
dnl If so, setup common stuff needed for both GUI and Base libs.
if test "$USE_WIN32" = 1 ; then
    AC_CHECK_HEADERS(w32api.h)
    AC_CHECK_HEADER(windows.h, [],
    [
        AC_MSG_ERROR(please set CFLAGS to contain the location of windows.h)
    ])

    dnl --- FIXME: This is still a somewhat random list of libs,
    dnl ---        some of them should probably be included conditionally.
    LIBS="$LIBS -lwinspool -lwinmm -lshell32 -lcomctl32 -lcomdlg32 -lctl3d32 -ladvapi32 -lwsock32 -lgdi32"

    if test "$wxUSE_ACCESSIBILITY" = "yes" ; then
        LIBS="$LIBS -loleacc"
    fi

    case "${host}" in
        *-*-cygwin* )
            dnl Cygwin doesn't include these by default
            LIBS="$LIBS -lkernel32 -luser32"
            TOOLCHAIN_DEFS="${TOOLCHAIN_DEFS} -D__WIN95__"
    esac

    dnl add extra odbc libs if we have compiled in odbc
    if test "$wxUSE_ODBC" = "sys" ; then
        wxUSE_ODBC = "yes"
    fi
    if test "$wxUSE_ODBC" = "yes" ; then
        LIBS=" -lodbc32 -lole32 -loleaut32 $LIBS"
        AC_DEFINE(wxUSE_ODBC)
    fi
    dnl We might want to abort here if wxUSE_ODBC="builtin" isn't supported on msw.

    RESFLAGS="--include-dir \$(top_srcdir)/include --include-dir \$(top_srcdir)/\$(program_dir) --define __WIN32__ --define __WIN95__ --define __GNUWIN32__"
    RESPROGRAMOBJ="\$(PROGRAM)_resources.o"

    dnl install Win32-specific files in "make install"
    WIN32INSTALL=win32install
fi

if test "$wxUSE_GUI" = "yes"; then
    USE_GUI=1

    GUI_TK_LIBRARY=

    WXGTK12=
    WXGTK127=
    WXGTK20=
    WXGPE=

    if test "$wxUSE_COCOA" = 1 ; then
        if test "$wxUSE_PRINTING_ARCHITECTURE" = "yes"; then
            AC_MSG_WARN([Printing not supported under wxCocoa yet, disabled])
            wxUSE_PRINTING_ARCHITECTURE=no
        fi
        if test "$wxUSE_DRAG_AND_DROP" = "yes"; then
            AC_MSG_WARN([Drag and Drop not supported under wxCocoa yet, disabled])
            wxUSE_DRAG_AND_DROP=no
        fi
        if test "$wxUSE_DRAGIMAGE" = "yes"; then
            AC_MSG_WARN([Drag Image and DandD not supported under wxCocoa yet, disabled])
            wxUSE_DRAGIMAGE=no
        fi
    fi

    if test "$wxUSE_MSW" = 1 ; then
        TOOLKIT=MSW
        GUIDIST=MSW_DIST

        dnl -mwindows causes a heap of other default gui libs to be linked in.
        case "${host}" in
            *-*-mingw32* )
                WXCONFIG_LDFLAGS_GUI="$LDFLAGS -Wl,--subsystem,windows -mwindows"
        esac
    fi

    if test "$wxUSE_GTK" = 1; then
        AC_MSG_CHECKING([for GTK+ version])

        gtk_version_cached=1
        AC_CACHE_VAL(wx_cv_lib_gtk,
            [
                dnl stupid GTK+ AM macros produce their own messages, so we
                dnl have to pass to the next line
                gtk_version_cached=0
                AC_MSG_RESULT()

                dnl we must link against lgthread unless the user
                dnl used --disable-threads
                GTK_MODULES=
                if test "$wxUSE_THREADS" = "yes"; then
                    GTK_MODULES=gthread
                fi

                dnl note that if we fail to find GTK2 we abort and don't fall
                dnl back to GTK1.x
                wx_cv_lib_gtk=
                if test "x$wxUSE_GTK2" = "xyes"; then
                    AM_PATH_GTK_2_0(2.0.0, wx_cv_lib_gtk=2.0, , $GTK_MODULES)
                else
                    AM_PATH_GTK(1.2.7, wx_cv_lib_gtk=1.2.7, , $GTK_MODULES)

                    if test -z "$wx_cv_lib_gtk"; then
                        AM_PATH_GTK(1.2.3, wx_cv_lib_gtk=1.2.3, , $GTK_MODULES)
                    fi
                fi

                if test -z "$wx_cv_lib_gtk"; then
                    dnl looks better in AC_MSG_RESULT
                    wx_cv_lib_gtk=none
                else
                    dnl we need to cache GTK_CFLAGS and GTK_LIBS for the
                    dnl subsequent runs
                    wx_cv_cflags_gtk=$GTK_CFLAGS
                    wx_cv_libs_gtk=$GTK_LIBS
                fi
            ]
        )

        dnl if it wasn't cached, the messages from AM_PATH_GTK() above are
        dnl enough
        if test "$gtk_version_cached" = 1; then
            AC_MSG_RESULT($wx_cv_lib_gtk)
        fi

        case "$wx_cv_lib_gtk" in
            2.0)    WXGTK20=1
                    TOOLKIT_VERSION=2
                    ;;
            1.2.7)  WXGTK127=1
                    WXGTK12=1
                    ;;
            1.2.3)  WXGTK12=1
                    ;;
            *)      AC_MSG_ERROR([
Please check that gtk-config is in path, the directory
where GTK+ libraries are installed (returned by
'gtk-config --libs' command) is in LD_LIBRARY_PATH or
equivalent variable and GTK+ is version 1.2.3 or above.
                            ])
                    ;;
        esac

        if test "$WXGTK20" = 1; then
            save_CFLAGS="$CFLAGS"
            save_LIBS="$LIBS"
            CFLAGS="$CFLAGS $GTK_CFLAGS"
            LIBS="$LIBS $GTK_LIBS"
            AC_CHECK_FUNCS([pango_font_family_is_monospace])
            CFLAGS="$save_CFLAGS"
            LIBS="$save_LIBS"
        else
            if test "$wxUSE_UNICODE" = "yes"; then
                AC_MSG_WARN([Unicode configuration not supported with GTK+ 1.x])
                wxUSE_UNICODE=no
            fi
        fi

        dnl we need poll() in src/gtk/app.cpp (we know that Darwin doesn't
        dnl have it but we do the check for the others)
        if test "$USE_DARWIN" != 1; then
            AC_CHECK_FUNCS(poll)
        fi

        TOOLKIT_INCLUDE="$wx_cv_cflags_gtk"
        GUI_TK_LIBRARY="$wx_cv_libs_gtk $GUI_TK_LIBRARY"

        AFMINSTALL=afminstall
        TOOLKIT=GTK
        GUIDIST=GTK_DIST

        dnl test for XIM support in libgdk
        AC_CHECK_LIB(gdk, gdk_im_open, AC_DEFINE(HAVE_XIM))
        
        
        dnl test for external libxpm if we're configured to use it
        if test "$wxUSE_GPE" = "yes"; then
            AC_MSG_CHECKING(for gpewidget library)
            WX_PATH_FIND_LIBRARIES($SEARCH_LIB,gpewidget)
            if test "$ac_find_libraries" != "" ; then
                WX_LINK_PATH_EXIST($ac_find_libraries,$GUI_TK_LIBRARY)
                dnl -lgpewidget must be before all GTK libs and
                dnl we guess its path from the prefix
                GUI_TK_LIBRARY="-L${prefix}/lib -lgpewidget $GUI_TK_LIBRARY"
                WXGPE=1
                AC_MSG_RESULT(found at $ac_find_libraries)
            else
                AC_MSG_RESULT(not found)
            fi

            dnl AC_MSG_CHECKING(for gpe library)
            dnl WX_PATH_FIND_LIBRARIES($SEARCH_LIB,gpe)
            dnl if test "$ac_find_libraries" != "" ; then
            dnl     WX_LINK_PATH_EXIST($ac_find_libraries,$GUI_TK_LIBRARY)
            dnl     GUI_TK_LIBRARY="$GUI_TK_LIBRARY -lgpe"
            dnl     AC_MSG_RESULT(found at $ac_find_libraries)
            dnl else
            dnl     AC_MSG_RESULT(not found)
            dnl fi
        fi
    fi

    if test "$wxUSE_MGL" = 1; then
       AC_MSG_CHECKING(for SciTech MGL library)
        if test "x$MGL_ROOT" = x ; then
            AC_MSG_RESULT(not found)
            AC_MSG_ERROR([Cannot find MGL library. Make sure MGL_ROOT is set.])
        else
            AC_MSG_RESULT($MGL_ROOT)
        fi

        AC_MSG_CHECKING(for libmgl location)
        dnl Find MGL library that we want
        dnl FIXME_MGL - test for MGL variants for freebsd etc.;
        dnl             and for non-x86 versions
        case "${host}" in
            *-*-linux* )
              dnl glibc.so, glibc are for older versions of MGL,
              dnl x86/a, x86/so are used by >= 5.0 R11
              if test "x$wxUSE_SHARED" = xyes ; then
                  mgl_os_candidates="linux/gcc/x86/so linux/gcc/x86/a linux/gcc/glibc.so linux/gcc/glibc"
              else
                  mgl_os_candidates="linux/gcc/x86/a linux/gcc/x86/so linux/gcc/glibc linux/gcc/glibc.so"
              fi
              ;;
            *-pc-msdosdjgpp )
              mgl_os_candidates="dos32/dj2"
              ;;
            *)
              AC_MSG_ERROR(This system type ${host} is not yet supported by wxMGL.)
        esac

        mgl_lib_type=""
        mgl_os=""

        for mgl_os_i in $mgl_os_candidates ; do
            if test "x$mgl_os" = x ; then
                if test "$wxUSE_DEBUG_FLAG" = yes ; then
                    if test -f $MGL_ROOT/lib/debug/$mgl_os_i/libmgl.a -o \
                            -f $MGL_ROOT/lib/debug/$mgl_os_i/libmgl.so; then
                        mgl_lib_type=debug
                        mgl_os=$mgl_os_i
                    fi
                fi
                if test "x$mgl_lib_type" = x ; then
                    if test -f $MGL_ROOT/lib/release/$mgl_os_i/libmgl.a -o \
                            -f $MGL_ROOT/lib/release/$mgl_os_i/libmgl.so; then
                        mgl_lib_type=release
                        mgl_os=$mgl_os_i
                    fi
                fi
            fi
        done

        if test "x$mgl_os" = x ; then
            AC_MSG_RESULT(not found)
            AC_MSG_ERROR([Cannot find MGL libraries, make sure they are compiled.])
        fi
        AC_MSG_RESULT("$MGL_ROOT/lib/$mgl_lib_type/$mgl_os")

        wxUSE_UNIVERSAL="yes"

        TOOLKIT_INCLUDE="-I$MGL_ROOT/include"
        GUI_TK_LIBRARY="-L$MGL_ROOT/lib/$mgl_lib_type/$mgl_os -lmgl -lmglcpp -lpm"

        AFMINSTALL=afminstall
        TOOLKIT=MGL
        GUIDIST=MGL_DIST
    fi

    if test "$wxUSE_MICROWIN" = 1; then
        AC_MSG_CHECKING(for MicroWindows)
        if test "x$MICROWINDOWS" = x ; then
            AC_MSG_RESULT(not found)
            AC_MSG_ERROR([Cannot find MicroWindows library. Make sure MICROWINDOWS is set.])
        else
            AC_MSG_RESULT($MICROWINDOWS)
        fi

        if test -f $MICROWINDOWS/lib/libmwin.a; then
            AC_MSG_RESULT(MicroWindows' libraries found.)
        else
            AC_MSG_ERROR([Cannot find MicroWindows libraries, make sure they are compiled.])
        fi

        TOOLKIT_INCLUDE="-I$MICROWINDOWS/include"
        GUI_TK_LIBRARY="-L$MICROWINDOWS/lib -lmwin -lmwengine -mwfonts -mwdrivers -mwinlib"

        wxUSE_UNIVERSAL="yes"

        AFMINSTALL=afminstall
        TOOLKIT=MICROWIN
        GUIDIST=MICROWIN_DIST

        TOOLCHAIN_DEFS="${TOOLCHAIN_DEFS} -D__WXMSW__ -D__WIN95__ -D__WIN32__ -DMWIN -DMICROWIN_NOCONTROLS -DMICROWIN_TODO=1"
    fi

    dnl common part of X11 and Motif port checks
    if test "$wxUSE_X11" = 1 -o "$wxUSE_MOTIF" = 1; then
        dnl use standard macros to check for X headers/libs, this brings
        dnl support for the standard configure options --x-includes,
        dnl --x-libraries and --no-x
        AC_PATH_XTRA

        if test "$no_x" = "yes"; then
          AC_MSG_ERROR(X11 not found, please use --x-includes and/or --x-libraries options)
        fi

        dnl for some reason AC_PATH_XTRA seems to add -INONE and -LNONE (and
        dnl also sometimes -RNONE) to X_CFLAGS and X_LIBS respectively, filter
        dnl this junk out
        GUI_TK_LIBRARY=`echo $X_LIBS | sed 's/ -LNONE//' | sed 's/ -RNONE//'`
        TOOLKIT_INCLUDE=`echo $X_CFLAGS | sed 's/ -INONE//'`
        AFMINSTALL=afminstall
        COMPILED_X_PROGRAM=0

    fi

    if test "$wxUSE_X11" = 1; then
        if test "$wxUSE_NANOX" = "yes"; then
            AC_MSG_CHECKING(for MicroWindows/NanoX distribution)
            if test "x$MICROWIN" = x ; then
                AC_MSG_RESULT(not found)
                AC_MSG_ERROR([Cannot find MicroWindows library. Make sure MICROWIN is set.])
            else
                AC_MSG_RESULT($MICROWIN)
                AC_DEFINE(wxUSE_NANOX)
            fi
        fi

        if test "$wxUSE_UNICODE" = "yes"; then
                PKG_CHECK_MODULES(PANGOX, pangox,
                    [
                        CXXFLAGS="$CXXFLAGS $PANGOX_CFLAGS"
                        GUI_TK_LIBRARY="$GUI_TK_LIBRARY $PANGOX_LIBS"
                    ],
                    [
                        AC_MSG_ERROR([pangox library not found, library cannot be compiled in Unicode mode])
                    ]
                )
                PKG_CHECK_MODULES(PANGOFT2, pangoft2,
                    [
                        CXXFLAGS="$CXXFLAGS $PANGOFT2_CFLAGS"
                        GUI_TK_LIBRARY="$GUI_TK_LIBRARY $PANGOFT2_LIBS"
                    ],
                    [
                        AC_MSG_WARN([pangoft2 library not found, library will be compiled without printing support])
                        wxUSE_PRINTING_ARCHITECTURE="no"
                    ]
                )
                PKG_CHECK_MODULES(PANGOXFT, pangoxft,
                    [
                        CXXFLAGS="$CXXFLAGS $PANGOXFT_CFLAGS"
                        GUI_TK_LIBRARY="$GUI_TK_LIBRARY $PANGOXFT_LIBS"
                    ],
                    [
                        AC_MSG_WARN([pangoxft library not found, library will be compiled without anti-aliasing support])
                    ]
                )
                save_CFLAGS="$CFLAGS"
                save_LIBS="$LIBS"
                CFLAGS="$CFLAGS $PANGOX_CFLAGS"
                LIBS="$LIBS $PANGOX_LIBS"
                AC_CHECK_FUNCS([pango_font_family_is_monospace])
                CFLAGS="$save_CFLAGS"
                LIBS="$save_LIBS"
        fi

        wxUSE_UNIVERSAL="yes"

        if test "$wxUSE_NANOX" = "yes"; then
            TOOLKIT_INCLUDE="-I\$(top_srcdir)/include/wx/x11/nanox -I\$(MICROWIN)/src/include $TOOLKIT_INCLUDE"
            TOOLCHAIN_DEFS="${TOOLCHAIN_DEFS} -D__NANOX__ -DMWPIXEL_FORMAT=MWPF_TRUECOLOR0888 -DHAVE_FILEIO -DHAVE_BMP_SUPPORT=1 -DHAVE_GIF_SUPPORT=1 -DHAVE_PNM_SUPPORT=1 -DHAVE_XPM_SUPPORT=1 -DUNIX=1 -DUSE_EXPOSURE -DSCREEN_HEIGHT=480 -DSCREEN_WIDTH=640 -DSCREEN_DEPTH=4 -DX11=1"
            GUI_TK_LIBRARY="$GUI_TK_LIBRARY \$(MICROWIN)/src/lib/libnano-X.a"
        else
            GUI_TK_LIBRARY="$GUI_TK_LIBRARY -lX11"
        fi

        TOOLKIT=X11
        GUIDIST=X11_DIST
    fi

    if test "$wxUSE_MOTIF" = 1; then
        AC_MSG_CHECKING(for Motif/Lesstif headers)
        WX_PATH_FIND_INCLUDES($SEARCH_INCLUDE, Xm/Xm.h)
        if test "$ac_find_includes" != "" ; then
            AC_MSG_RESULT(found in $ac_find_includes)
            WX_INCLUDE_PATH_EXIST($ac_find_includes, $TOOLKIT_INCLUDE)
            TOOLKIT_INCLUDE="$TOOLKIT_INCLUDE$ac_path_to_include"
        else
            AC_TRY_COMPILE(
                [
                    #include <Xm/Xm.h>
                ],
                [
                    int version;
                    version = xmUseVersion;
                ],
                [
                    AC_MSG_RESULT(found in default search path)
                    COMPILED_X_PROGRAM=1
                ],
                [
                    AC_MSG_RESULT(no)
                    AC_MSG_ERROR(please set CFLAGS to contain the location of Xm/Xm.h)
                ]
            )
        fi

        if test "$COMPILED_X_PROGRAM" = 0; then
            AC_MSG_CHECKING(for Motif/Lesstif library)
            WX_PATH_FIND_LIBRARIES($SEARCH_LIB, Xm)
            if test "$ac_find_libraries" != "" ; then
                AC_MSG_RESULT(found at $ac_find_libraries)

                WX_LINK_PATH_EXIST($ac_find_libraries, $GUI_TK_LIBRARY)
                GUI_TK_LIBRARY="$GUI_TK_LIBRARY$ac_path_to_link"
            else
                dnl it might happen that we found headers in one of the
                dnl standard paths but the libs are elsewhere - we do need to
                dnl try to compile a sample program then here
                save_CFLAGS=$CFLAGS
                CFLAGS="$CFLAGS $TOOLKIT_INCLUDE"

                AC_TRY_COMPILE(
                    [
                        #include <Xm/Xm.h>
                    ],
                    [
                        int version;
                        version = xmUseVersion;
                    ],
                    [
                        AC_MSG_RESULT(found in default search path)
                        COMPILED_X_PROGRAM=1
                    ],
                    [
                        AC_MSG_RESULT(no)
                        AC_MSG_ERROR(please set LDFLAGS to contain the location of libXm)
                    ]
                )

                CFLAGS=$save_CFLAGS
            fi
        fi

        AC_MSG_CHECKING([if we need -lXp and/or -lSM -lICE])
        libp_link=""
        libsm_ice_link=""
        libs_found=0
        for libp in "" " -lXp"; do
            if test "$libs_found" = "0"; then
                for libsm_ice in "" " -lSM -lICE"; do
                    if test "$libs_found" = "0"; then
                        save_LIBS="$LIBS"
                        LIBS="$GUI_TK_LIBRARY -lXm ${libp} -lXmu -lXext -lXt${libsm_ice} -lX11"
                        save_CFLAGS=$CFLAGS
                        CFLAGS="$CFLAGS $TOOLKIT_INCLUDE"

                        AC_TRY_LINK(
                            [
                                #include <Xm/Xm.h>
                                #include <Xm/List.h>
                            ],
                            [
                                XmString string = NULL;
                                Widget w = NULL;
                                int position = 0;
                                XmListAddItem(w, string, position);
                            ],
                            [
                                libp_link="$libp"
                                libsm_ice_link="$libsm_ice"
                                AC_MSG_RESULT(
                                    [need${libp_link}${libsm_ice_link}])
                                libs_found=1
                            ], []
                        )

                        LIBS="$save_LIBS"
                        CFLAGS=$save_CFLAGS
                    fi
                done
            fi
        done

        if test "$libs_found" = "0"; then
            AC_MSG_RESULT([can't find the right libraries])
            AC_MSG_ERROR([can't link a simple motif program])
        fi

        save_CFLAGS=$CFLAGS
        CFLAGS="$CFLAGS $TOOLKIT_INCLUDE"
        AC_MSG_CHECKING([for Motif 2])
        AC_TRY_COMPILE([
                        #include <Xm/Xm.h>
                       ],
                       [
                        #if XmVersion < 2000
                        #error "Not Motif 2"
                        #endif
                       ],
                       [
                        AC_DEFINE(__WXMOTIF20__,1)
                        AC_MSG_RESULT([found])
                       ],
                       [
                        AC_DEFINE(__WXMOTIF20__,0)
                        AC_MSG_RESULT([not found])
                       ])
        CFLAGS=$save_CFLAGS

        GUI_TK_LIBRARY="$GUI_TK_LIBRARY -lXm${libp_link} -lXmu -lXext -lXt${libsm_ice_link} -lX11"
        TOOLKIT=MOTIF
        GUIDIST=MOTIF_DIST
    fi

    dnl more tests common to X11 and Motif:
    if test "$wxUSE_X11" = 1 -o "$wxUSE_MOTIF" = 1; then
        dnl test for external libxpm if we're configured to use it
        if test "$wxUSE_LIBXPM" = "sys"; then
            AC_MSG_CHECKING(for Xpm library)
            WX_PATH_FIND_LIBRARIES($SEARCH_LIB,Xpm)
            if test "$ac_find_libraries" != "" ; then
                WX_LINK_PATH_EXIST($ac_find_libraries,$GUI_TK_LIBRARY)
                GUI_TK_LIBRARY="$GUI_TK_LIBRARY$ac_path_to_link"
                AC_MSG_RESULT(found at $ac_find_libraries)

                AC_CACHE_CHECK([for X11/xpm.h],
                    wx_cv_x11_xpm_h,
                    [
                        save_CFLAGS=$CFLAGS
                        CFLAGS="$CFLAGS $TOOLKIT_INCLUDE"

                        AC_TRY_COMPILE(
                            [
                                #include <X11/xpm.h>
                            ],
                            [
                                int version;
                                version = XpmLibraryVersion();
                            ],
                            wx_cv_x11_xpm_h=yes,
                            wx_cv_x11_xpm_h=no
                        )

                        CFLAGS=$save_CFLAGS
                    ]
                )

                if test $wx_cv_x11_xpm_h = "yes"; then
                    GUI_TK_LIBRARY="$GUI_TK_LIBRARY -lXpm"
                    AC_DEFINE(wxHAVE_LIB_XPM)
                else
                    AC_MSG_WARN([built-in less efficient XPM decoder will be used])
                fi
            fi

        fi

        dnl XShapeQueryExtension checks: first the library, then prototype
        AC_CHECK_LIB([Xext], [XShapeQueryExtension],
                     [
                      GUI_TK_LIBRARY="$GUI_TK_LIBRARY -lXext"
                      wxHAVE_XEXT_LIB=1
                     ],
                     [], [$GUI_TK_LIBRARY -lX11])

        if test "$wxHAVE_XEXT_LIB" = 1; then
            save_CFLAGS="$CFLAGS"
            CFLAGS="$CFLAGS $TOOLKIT_INCLUDE"

            AC_MSG_CHECKING([for X11/extensions/shape.h])
            AC_TRY_COMPILE([
                            #include <X11/Xlib.h> 
                            #include <X11/extensions/shape.h>
                           ],
                           [
                            int dummy1, dummy2;
                            XShapeQueryExtension((Display*)NULL,
                                                 (int*)NULL, (int*)NULL);
                           ],
                           [
                            AC_DEFINE(HAVE_XSHAPE)
                            AC_MSG_RESULT([found])
                           ],
                           [
                            AC_MSG_RESULT([not found])
                           ])
            CFLAGS="$save_CFLAGS"

        fi
    fi

    if test "$wxUSE_MAC" = 1; then
        AC_MSG_CHECKING([for compiler syntax to enable Pascal strings])
        if test "$GCC" = yes; then
            AC_MSG_RESULT([gcc])
            CPPFLAGS_PASCAL="-fpascal-strings"
        elif test "`echo $CXX | sed -e 's@.*/@@'`" = "xlC"; then
            AC_MSG_RESULT([xlc])
            CPPFLAGS_PASCAL="-qmacpstr"
        else
            AC_MSG_RESULT([none])
        fi

        CPPFLAGS="$CPPFLAGS $CPPFLAGS_PASCAL -I\${top_srcdir}/src/mac/carbon/morefilex -I/Developer/Headers/FlatCarbon"

        TOOLKIT=MAC
        dnl we can't call this MAC_DIST or autoconf thinks its a macro
        GUIDIST=MACX_DIST
        dnl wxMac version of wxBase and wxCocoa or wxBase-only built on Darwin
        dnl are different, so they need different names:
        WXBASEPORT="_carbon"
    fi

    if test "$wxUSE_COCOA" = 1; then
        TOOLKIT=COCOA
        GUIDIST=COCOA_DIST
    fi

    if test "$wxUSE_PM" = 1; then
        TOOLKIT=PM
        GUIDIST=PM_DIST
        AC_CACHE_CHECK([for type SPBCDATA],
            wx_cv_spbcdata,
            [
                AC_TRY_COMPILE(
                    [
                        #define INCL_PM
                        #include <os2.h>
                    ],
                    [
                        SPBCDATA test;
                    ],
                    wx_cv_spbcdata=yes,
                    wx_cv_spbcdata=no
                )
            ]
        )

        if test $wx_cv_spbcdata = "yes"; then
            AC_DEFINE(HAVE_SPBCDATA)
        fi
    fi

    dnl the name of the directory where the files for this toolkit live
    if test "$TOOLKIT" = "PM" ; then
        TOOLKIT_DIR="os2"
    else
        TOOLKIT_DIR=`echo ${TOOLKIT} | tr "[[A-Z]]" "[[a-z]]"`
    fi

    if test "$wxUSE_UNIVERSAL" = "yes"; then
        TOOLCHAIN_DEFS="${TOOLCHAIN_DEFS} -D__WXUNIVERSAL__"
        WIDGET_SET=univ
    fi

    dnl distribute samples/demos/utils with GUI versions
    GUIDIST="${GUIDIST} SAMPLES_DIST DEMOS_DIST UTILS_DIST MISC_DIST"
    DISTDIR="wx\$(TOOLKIT)"
else
    USE_GUI=0

    dnl this doesn't quite work right for wxBase, but the places
    dnl where it is wrong aren't fatal (yet) though.
    TOOLKIT_DIR="base"

    dnl the sources, their dependenices and the headers
    if test "$USE_WIN32" = 1 ; then
        dnl yes, the toolkit for wxBase on win32 is actually MSW
        dnl wxBase on unix does not need a 'TOOLKIT' defined.
        TOOLKIT="MSW"
    fi

    dnl distribute only wxBase sources/headers
    GUIDIST="BASE_DIST"
    DISTDIR="wxBase"
fi

dnl ---------------------------------------------------------------------------
dnl Optional libraries included when system library is not used
dnl ---------------------------------------------------------------------------


dnl ----------------------------------------------------------------
dnl iODBC support
dnl ----------------------------------------------------------------

IODBC_C_SRC=""


dnl ODBC is handled seperately for MSW 
if test "$TOOLKIT" != "MSW" ; then

    if test "$wxUSE_ODBC" = "sys" -o "$wxUSE_ODBC" = "yes" ; then
        dnl This is not ideal we really ough to use the unixodbc-config
        dnl  or iodbc-config if they exist.

        AC_CHECK_HEADER([sql.h], [found_sql_h=1])
        if test "x$found_sql_h" = "x1" ; then
            AC_CHECK_LIB(iodbc, SQLAllocEnv, ODBC_LINK=" -liodbc",
            [
                AC_CHECK_LIB(unixodbc, SQLAllocEnv, ODBC_LINK=" -lunixodbc",
                [
                    AC_CHECK_LIB(odbc, SQLAllocEnv, ODBC_LINK=" -lodbc")
                ])
            ])
        fi 
        if test "x$ODBC_LINK" = "x" ; then
            if test "$wxUSE_ODBC" = "sys" ; then
                AC_MSG_ERROR([system ODBC library not found! Use --with-odbc=builtin to use built-in version])
            else
                AC_MSG_WARN([system ODBC library not found, will use built-in instead])
                wxUSE_ODBC=builtin
            fi
        else
            dnl we are using the system library
            wxUSE_ODBC=sys
        fi
    fi
    if test "$wxUSE_ODBC" != "no" ; then
        AC_DEFINE(wxUSE_ODBC)
        if test "$wxUSE_ODBC" = "builtin" ; then
            AC_DEFINE(wxUSE_BUILTIN_IODBC)
        fi
        SAMPLES_SUBDIRS="$SAMPLES_SUBDIRS db"
    
        dnl is this still necessary in 2.5?
        WXODBCFLAG="-D_IODBC_"
    fi
fi


dnl ---------------------------------------------------------------------------
dnl wxDisplay Sanity checks
dnl ---------------------------------------------------------------------------

if test "$wxUSE_DISPLAY" = "yes"; then
dnl ---------------------------------------------------------------------------
dnl Xinerama (for unix ) - Brian Victor
dnl ---------------------------------------------------------------------------
    if test "$wxUSE_UNIX" = "yes" -a "$wxUSE_MAC" != 1 -a "$wxUSE_COCOA" != 1; then
        AC_MSG_CHECKING([for Xinerama])
        WX_PATH_FIND_LIBRARIES([$SEARCH_LIB],Xinerama)
        if test "$ac_find_libraries" != "" ; then
            WX_LINK_PATH_EXIST([$ac_find_libraries],[$LDFLAGS])
            if test "$ac_path_to_link" != " -L/usr/lib" ; then
                LDFLAGS="$LDFLAGS $ac_path_to_link"
            fi
            GUI_TK_LIBRARY="$GUI_TK_LIBRARY -lXinerama"
            AC_MSG_RESULT([yes])

            AC_MSG_CHECKING([for Xxf86vm extension])
            WX_PATH_FIND_LIBRARIES([$SEARCH_LIB],Xxf86vm)
            if test "$ac_find_libraries" != "" ; then
                AC_MSG_RESULT([yes])
                AC_CHECK_HEADERS([X11/extensions/xf86vmode.h],
                                 [
                                  GUI_TK_LIBRARY="$GUI_TK_LIBRARY -lXxf86vm"
                                 ],
                                 [], 
                                 [
                                      #if HAVE_X11_XLIB_H
                                        #include <X11/Xlib.h>
                                      #endif
                                 ])
            else
                AC_MSG_RESULT([no])
            fi
        
        else
            AC_MSG_RESULT([no])
            AC_MSG_WARN([Xinerama not found; disabling wxDisplay])
            wxUSE_DISPLAY="no"
        fi
    elif test "$wxUSE_MSW" = 1; then
dnl ---------------------------------------------------------------------------
dnl DirectDraw  / Multimon for MSW
dnl ---------------------------------------------------------------------------
        AC_CHECK_HEADERS([multimon.h ddraw.h], [], [
            wxUSE_DISPLAY="no"
            AC_MSG_WARN([ddraw.h or multimon.h not found; disabling wxDisplay])
          ] )
    fi
fi

dnl ---------------------------------------------------------------------------
dnl wxMediaCtrl 
dnl ---------------------------------------------------------------------------

if test "$wxUSE_MEDIACTRL" = "yes"; then
    if test "$wxUSE_MSW" = 1; then
dnl ---------------------------------------------------------------------------
dnl DirectShow MSW
dnl ---------------------------------------------------------------------------
        wxUSE_DIRECTSHOW="yes"
        AC_CHECK_HEADERS([dshow.h], [], [
            wxUSE_DIRECTSHOW="no"
            AC_MSG_WARN([DirectShow not installed; consider installing the DirectX7 SDK or higher])
          ] )

       if test "$wxUSE_DIRECTSHOW" = "yes"; then
          AC_DEFINE(wxUSE_DIRECTSHOW)
	    LIBS="$LIBS -lstrmiids"          
       fi
    fi
    
    AC_DEFINE(wxUSE_MEDIACTRL)
fi

dnl ---------------------------------------------------------------------------
dnl OpenGL libraries
dnl ---------------------------------------------------------------------------

USE_OPENGL=0
if test "$wxUSE_OPENGL" = "yes"; then
    if test "$wxUSE_MAC" = 1 -o "$wxUSE_COCOA" = "1"; then
        OPENGL_LIBS="-framework OpenGL -framework AGL"
    elif test "$wxUSE_MSW" = 1; then
        OPENGL_LIBS="-lopengl32 -lglu32"
    else
        dnl David Elliott: Without this snippet AC_CHECK_HEADER fails to find
        dnl GL/gl.h on Mac OS X where it is located in
        dnl /usr/X11R6/include/GL/gl.h:

        WX_PATH_FIND_INCLUDES($SEARCH_INCLUDE, GL/gl.h)
        if test "$ac_find_includes" != "" ; then
            AC_MSG_RESULT(found in $ac_find_includes)
            WX_INCLUDE_PATH_EXIST($ac_find_includes, $CPPFLAGS)
            CPPFLAGS="$CPPFLAGS$ac_path_to_include"
        fi

        AC_CHECK_HEADER(GL/gl.h,
            [
                found_gl=0

                AC_MSG_CHECKING([for -lGL])
                WX_PATH_FIND_LIBRARIES([$SEARCH_LIB],GL)
                if test "$ac_find_libraries" != "" ; then
                    WX_LINK_PATH_EXIST([$ac_find_libraries],[$LDFLAGS])
                    if test "$ac_path_to_link" != " -L/usr/lib" ; then
                        LDFLAGS_GL="$ac_path_to_link"
                    fi

                    dnl don't suppose that libGL and libGLU are always in the
                    dnl same directory -- this is not true for some common
                    dnl distributions
                    WX_PATH_FIND_LIBRARIES([$SEARCH_LIB],GLU)
                    if test "$ac_find_libraries" != "" ; then
                        WX_LINK_PATH_EXIST([$ac_find_libraries],[$LDFLAGS])
                        if test "$ac_path_to_link" != " -L/usr/lib" -a \
                                    "$ac_path_to_link" != "$LDFLAGS_GL"; then
                            LDFLAGS_GL="$LDFLAGS_GL$ac_path_to_link"
                        fi

                        found_gl=1
                        OPENGL_LIBS="-lGL -lGLU"
                        AC_MSG_RESULT([yes])
                    fi
                fi

                if test "$found_gl" != 1; then
                    AC_MSG_RESULT([no])
                    AC_MSG_CHECKING([for -lMesaGL])
                    WX_PATH_FIND_LIBRARIES([$SEARCH_LIB],MesaGL)
                    if test "$ac_find_libraries" != "" ; then
                        WX_LINK_PATH_EXIST([$ac_find_libraries],[$LDFLAGS])
                        LDFLAGS_GL="$LDFLAGS$ac_path_to_link"
                        OPENGL_LIBS="-lMesaGL -lMesaGLU"
                        AC_MSG_RESULT([yes])
                    else
                        AC_MSG_RESULT([no])
                    fi
                fi
            ])

        if test "x$OPENGL_LIBS" = "x"; then
            dnl it should be an error and not a warning because OpenGL is not on
            dnl by default and so if it had been explicitely requested, we
            dnl shouldn't just fall back to compiling the library without it
            AC_MSG_ERROR(OpenGL libraries not available)
        fi
    fi

    if test "$wxUSE_OPENGL" = "yes"; then
        USE_OPENGL=1
        AC_DEFINE(wxUSE_OPENGL)
        AC_DEFINE(wxUSE_GLCANVAS)
        SAMPLES_SUBDIRS="$SAMPLES_SUBDIRS opengl opengl/cube opengl/penguin opengl/isosurf"
    fi
fi


dnl the symbol which allows conditional compilation for the given toolkit
if test -n "$TOOLKIT" ; then
    TOOLCHAIN_DEFS="${TOOLCHAIN_DEFS} -D__WX${TOOLKIT}__"
fi


dnl --- the marker for quick search, leave it here: SHARED_LIB_SETUP ---

if test "$wxUSE_SHARED" = "yes"; then
    case "${host}" in
      *-pc-msdosdjgpp )
        dnl only static for now
        wxUSE_SHARED=no
        AC_MSG_WARN([Host system doesn't support shared libraries, disabling])
      ;;
    esac
fi

if test "$wxUSE_SHARED" = "yes"; then

    dnl use versioned symbols if available on the platform
    WX_VERSIONED_SYMBOLS([\$(top_builddir)version-script])

    case "${host}" in
      *-*-linux* | *-*-gnu* )
        SAMPLES_RPATH_FLAG="-Wl,-rpath,\$(top_builddir)lib"
        WXCONFIG_RPATH="-Wl,-rpath,\$libdir"
	;;

      *-*-solaris2* )
        if test "$GCC" = yes ; then
            dnl newer versions of gcc need -isystem to compile X headers on
            dnl Solaris (which use old style C syntax)
            CPPFLAGS="$CPPFLAGS -isystem /usr/openwin/include"

            dnl gcc may use Sun's ld, in which case -rpath gives a confusing
            dnl error message. We have to try both -Wl,-rpath and -Wl,-R:
            saveLdflags="$LDFLAGS"
            LDFLAGS="$saveLdflags -Wl,-rpath,/"
            AC_MSG_CHECKING([if the linker accepts -rpath])
            AC_TRY_LINK(
                [],[],
                [
                    AC_MSG_RESULT([yes])
                    SAMPLES_RPATH_FLAG="-Wl,-rpath,\$(top_builddir)lib"
                    WXCONFIG_RPATH="-Wl,-rpath,\$libdir"
                ],[
                    AC_MSG_RESULT([no])
                    AC_MSG_CHECKING([if the linker accepts -R])
                    LDFLAGS="$saveLdflags -Wl,-R,/"
                    AC_TRY_LINK(
                        [],[],
                        [
                            AC_MSG_RESULT([yes])
                            SAMPLES_RPATH_FLAG="-Wl,-R,\$(top_builddir)lib"
                            WXCONFIG_RPATH="-Wl,-R,\$libdir"
                        ],[
                            AC_MSG_RESULT([no])
                        ])                    
                ])
            LDFLAGS="$saveLdflags"
        else
            SAMPLES_RPATH_FLAG="-R\$(top_builddir)lib"
            WXCONFIG_RPATH="-R\$libdir"
        fi
      ;;
      
      powerpc-*-darwin* )
        SAMPLES_RPATH_POSTLINK="\$(top_builddir)change-install-names \$(LIBDIRNAME) \$(prefix) \$@"
        cat <<EOF >change-install-names
#!/bin/sh
libnames=\`cd \${1} ; ls -1 | grep '\.[[0-9]][[0-9]]*\.dylib\$'\`
inst_cmd="install_name_tool "
for i in \${libnames} ; do
    inst_cmd="\${inst_cmd} -change \${2}/lib/\${i} \${1}/\${i}"
done
\${inst_cmd} \${3}
EOF
        chmod +x change-install-names
      ;;
      
      *-*-cygwin* | *-*-mingw32* )
        TOOLCHAIN_DLL_DEFS="${TOOLCHAIN_DLL_DEFS} -DWXUSINGDLL=1"
	;;
      
    esac

    if test $wxUSE_RPATH = "no"; then
        SAMPLES_RPATH_FLAG=''
        SAMPLES_RPATH_POSTLINK=''
        WXCONFIG_RPATH=''
    fi

    SHARED=1

else

    config_linkage_component="-static"
    SHARED=0

fi


UNICODE=0
lib_unicode_suffix=
WX_CHARTYPE="ansi"
if test "$wxUSE_UNICODE" = "yes"; then
    lib_unicode_suffix=u
    WX_CHARTYPE="unicode"
    UNICODE=1
fi

lib_debug_suffix=
WX_DEBUGTYPE="release"
DEBUG_FLAG=0
if test "$wxUSE_DEBUG_FLAG" = "yes"; then
    lib_debug_suffix=d
    WX_DEBUGTYPE="debug"
    DEBUG_FLAG=1
fi

WX_FLAVOUR=${WX_FLAVOUR:+-$WX_FLAVOUR}
WX_LIB_FLAVOUR=`echo $WX_FLAVOUR | tr '-' '_'`

DEBUG_INFO=0
if test "$wxUSE_DEBUG_INFO" = "yes"; then
    DEBUG_INFO=1
fi

WX_VERSION_TAG=`echo WX${lib_unicode_suffix}${lib_debug_suffix}${WX_LIB_FLAVOUR}_${WX_RELEASE} | tr "[[a-z]]" "[[A-Z]]"`

TOOLCHAIN_NAME="${TOOLKIT_DIR}${TOOLKIT_VERSION}${WIDGET_SET}${lib_unicode_suffix}${lib_debug_suffix}${WX_LIB_FLAVOUR}-${WX_RELEASE}"

TOOLCHAIN_FULLNAME="${TOOLKIT_DIR}${TOOLKIT_VERSION}${WIDGET_SET}-${WX_CHARTYPE}-${WX_DEBUGTYPE}${config_linkage_component}-${WX_RELEASE}${WX_FLAVOUR}"

if test "$cross_compiling" = "yes"; then
    HOST_SUFFIX="-$host_alias"
    TOOLCHAIN_NAME="$TOOLCHAIN_NAME$HOST_SUFFIX"
    TOOLCHAIN_FULLNAME="${host_alias}-$TOOLCHAIN_FULLNAME"
fi

dnl library link name
dnl These just save us from exporting lib_{unicode,debug,flavour}_suffix.
dnl If we ever need to do that, we won't need to keep these.
WX_LIBRARY_BASENAME_NOGUI="wx_base${WXBASEPORT}${lib_unicode_suffix}${lib_debug_suffix}${WX_LIB_FLAVOUR}"
WX_LIBRARY_BASENAME_GUI="wx_${TOOLKIT_DIR}${TOOLKIT_VERSION}${WIDGET_SET}${lib_unicode_suffix}${lib_debug_suffix}${WX_LIB_FLAVOUR}"



if test "$wxUSE_MAC" = 1 -o "$wxUSE_COCOA" = 1; then
    dnl base name of the resource file for wxMac must be the same
    dnl as library installation base name (-install_name)
    WX_RESOURCES_MACOSX_ASCII="libwx_${TOOLCHAIN_NAME}.${wx_release_number}.r"
    WX_RESOURCES_MACOSX_DATA="libwx_${TOOLCHAIN_NAME}.${wx_release_number}.rsrc"

    dnl add the resources target for wxMac
    LIBWXMACRES="\$(top_builddir)lib/${WX_RESOURCES_MACOSX_ASCII}"

    AC_CHECK_PROG(RESCOMP, Rez, Rez, /Developer/Tools/Rez)
    AC_CHECK_PROG(DEREZ, DeRez, DeRez, /Developer/Tools/DeRez)
    AC_CHECK_PROG(SETFILE, SetFile, SetFile, /Developer/Tools/SetFile)

    MACSETFILE="\$(SETFILE)"

    dnl resources are bundled both with shared library and applications
    dnl since the carb resource *must* be included in the application
    if test "$wxUSE_MAC" = 1; then
        MACRESCOMP="\$(RESCOMP) -d __DARWIN__ -t APPL Carbon.r ${LIBWXMACRES} -o"

        dnl this command is used to implement `wx-config --rezflags` and it is
        dnl eval'd there so escape any metacharacters inside
        MACRESWXCONFIG="${RESCOMP} -d __DARWIN__ -t APPL Carbon.r \\\${exec_prefix}/lib/${WX_RESOURCES_MACOSX_ASCII} -o"
    else
        MACRESCOMP="echo -n | \$(RESCOMP) -d __DARWIN__ -t APPL ${LIBWXMACRES} -o"
        MACRESWXCONFIG="echo -n \\\| ${RESCOMP} -d __DARWIN__ -t APPL \\\${exec_prefix}/lib/${WX_RESOURCES_MACOSX_ASCII} -o"
    fi
else
    dnl default value is to (silently) do nothing in the makefile
    MACSETFILE="@true"
    MACRESWXCONFIG="@true"

    dnl confusingly, wxOS2 also uses MACRESCOMP in its post-link step
    if test "$wxUSE_PM" = 1; then
        MACRESCOMP="emxbind -ep"
    else
        MACRESCOMP="@true"
    fi
fi


dnl ---------------------------------------------------------------------------
dnl Checks for typedefs
dnl ---------------------------------------------------------------------------

dnl   defines mode_t if not already defined
AC_TYPE_MODE_T
dnl   defines off_t if not already defined
AC_TYPE_OFF_T
dnl   defines pid_t if not already defined
AC_TYPE_PID_T
dnl   defines size_t if not already defined
AC_TYPE_SIZE_T
dnl   defines uid_t and gid_t if not already defined
AC_TYPE_UID_T

dnl sets HAVE_SSIZE_T if ssize_t is defined
AC_CHECK_TYPES(ssize_t)

dnl check what exactly size_t is on this machine - this is necessary to avoid
dnl ambiguous overloads in several places, notably wx/string.h and wx/array.h
AC_LANG_SAVE
AC_LANG_CPLUSPLUS
AC_CACHE_CHECK([if size_t is unsigned int],
    wx_cv_size_t_is_uint,
    [
    dnl an obvious check like AC_TRY_COMPILE[struct Foo { ... };] doesn't work
    dnl with egcs (at least) up to 1.1.1 as it allows you to compile duplicate
    dnl methods in a local class (i.e. class inside a function) declaration
    dnl without any objections!!
    dnl
    dnl hence the hack below: we must have Foo at global scope!
    AC_TRY_COMPILE([#include <stddef.h>],
        [
            return 0; }

            struct Foo { void foo(size_t); void foo(unsigned int); };

            int bar() {
        ],
        wx_cv_size_t_is_uint=no,
        wx_cv_size_t_is_uint=yes
    )
    ]
)

if test "$wx_cv_size_t_is_uint" = "yes"; then
    AC_DEFINE(wxSIZE_T_IS_UINT)
else
    AC_CACHE_CHECK([if size_t is unsigned long],
        wx_cv_size_t_is_ulong,
        AC_TRY_COMPILE([#include <stddef.h>],
            [
                return 0; }

                struct Foo { void foo(size_t); void foo(unsigned long); };

                int bar() {
            ],
            wx_cv_size_t_is_ulong=no,
            wx_cv_size_t_is_ulong=yes
        )
    )

    if test "$wx_cv_size_t_is_ulong" = "yes"; then
        AC_DEFINE(wxSIZE_T_IS_ULONG)
    fi
fi

AC_LANG_RESTORE

dnl ---------------------------------------------------------------------------
dnl Checks for structures
dnl ---------------------------------------------------------------------------

dnl does passwd struct has the pw_gecos field?
AC_CACHE_CHECK([for pw_gecos in struct passwd], wx_cv_struct_pw_gecos,
    [
        AC_TRY_COMPILE([#include <pwd.h>],
             [
                char *p;
                struct passwd *pw;
                p = pw->pw_gecos;
             ],
             [
                wx_cv_struct_pw_gecos=yes
             ],
             [
                wx_cv_struct_pw_gecos=no
             ]
        )
    ]
)

if test "$wx_cv_struct_pw_gecos" = "yes"; then
    AC_DEFINE(HAVE_PW_GECOS)
fi

dnl ---------------------------------------------------------------------------
dnl Check for functions
dnl ---------------------------------------------------------------------------

dnl don't check for wchar_t functions if we haven't got wchar_t itself
if test "$wx_cv_sizeof_wchar_t" != "0"; then
    AC_DEFINE(wxUSE_WCHAR_T)

    dnl check for wcslen in all possible places
    WCSLEN_FOUND=0
    WCHAR_LINK=
    AC_CHECK_FUNCS(wcslen, WCSLEN_FOUND=1)

    if test "$WCSLEN_FOUND" = 0; then
        if test "$TOOLKIT" = "MSW"; then
            AC_CHECK_LIB(msvcrt, wcslen, WCHAR_OK=1)
        else
            AC_CHECK_LIB(w, wcslen, [
                            WCHAR_LINK=" -lw"
                            WCSLEN_FOUND=1
                        ])
        fi
    fi

    if test "$WCSLEN_FOUND" = 1; then
        AC_DEFINE(HAVE_WCSLEN)
    fi

    dnl HP-UX aCC needs this define to find mbstrtowcs() &c
    if test "$USE_HPUX" = 1 -a "x$GCC" != "xyes"; then
        CPPFLAGS="$CPPFLAGS -D_INCLUDE__STDC_A1_SOURCE "
    fi

    dnl try to use wcsrtombs instead of wcstombs which is buggy in old GNU
    dnl libc versions if possible
    AC_CHECK_FUNCS(wcsrtombs)
else
    AC_MSG_WARN([Wide character support is unavailable])
fi

dnl *printf() functions sometimes are available in the library but not
dnl prototyped -- if this is the case, we can't use them from C++ code, but to
dnl detect this we have to use C++ compiler for testing
AC_LANG_SAVE
AC_LANG_CPLUSPLUS

dnl check for vsnprintf() -- a safe version of vsprintf())
dnl
dnl the trouble here is that on some systems (notable HP-UX) this function is
dnl present in libc but not in the system headers and so AC_CHECK_FUNCS (which,
dnl stupidly, provides a dummy function declaration inside its extension)
dnl succeeds, even with C++ compiler, but the compilation of wxWidgets fails
dnl
dnl so we first check if the function is in the library
AC_CHECK_FUNCS(vsnprintf)

if test "$ac_cv_func_vsnprintf" = "yes"; then
    dnl yes it is -- now check if it is in the headers
    AC_CACHE_CHECK([for vsnprintf declaration], wx_cv_func_vsnprintf_decl,
        [
            AC_TRY_COMPILE(
                [
                    #include <stdio.h>
                    #include <stdarg.h>
                ],
                [
                    char *buf;
                    va_list ap;
                    vsnprintf(buf, 10u, "%s", ap);
                ],
                wx_cv_func_vsnprintf_decl=yes,
                wx_cv_func_vsnprintf_decl=no
            )
        ]
    )

    if test "$wx_cv_func_vsnprintf_decl" = "yes"; then
        AC_DEFINE(HAVE_VSNPRINTF_DECL)
    fi
fi

if test "$wxUSE_UNICODE" = yes; then
    dnl also look if we have wide char IO functions
    AC_CHECK_FUNCS(wputc wputchar putws fputws wprintf vswprintf)

    dnl MinGW has a vswprintf with a different prototype, and
    dnl a _vsnwprintf with the correct prototype, but AC_CHECK_FUNCS
    dnl finds it even if it is not declared in some versions...
    AC_MSG_CHECKING([for _vsnwprintf])
    AC_TRY_COMPILE([#include <wchar.h>],
                   [&_vsnwprintf;],
                   [AC_MSG_RESULT([yes])
                    AC_DEFINE(HAVE__VSNWPRINTF)],
                   [AC_MSG_RESULT([no])]);
fi

AC_LANG_RESTORE



dnl the following tests are for Unix(like) systems only
if test "$TOOLKIT" != "MSW"; then

dnl check for available version of iconv()

AC_LANG_SAVE
AC_LANG_CPLUSPLUS
AM_ICONV
LIBS="$LIBICONV $LIBS"
AC_LANG_RESTORE

dnl check for POSIX signals if we need them
if test "$wxUSE_ON_FATAL_EXCEPTION" = "yes" -a "$wxUSE_UNIX" = "yes"; then
    AC_CHECK_FUNCS(sigaction)

    if test "$ac_cv_func_sigaction" = "no"; then
        AC_MSG_WARN([No POSIX signal functions on this system, wxApp::OnFatalException will not be called])
        wxUSE_ON_FATAL_EXCEPTION=no
    fi

    if test "$wxUSE_ON_FATAL_EXCEPTION" = "yes"; then
      AC_LANG_SAVE
      AC_LANG_CPLUSPLUS

      AC_CACHE_CHECK([for sa_handler type], wx_cv_type_sa_handler,
      [
        AC_TRY_COMPILE([#include <signal.h>],
                     [
                        extern void testSigHandler(int);

                        struct sigaction sa;
                        sa.sa_handler = testSigHandler;
                     ], [
                        wx_cv_type_sa_handler=int
                     ], [
                        wx_cv_type_sa_handler=void
                     ])
      ])

      AC_LANG_RESTORE

      AC_DEFINE_UNQUOTED(wxTYPE_SA_HANDLER, $wx_cv_type_sa_handler)
    fi
fi

dnl check for the function for temp files creation
AC_CHECK_FUNCS(mkstemp mktemp, break)

dnl get the library function to use for wxGetDiskSpace(): it is statfs() under
dnl Linux and *BSD and statvfs() under Solaris
AC_CACHE_CHECK(for statfs, wx_cv_func_statfs,
    AC_TRY_COMPILE(
        [
             #if defined(__BSD__)
               #include <sys/param.h>
               #include <sys/mount.h>
             #else
               #include <sys/vfs.h>
             #endif
        ],
        [
             long l;
             struct statfs fs;
             statfs("/", &fs);
             l = fs.f_bsize;
             l += fs.f_blocks;
             l += fs.f_bavail;
        ],
        [
             wx_cv_func_statfs=yes
        ],
        [
             wx_cv_func_statfs=no
        ]
    )
)

if test "$wx_cv_func_statfs" = "yes"; then
    wx_cv_type_statvfs_t="struct statfs"
    AC_DEFINE(HAVE_STATFS)
else
    AC_CACHE_CHECK(for statvfs, wx_cv_func_statvfs,
        AC_TRY_COMPILE(
            [
                #include <stddef.h>
                #include <sys/statvfs.h>
            ],
            [
                statvfs("/", NULL);
            ],
            wx_cv_func_statvfs=yes,
            wx_cv_func_statvfs=no
        )
    )

    if test "$wx_cv_func_statvfs" = "yes"; then
        dnl we also have to check whether we should use statvfs_t (works under
        dnl Solaris 8, doesn't work under Solaris 7) or "struct statvfs" (vice
        dnl versa) as the argument for statvfs in 64 bit off_t mode (in 32 bit
        dnl mode both work fine)
        dnl
        dnl for this check C++ compiler has to be used as passing incompatible
        dnl pointers is just a warning and not an error in C
        AC_LANG_SAVE
        AC_LANG_CPLUSPLUS

        AC_CACHE_CHECK(for statvfs argument type, wx_cv_type_statvfs_t,
            AC_TRY_COMPILE(
                [
                    #include <sys/statvfs.h>
                ],
                [
                    long l;
                    statvfs_t fs;
                    statvfs("/", &fs);
                    l = fs.f_bsize;
                    l += fs.f_blocks;
                    l += fs.f_bavail;
                ],
                wx_cv_type_statvfs_t=statvfs_t,
                [
                    AC_TRY_COMPILE(
                        [
                            #include <sys/statvfs.h>
                        ],
                        [
                            long l;
                            struct statvfs fs;
                            statvfs("/", &fs);
                            l = fs.f_bsize;
                            l += fs.f_blocks;
                            l += fs.f_bavail;
                        ],
                        wx_cv_type_statvfs_t="struct statvfs",
                        wx_cv_type_statvfs_t="unknown"
                    )
                ]
            )
        )

        AC_LANG_RESTORE

        if test "$wx_cv_type_statvfs_t" != "unknown"; then
            AC_DEFINE(HAVE_STATVFS)
        fi
    else
        dnl set it for the test below
        wx_cv_type_statvfs_t="unknown"
    fi
fi

if test "$wx_cv_type_statvfs_t" != "unknown"; then
    AC_DEFINE_UNQUOTED(WX_STATFS_T, $wx_cv_type_statvfs_t)
else
    AC_MSG_WARN([wxGetDiskSpace() function won't work without statfs()])
fi

dnl check for fcntl() or at least flock() needed by Unix implementation of
dnl wxSingleInstanceChecker
if test "$wxUSE_SNGLINST_CHECKER" = "yes"; then
    AC_CHECK_FUNCS(fcntl flock, break)

    if test "$ac_cv_func_fcntl" != "yes" -a "$ac_cv_func_flock" != "yes"; then
        AC_MSG_WARN(wxSingleInstanceChecker not available)
        wxUSE_SNGLINST_CHECKER=no
    fi
fi

dnl check for timegm() used by datetime.cpp
AC_CHECK_FUNCS(timegm)

dnl look for a function to modify the environment
AC_CHECK_FUNCS(putenv setenv, break)

HAVE_SOME_SLEEP_FUNC=0
if test "$USE_BEOS" = 1; then
    dnl BeOS has its own (wonder where did they get it from) sleep() function
    dnl in unistd.h
    AC_DEFINE(HAVE_SLEEP)
    HAVE_SOME_SLEEP_FUNC=1
fi

if test "$USE_DARWIN" = 1; then
    dnl Mac OS X/Darwin has both nanosleep and usleep
    dnl but only usleep is defined in unistd.h
    AC_DEFINE(HAVE_USLEEP)
    HAVE_SOME_SLEEP_FUNC=1
fi

if test "$HAVE_SOME_SLEEP_FUNC" != 1; then
    dnl try nanosleep() in libc and libposix4, if this fails - usleep()
    POSIX4_LINK=
    AC_CHECK_FUNCS(nanosleep,
        AC_DEFINE(HAVE_NANOSLEEP),
        [
            AC_CHECK_LIB(posix4, nanosleep,
                [
                    AC_DEFINE(HAVE_NANOSLEEP)
                    POSIX4_LINK=" -lposix4"
                ],
                [
                    AC_CHECK_FUNCS(usleep,
                        AC_DEFINE(HAVE_USLEEP),
                        AC_MSG_WARN([wxSleep() function will not work])
                    )
                ]
            )
        ]
    )
fi

dnl check for uname (POSIX) and gethostname (BSD)
AC_CHECK_FUNCS(uname gethostname, break)

dnl check for MT-safe version of strtok (on DEC Alpha, it's ok for C compiler
dnl but not for C++ one - hence change language)
AC_LANG_SAVE
AC_LANG_CPLUSPLUS

AC_CHECK_FUNCS(strtok_r)

AC_LANG_RESTORE

dnl check for inet_addr and inet_aton (these may live either in libc, or in
dnl libnsl or libresolv or libsocket)
INET_LINK=
AC_CHECK_FUNCS(inet_addr,
    AC_DEFINE(HAVE_INET_ADDR),
    [
        AC_CHECK_LIB(nsl, inet_addr,
            INET_LINK="nsl",
            [
                AC_CHECK_LIB(resolv, inet_addr,
                    INET_LINK="resolv",
                    [
                        AC_CHECK_LIB(socket, inet_addr,
                            INET_LINK="socket"
                        )
                    ]
                )
            ]
        )
    ]
)

AC_CHECK_FUNCS(inet_aton,
        AC_DEFINE(HAVE_INET_ATON),
        [
            dnl only check it in the same lib
            AC_CHECK_LIB($INET_LINK, inet_aton, AC_DEFINE(HAVE_INET_ATON))
        ])

if test "x$INET_LINK" != "x"; then
    AC_DEFINE(HAVE_INET_ADDR)
    INET_LINK=" -l$INET_LINK"
fi

fi
dnl if !MSW


dnl ------------------------------------------
dnl Checks specific to contrib libs (MMedia)
dnl ------------------------------------------

dnl Check for ESD:
EXTRALIBS_ESD=
AC_CHECK_LIB(esd, esd_close, [
    AC_CHECK_HEADERS([esd.h], [
    EXTRALIBS_ESD="-lesd"
  ])
])
AC_SUBST(EXTRALIBS_ESD)

dnl check for known CD-ROM interface
AC_MSG_CHECKING([for known CD-ROM interface])
AC_TRY_COMPILE([#ifdef __linux__
                #include <linux/cdrom.h>
                #else
                /* For Solaris */
                #include <sys/cdio.h>
                #endif
                ],
               [struct cdrom_tocentry entry, old_entry;],
               [AC_MSG_RESULT([yes])
                AC_DEFINE(HAVE_KNOWN_CDROM_INTERFACE)],
               [AC_MSG_RESULT([no])])

dnl ===========================================================================
dnl Now we have all the info we need - use it!
dnl ===========================================================================

dnl flush the cache
AC_CACHE_SAVE

dnl ---------------------------------------------------------------------------
dnl thread support for Unix (for Win32 and OS/2 see past
dnl                          the next matching "else")
dnl ---------------------------------------------------------------------------

dnl under MSW (except mingw32) we always have thread support
CPP_MT_FLAG=
if test "$TOOLKIT" != "MSW" -a "$USE_OS2" != 1; then

    dnl the code below:
    dnl
    dnl   defines THREADS_LINK and THREADS_CFLAGS which are the options
    dnl   necessary to build the MT programs for the linker and compiler
    dnl   respectively
    dnl
    dnl   sets wxUSE_THREADS=1 if thread support is activated

    THREADS_LINK=
    THREADS_CFLAGS=

    if test "$wxUSE_THREADS" = "yes" ; then
        if test "$USE_BEOS" = 1; then
            AC_MSG_WARN([BeOS threads are not yet supported... disabled])
            wxUSE_THREADS="no"
        fi
    fi

    if test "$wxUSE_THREADS" = "yes" ; then
        dnl find if POSIX threads are available
        dnl
        dnl the tests here are based on ACX_PTHREAD macro from autoconf macro
        dnl archive from http://ac-archive.sourceforge.net/
        dnl
        dnl thanks to Steven G. Johnson <stevenj@alum.mit.edu> and Alejandro
        dnl Forero Cuervo <bachue@bachue.com> for the original code

        dnl TODO: cache the result

        dnl define the list of the thread options to try in the loop below
        dnl with the convention that anything starting with '-' is a cpp flag
        dnl while anything else is a library (i.e. there is an implicit "-l")
        THREAD_OPTS="-pthread"
        case "${host}" in
          *-*-solaris2* | *-*-sunos4* )
                if test "x$GCC" = "xyes"; then
                    dnl Solaris/gcc combination use this one for some reason
                    THREAD_OPTS="-pthreads $THREAD_OPTS"
                else
                    THREAD_OPTS="-mt $THREAD_OPTS"
                fi
                ;;
          *-*-freebsd*)
                dnl look, in order, for the kernel threads, then Linux threads
                dnl and finally the userland threads
                THREAD_OPTS="-kthread lthread $THREAD_OPTS c_r"
                ;;
          *-*-darwin* | *-*-cygwin* )
                dnl Darwin / Mac OS X just uses -lpthread tested below
                dnl and so does Cygwin
                THREAD_OPTS=""
                ;;
          *-*-aix*)
                dnl AIX calls the library libpthreads - thanks IBM!
                THREAD_OPTS="pthreads"
                ;;
          *-hp-hpux* )
                dnl HP-UX aCC only gives a warning, not an error about
                dnl -pthread but it doesn't work and we have to use -lpthread
                dnl there
                if test "x$GCC" != "xyes"; then
                    THREAD_OPTS=""
                fi
                ;;

          *-*-irix* )
                dnl gcc under IRIX doesn't seem to like -pthread, but it
                dnl doesn't give an error for it neither, just a warning
                dnl message -- but this is still very annoying
                if test "x$GCC" = "xyes"; then
                    THREAD_OPTS=""
                fi
                ;;

          *-*-*UnixWare*)
                dnl flying by man pages here: Caldera online docs use this
                if test "x$GCC" != "xyes"; then
                    THREAD_OPTS="-Ethread"
                fi
                ;;
        esac

        dnl simply linking with libpthread should make the test below work but
        dnl it's far from certain that the threaded programs compiled without
        dnl any special switches actually work, so try it after all the others
        THREAD_OPTS="$THREAD_OPTS pthread none"

        dnl now test for all possibilities
        THREADS_OK=no
        for flag in $THREAD_OPTS; do
            case $flag in
                none)
                    AC_MSG_CHECKING([whether pthreads work without any flags])
                    ;;

                -*)
                    AC_MSG_CHECKING([whether pthreads work with $flag])
                    THREADS_CFLAGS="$flag"
                    ;;

                *)
                    AC_MSG_CHECKING([for the pthreads library -l$flag])
                    THREADS_LINK="-l$flag"
                    ;;
              esac

              save_LIBS="$LIBS"
              save_CFLAGS="$CFLAGS"
              LIBS="$THREADS_LINK $LIBS"
              CFLAGS="$THREADS_CFLAGS $CFLAGS"

              AC_TRY_LINK([#include <pthread.h>],
                          [pthread_create(0,0,0,0);],
                          THREADS_OK=yes)

              LIBS="$save_LIBS"
              CFLAGS="$save_CFLAGS"

              AC_MSG_RESULT($THREADS_OK)
              if test "x$THREADS_OK" = "xyes"; then
                      break;
              fi

              THREADS_LINK=""
              THREADS_CFLAGS=""
      done

      if test "x$THREADS_OK" != "xyes"; then
          wxUSE_THREADS=no
          AC_MSG_WARN([No thread support on this system... disabled])
      else
          dnl yes, these special compiler flags should be used with the
          dnl linker as well
          LDFLAGS="$THREADS_CFLAGS $LDFLAGS"
          LIBS="$THREADS_LINK $LIBS"

          AC_MSG_CHECKING([if more special flags are required for pthreads])
          flag=no
          case "${host}" in
            *-aix*)
                dnl again quoting from
                dnl http://www-1.ibm.com/servers/esdd/articles/gnu.html:
                dnl
                dnl     When compiling and linking with -pthread, the library
                dnl     search path should include -L/usr/lib/threads at the
                dnl     beginning of the path.
                LDFLAGS="-L/usr/lib/threads $LDFLAGS"
                flag="-D_THREAD_SAFE"
                ;;
            *-freebsd*)
                flag="-D_THREAD_SAFE"
                ;;
            *-hp-hpux* )
                flag="-D_REENTRANT"
                if test "x$GCC" != "xyes"; then
                    dnl see http://docs.hp.com/hpux/onlinedocs/2213/threads.htm
                    flag="$flag -D_RWSTD_MULTI_THREAD"

                    dnl we also need -lcma for user threads (-lpthread is for
                    dnl kernel threads only) according to Ian Brown
                    LIBS="$LIBS -lcma"
                fi
                ;;
            *solaris* | alpha*-osf*)
                flag="-D_REENTRANT"
                ;;
          esac
          AC_MSG_RESULT(${flag})
          if test "x$flag" != xno; then
              THREADS_CFLAGS="$THREADS_CFLAGS $flag"
          fi

          if test "x$THREADS_CFLAGS" != "x"; then
            dnl don't add these options to CPPFLAGS as cpp might not know them
            CFLAGS="$CFLAGS $THREADS_CFLAGS"
            CXXFLAGS="$CXXFLAGS $THREADS_CFLAGS"
          fi
      fi
    fi

    dnl do other tests only if we are using threads
    if test "$wxUSE_THREADS" = "yes" ; then
      AC_CHECK_FUNCS(thr_setconcurrency)

      AC_CHECK_HEADERS(sched.h)
      if test "$ac_cv_header_sched_h" = "yes"; then
          AC_CHECK_FUNC(sched_yield,
            AC_DEFINE(HAVE_SCHED_YIELD),
            [
                AC_CHECK_LIB(posix4,
                    sched_yield,
                    [AC_DEFINE(HAVE_SCHED_YIELD) POSIX4_LINK=" -lposix4"],
                    AC_MSG_WARN(wxThread::Yield will not work properly)
                )
            ]
          )
      fi

      dnl to be able to set the thread priority, we need to have all of the
      dnl following functions:
      dnl   1. pthread_attr_getschedpolicy
      dnl   2. sched_get_priority_min and sched_get_priority_max
      dnl      (this one can be in either libpthread or libposix4 (under Solaris))
      dnl   3. pthread_attr_getschedparam and pthread_attr_setschedparam
      HAVE_PRIOR_FUNCS=0
      AC_CHECK_FUNC(pthread_attr_getschedpolicy,
                     [AC_CHECK_FUNC(pthread_attr_setschedparam,
                       [AC_CHECK_FUNC(sched_get_priority_max,
                           HAVE_PRIOR_FUNCS=1,
                           [AC_CHECK_LIB([posix4], sched_get_priority_max,
                             [
                                HAVE_PRIOR_FUNCS=1
                                POSIX4_LINK=" -lposix4"
                             ],
                           )]
                       )]
                     )]
                   )

      if test "$HAVE_PRIOR_FUNCS" = 1; then
        AC_DEFINE(HAVE_THREAD_PRIORITY_FUNCTIONS)
      else
        AC_MSG_WARN(Setting thread priority will not work)
      fi

      AC_CHECK_FUNC(pthread_cancel,
                    AC_DEFINE(HAVE_PTHREAD_CANCEL),
                    AC_MSG_WARN([wxThread::Kill() will not work properly]))

      dnl we don't use pthread_cleanup_push/pop() any more in the code, but I
      dnl the code here for now in case we need it again soon; otherwise it
      dnl should be removed in a couple of months (VZ in Sep 2003)
      dnl
      dnl AC_CACHE_CHECK([for pthread_cleanup_push/pop], wx_cv_func_pthread_cleanup_push,
      dnl [
      dnl   AC_TRY_COMPILE([#include <pthread.h>],
      dnl                [
      dnl                   pthread_cleanup_push(NULL, NULL);
      dnl                   pthread_cleanup_pop(0);
      dnl                ], [
      dnl                   wx_cv_func_pthread_cleanup_push=yes
      dnl                ], [
      dnl                   wx_cv_func_pthread_cleanup_push=no
      dnl                ])
      dnl ])
      dnl 
      dnl if test "$wx_cv_func_pthread_cleanup_push" = "yes"; then
      dnl     AC_DEFINE(HAVE_THREAD_CLEANUP_FUNCTIONS)
      dnl fi

      dnl mutexattr_t initialization is done in quite different ways on different
      dnl platforms, so check for a few things:
      dnl
      dnl HAVE_MUTEX_RECURSIVE means that we can create recursive mutexes
      dnl HAVE_MUTEXATTR_SETTYPE means that we do it using
      dnl pthread_mutexattr_settype(PTHREAD_MUTEX_RECURSIVE) and if it is not
      dnl defined, we do it by directly assigned
      dnl PTHREAD_MUTEX_RECURSIVE_MUTEX_INITIALIZER_NP to attr

      AC_CACHE_CHECK([for pthread_mutexattr_t], wx_cv_type_pthread_mutexattr_t,
      [
        AC_TRY_COMPILE([#include <pthread.h>],
            [
               pthread_mutexattr_t attr;
               pthread_mutexattr_settype(&attr, PTHREAD_MUTEX_RECURSIVE);
            ], [
               wx_cv_type_pthread_mutexattr_t=yes
            ], [
               wx_cv_type_pthread_mutexattr_t=no
            ]
        )
      ])

      if test "$wx_cv_type_pthread_mutexattr_t" = "yes"; then
        AC_DEFINE(HAVE_PTHREAD_MUTEXATTR_T)
      else
        dnl don't despair, there may be another way to do it
        AC_CACHE_CHECK([for PTHREAD_RECURSIVE_MUTEX_INITIALIZER],
                       wx_cv_type_pthread_rec_mutex_init,
        [
            AC_TRY_COMPILE([#include <pthread.h>],
                [
                    pthread_mutex_t attr = PTHREAD_RECURSIVE_MUTEX_INITIALIZER_NP;
                ], [
                    wx_cv_type_pthread_rec_mutex_init=yes
                ], [
                    wx_cv_type_pthread_rec_mutex_init=no
                ]
            )
        ])
        if test "$wx_cv_type_pthread_rec_mutex_init" = "yes"; then
          AC_DEFINE(HAVE_PTHREAD_RECURSIVE_MUTEX_INITIALIZER)
        else
          dnl this may break code working elsewhere, so at least warn about it
          AC_MSG_WARN([wxMutex won't be recursive on this platform])
        fi
      fi
    fi

dnl from if !MSW
else
    if test "$wxUSE_THREADS" = "yes" ; then
      case "${host}" in
        *-*-mingw32* )
            dnl check if the compiler accepts -mthreads
            AC_CACHE_CHECK([if compiler supports -mthreads],
                wx_cv_cflags_mthread,
                [
                    CFLAGS_OLD="$CFLAGS"
                    CFLAGS="$CFLAGS -mthreads"
                    AC_TRY_COMPILE([], [],
                        wx_cv_cflags_mthread=yes,
                        wx_cv_cflags_mthread=no
                    )
                ]
            )

            if test "$wx_cv_cflags_mthread" = "yes"; then
                dnl it does, use it
                CXXFLAGS="$CXXFLAGS -mthreads"
                TOOLCHAIN_DEFS="$TOOLCHAIN_DEFS -mthreads"
                LDFLAGS="$LDFLAGS -mthreads"
            else
                dnl it doesn't
                CFLAGS="$CFLAGS_OLD"
            fi
        ;;
        *-pc-os2*emx )
            CFLAGS="$CFLAGS -Zmt -D__ST_MT_ERRNO__"
            CXXFLAGS="$CXXFLAGS -Zmt -D__ST_MT_ERRNO__"
            LDFLAGS="$LDFLAGS -Zmt"
        ;;
      esac
    fi
fi

if test "$wxUSE_THREADS" = "yes"; then
  AC_DEFINE(wxUSE_THREADS)

  SAMPLES_SUBDIRS="$SAMPLES_SUBDIRS thread"
else
  dnl on some systems, _REENTRANT should be defined if we want to use any _r()
  dnl functions - add tests for other functions here as well
  if test "$ac_cv_func_strtok_r" = "yes"; then
    AC_MSG_CHECKING(if -D_REENTRANT is needed)
    if test "$NEEDS_D_REENTRANT_FOR_R_FUNCS" = 1; then
        TOOLCHAIN_DEFS="${TOOLCHAIN_DEFS} -D_REENTRANT"
        AC_MSG_RESULT(yes)
    else
        AC_MSG_RESULT(no)
    fi
  fi
fi

if test "$WXGTK20" = 1 ; then
  AC_DEFINE_UNQUOTED(__WXGTK20__,$WXGTK20)
  WXGTK12=1
fi

if test "$WXGTK12" = 1 ; then
  AC_DEFINE_UNQUOTED(__WXGTK12__,$WXGTK12)
fi

if test "$WXGTK127" = 1 ; then
  AC_DEFINE_UNQUOTED(__WXGTK127__,$WXGTK127)
fi

if test "$WXGPE" = 1 ; then
  AC_DEFINE_UNQUOTED(__WXGPE__,$WXGPE)
fi

WXDEBUG=

if test "$wxUSE_DEBUG_INFO" = "yes" ; then
    WXDEBUG="-g"
    wxUSE_OPTIMISE=no
fi

if test "$wxUSE_DEBUG_GDB" = "yes" ; then
    wxUSE_DEBUG_INFO=yes
    WXDEBUG="-ggdb"
fi

if test "$wxUSE_DEBUG_FLAG" = "yes" ; then
    AC_DEFINE(WXDEBUG)
    WXDEBUG_DEFINE="-D__WXDEBUG__"
else
    if test "$wxUSE_GTK" = 1 ; then
        WXDEBUG_DEFINE="-DGTK_NO_CHECK_CASTS"
    fi
fi

if test "$wxUSE_MEM_TRACING" = "yes" ; then
    AC_DEFINE(wxUSE_MEMORY_TRACING)
    AC_DEFINE(wxUSE_GLOBAL_MEMORY_OPERATORS)
    AC_DEFINE(wxUSE_DEBUG_NEW_ALWAYS)
    SAMPLES_SUBDIRS="$SAMPLES_SUBDIRS memcheck"
fi

if test "$wxUSE_DMALLOC" = "yes" ; then
    DMALLOC_LIBS="-ldmalloc"
fi

PROFILE=
if test "$wxUSE_PROFILE" = "yes" ; then
    PROFILE=" -pg"
fi

CODE_GEN_FLAGS=
CODE_GEN_FLAGS_CXX=
if test "$GCC" = "yes" ; then
    if test "$wxUSE_NO_RTTI" = "yes" ; then
        CODE_GEN_FLAGS_CXX="$CODE_GEN_FLAGS_CXX -fno-rtti"
    fi
    if test "$wxUSE_NO_EXCEPTIONS" = "yes" ; then
        CODE_GEN_FLAGS_CXX="$CODE_GEN_FLAGS_CXX -fno-exceptions"
    fi
    if test "$wxUSE_PERMISSIVE" = "yes" ; then
        CODE_GEN_FLAGS="$CODE_GEN_FLAGS -fpermissive"
    fi

    dnl Ian Brown <ian.brown@printsoft.de> reports that versions of gcc before
    dnl 3.0 overflow the table of contents on rs6000 as they create an entry
    dnl for each subroutine by default -- using the switch below only creates
    dnl one entry per file instead at the price of minor performance penalty
    dnl
    dnl As of wx2.4 a bug in the hppa gcc compiler causes a similar problem
    dnl without -ffunction-sections.  No idea how long we'll need to maintain
    dnl this, or even the extent of gcc/wx version combinations affected, but
    dnl also as above, this 'fix' does not come without side effects.
    dnl
    dnl TODO: test for the gcc version here (how?)
    case "${host}" in
      powerpc*-*-aix* )
        CODE_GEN_FLAGS="$CODE_GEN_FLAGS -mminimal-toc"
        ;;
      *-hppa* )
        CODE_GEN_FLAGS="$CODE_GEN_FLAGS -ffunction-sections"
        ;;
    esac
fi

OPTIMISE=
if test "$wxUSE_OPTIMISE" = "no" ; then
    if test "$GCC" = yes ; then
        dnl use -O0 because compiling with it is faster than compiling with no
        dnl optimization options at all (at least with g++ 3.2)
        OPTIMISE="-O0"
    fi
else
    if test "$GCC" = yes ; then
        OPTIMISE="-O2"

        dnl VZ: does anybody know what does this do??
        case "${host}" in
            i586-*-*|i686-*-* )
            OPTIMISE="${OPTIMISE} "
        ;;
        esac
    else
        OPTIMISE="-O"
    fi
fi

dnl ---------------------------------------------------------------------------
dnl compatibility level
dnl ---------------------------------------------------------------------------

if test "x$WXWIN_COMPATIBILITY_2_2" = "xyes"; then
    AC_DEFINE(WXWIN_COMPATIBILITY_2_2)

    WXWIN_COMPATIBILITY_2_4="yes"
fi

if test "x$WXWIN_COMPATIBILITY_2_4" != "xno"; then
    AC_DEFINE(WXWIN_COMPATIBILITY_2_4)
fi

dnl ---------------------------------------------------------------------------
dnl the library may be built without GUI classes at all
dnl ---------------------------------------------------------------------------

if test "$wxUSE_GUI" = "yes"; then
  AC_DEFINE(wxUSE_GUI)

  dnl the things we always pull in the GUI version of the library:
  dnl 1. basic things like wxApp, wxWindow, wxControl, wxFrame, wxDialog (the
  dnl    library really can't be built without those)
  dnl 2. basic controls: wxButton, wxStaticText, wxTextCtrl (these are used in
  dnl    almost any program and the first 2 are needed to show a message box
  dnl    which want to be always able to do)
  dnl 3. GDI stuff: icon, cursors and all that. Although it would be very nice
  dnl    to compile without them (if the app doesn't do any drawing, it doesn't
  dnl    need the dcs, pens, brushes, ...), this just can't be done now
  dnl 4. menu stuff: wxMenu, wxMenuBar, wxMenuItem
  dnl 5. misc stuff: timers, settings, message box
else
  AC_DEFINE(wxUSE_NOGUI)
fi

dnl ---------------------------------------------------------------------------
dnl Unix/Windows
dnl ---------------------------------------------------------------------------

if test "$wxUSE_UNIX" = "yes"; then
  AC_DEFINE(wxUSE_UNIX)
fi

dnl ------------------------------------------------------------------------
dnl DLL support
dnl ------------------------------------------------------------------------

dnl under MSW we always have LoadLibrary/GetProcAddress
if test "$TOOLKIT" != "MSW" -a "$USE_OS2" != 1; then

    HAVE_DL_FUNCS=0
    HAVE_SHL_FUNCS=0
    if test "$wxUSE_DYNAMIC_LOADER" = "yes" -o "$wxUSE_DYNLIB_CLASS" = "yes" ; then
        if test "$USE_DARWIN" = 1; then
            dnl dlopen/dlerror is implemented in dynlib.cpp for Darwin/Mac OS X
            HAVE_DL_FUNCS=1
        else
            dnl the test is a bit complicated because we check for dlopen() both with
            dnl and without -ldl and we also try to find shl_load() if there is no
            dnl dlopen() on this system
            AC_CHECK_FUNCS(dlopen,
            [
                AC_DEFINE(HAVE_DLOPEN)
                HAVE_DL_FUNCS=1
            ],
            [
                AC_CHECK_LIB(dl, dlopen,
                            [
                                AC_DEFINE(HAVE_DLOPEN)
                                HAVE_DL_FUNCS=1
                                DL_LINK=" -ldl$DL_LINK"
                            ],
                            [
                                AC_CHECK_FUNCS(shl_load,
                                              [
                                                AC_DEFINE(HAVE_SHL_LOAD)
                                                HAVE_SHL_FUNCS=1
                                              ],
                                              [
                                                AC_CHECK_LIB(shl_load, dld,
                                                             [
                                                                HAVE_SHL_FUNCS=1
                                                                DL_LINK=" -ldld$DL_LINK"
                                                             ])
                                              ])
                            ])
            ])

        dnl check also for dlerror()
        if test "$HAVE_DL_FUNCS" = 1; then
            AC_CHECK_FUNCS(dlerror,
                AC_DEFINE(HAVE_DLERROR),
                [
                    AC_CHECK_LIB(dl, dlerror, AC_DEFINE(HAVE_DLERROR))
                ]
            )
        fi
        fi

        if test "$HAVE_DL_FUNCS" = 0; then
            if test "$HAVE_SHL_FUNCS" = 0; then
              if test "$USE_UNIX" = 1; then
                  AC_MSG_WARN([Missing dynamic loading support, several features will be disabled])
                  wxUSE_DYNAMIC_LOADER=no
                  wxUSE_DYNLIB_CLASS=no
              else
                  AC_MSG_WARN([Assuming wxLibrary class works on this platform])
              fi
            fi
        fi
    fi
fi

if test "$wxUSE_DYNAMIC_LOADER" = "yes" ; then
    AC_DEFINE(wxUSE_DYNAMIC_LOADER)
fi
if test "$wxUSE_DYNLIB_CLASS" = "yes" ; then
    AC_DEFINE(wxUSE_DYNLIB_CLASS)
fi


dnl ---------------------------------------------------------------------------
dnl Verify consistency of plugins/monolithic/shared settings:
dnl ---------------------------------------------------------------------------

if test "$wxUSE_PLUGINS" = "yes" ; then
    if test "$wxUSE_SHARED" = "no" ; then
        AC_MSG_WARN([plugins supported only in shared build, disabling])
        wxUSE_PLUGINS=no
    fi
    if test "$wxUSE_MONOLITHIC" = "yes" ; then
        AC_MSG_WARN([plugins not supported monolithic build, disabling])
        wxUSE_PLUGINS=no
    fi
    if test "$wxUSE_DYNLIB_CLASS" = "no" ; then
        AC_MSG_WARN([plugins require wxDynamicLibrary, disabling])
        wxUSE_PLUGINS=no
    fi
    if test "$wxUSE_PLUGINS" = "yes" ; then
        AC_DEFINE(wxUSE_PLUGINS)
    fi
fi

dnl ---------------------------------------------------------------------------
dnl Register non-GUI class options for makefiles and setup.h
dnl ---------------------------------------------------------------------------

if test "$wxUSE_STL" = "yes"; then
  AC_DEFINE(wxUSE_STL)
fi

if test "$wxUSE_APPLE_IEEE" = "yes"; then
  AC_DEFINE(wxUSE_APPLE_IEEE)
fi

if test "$wxUSE_TIMER" = "yes"; then
  AC_DEFINE(wxUSE_TIMER)
fi

WITH_PLUGIN_SDL=0
if test "$wxUSE_SOUND" = "yes"; then
  dnl Unix implementation needs additional checks because audio support
  dnl comes in many favours:
  if test "$USE_UNIX" = "1" ; then
    AC_CHECK_HEADERS([sys/soundcard.h])

    if test "$wxUSE_LIBSDL" != "no"; then
      AM_PATH_SDL([1.2.0],
                  [
                    EXTRALIBS_SDL="$SDL_LIBS"
                    CXXFLAGS="$CXXFLAGS $SDL_CFLAGS"
                    AC_DEFINE(wxUSE_LIBSDL)
                  ],
                  [wxUSE_LIBSDL="no"])
      if test "$wxUSE_LIBSDL" = "yes" -a "$wxUSE_PLUGINS" = "yes" ; then
        WITH_PLUGIN_SDL=1
      fi
    fi
  fi
 
  AC_DEFINE(wxUSE_SOUND)
  SAMPLES_SUBDIRS="$SAMPLES_SUBDIRS sound"
fi

if test "$WXGTK20" = 1; then
    if test "$wxUSE_PRINTING_ARCHITECTURE" = "yes" ; then
        if test "$wxUSE_LIBGNOMEPRINT" = "yes" ; then
        
                PKG_CHECK_MODULES(LIBGNOMEPRINTUI, libgnomeprintui-2.2,
                    [
                        EXTRALIBS_GNOMEPRINT="$LIBGNOMEPRINTUI_LIBS"
                        CXXFLAGS="$CXXFLAGS $LIBGNOMEPRINTUI_CFLAGS"
                        AC_DEFINE(wxUSE_LIBGNOMEPRINT)
                    ],
                    [
                        AC_MSG_WARN([libgnomeprintui not found, library will use standard PostScript printing])
                        wxUSE_LIBGNOMEPRINT="no"
                    ]
                )
        fi
    fi
fi


if test "$wxUSE_CMDLINE_PARSER" = "yes"; then
  AC_DEFINE(wxUSE_CMDLINE_PARSER)
fi

if test "$wxUSE_STOPWATCH" = "yes"; then
  AC_DEFINE(wxUSE_STOPWATCH)
fi

if test "$wxUSE_DATETIME" = "yes"; then
  AC_DEFINE(wxUSE_DATETIME)
fi

if test "$wxUSE_FILE" = "yes"; then
  AC_DEFINE(wxUSE_FILE)
fi

if test "$wxUSE_FFILE" = "yes"; then
  AC_DEFINE(wxUSE_FFILE)
fi

if test "$wxUSE_FILESYSTEM" = "yes"; then
  AC_DEFINE(wxUSE_FILESYSTEM)
fi

if test "$wxUSE_FS_ZIP" = "yes"; then
  AC_DEFINE(wxUSE_FS_ZIP)
fi

if test "$wxUSE_ZIPSTREAM" = "yes"; then
  AC_DEFINE(wxUSE_ZIPSTREAM)
fi

if test "$wxUSE_ON_FATAL_EXCEPTION" = "yes"; then
  AC_DEFINE(wxUSE_ON_FATAL_EXCEPTION)
fi

if test "$wxUSE_SNGLINST_CHECKER" = "yes"; then
  AC_DEFINE(wxUSE_SNGLINST_CHECKER)
fi

if test "$wxUSE_BUSYINFO" = "yes"; then
  AC_DEFINE(wxUSE_BUSYINFO)
fi

if test "$wxUSE_STD_IOSTREAM" = "yes"; then
  AC_DEFINE(wxUSE_STD_IOSTREAM)
fi

if test "$wxUSE_TEXTBUFFER" = "yes"; then
    AC_DEFINE(wxUSE_TEXTBUFFER)
fi

if test "$wxUSE_TEXTFILE" = "yes"; then
  if test "$wxUSE_FILE" != "yes" -o "$wxUSE_TEXTBUFFER" != "yes" ; then
    AC_MSG_WARN(wxTextFile requires wxFile and wxTextBuffer... disabled)
  else
    AC_DEFINE(wxUSE_TEXTFILE)
  fi
fi

if test "$wxUSE_CONFIG" = "yes" ; then
  if test "$wxUSE_TEXTFILE" != "yes"; then
    AC_MSG_WARN(wxConfig requires wxTextFile... disabled)
  else
    AC_DEFINE(wxUSE_CONFIG)
    SAMPLES_SUBDIRS="$SAMPLES_SUBDIRS config"
  fi
fi

if test "$wxUSE_INTL" = "yes" ; then
  if test "$wxUSE_FILE" != "yes"; then
    AC_MSG_WARN(I18n code requires wxFile... disabled)
  else
    AC_DEFINE(wxUSE_INTL)
    SAMPLES_SUBDIRS="$SAMPLES_SUBDIRS internat"
    GUIDIST="$GUIDIST INTL_DIST"
  fi
fi

if test "$wxUSE_LOG" = "yes"; then
  AC_DEFINE(wxUSE_LOG)

    if test "$wxUSE_LOGGUI" = "yes"; then
      AC_DEFINE(wxUSE_LOGGUI)
    fi

    if test "$wxUSE_LOGWINDOW" = "yes"; then
      AC_DEFINE(wxUSE_LOGWINDOW)
    fi

    if test "$wxUSE_LOGDIALOG" = "yes"; then
      AC_DEFINE(wxUSE_LOG_DIALOG)
    fi
fi

if test "$wxUSE_LONGLONG" = "yes"; then
  AC_DEFINE(wxUSE_LONGLONG)
fi

if test "$wxUSE_GEOMETRY" = "yes"; then
  AC_DEFINE(wxUSE_GEOMETRY)
fi

if test "$wxUSE_DIALUP_MANAGER" = "yes" ; then
  AC_DEFINE(wxUSE_DIALUP_MANAGER)
  SAMPLES_SUBDIRS="$SAMPLES_SUBDIRS dialup"
fi

if test "$wxUSE_STREAMS" = "yes" ; then
  AC_DEFINE(wxUSE_STREAMS)
fi

dnl ---------------------------------------------------------------------------
dnl time/date functions
dnl ------------------------------------------------------------------------

if test "$wxUSE_DATETIME" = "yes"; then
    dnl check for strptime
    AC_CHECK_FUNCS(strptime)

    dnl check for timezone variable
    dnl   doesn't exist under Darwin / Mac OS X which uses tm_gmtoff instead
    AC_CACHE_CHECK(for timezone variable in <time.h>,
                   wx_cv_var_timezone,
                   [
                        AC_TRY_COMPILE(
                            [
                                #include <time.h>
                            ],
                            [
                                int tz;
                                tz = timezone;
                            ],
                            [
                                wx_cv_var_timezone=timezone
                            ],
                            [
                                AC_TRY_COMPILE(
                                    [
                                        #include <time.h>
                                    ],
                                    [
                                        int tz;
                                        tz = _timezone;
                                    ],
                                    [
                                        wx_cv_var_timezone=_timezone
                                    ],
                                    [
                                        AC_TRY_COMPILE(
                                            [
                                                #include <time.h>
                                            ],
                                            [
                                                int tz;
                                                tz = __timezone;
                                            ],
                                            [
                                                wx_cv_var_timezone=__timezone
                                            ],
                                            [
                                                if test "$USE_DOS" = 0 ; then
                                                    AC_MSG_WARN(no timezone variable, will use tm_gmtoff instead)
                                                fi
                                            ]
                                        )
                                    ]
                                )
                            ]
                        )
                   ]
    )

    dnl as we want $wx_cv_var_timezone to be expanded, use AC_DEFINE_UNQUOTED
    if test "x$wx_cv_var_timezone" != x ; then
        AC_DEFINE_UNQUOTED(WX_TIMEZONE, $wx_cv_var_timezone)
    fi

    dnl check for localtime (it's POSIX, but the check can do no harm...)
    AC_CHECK_FUNCS(localtime)

    if test "$ac_cv_func_localtime" = "yes"; then
        AC_CACHE_CHECK(for tm_gmtoff in struct tm,
                       wx_cv_struct_tm_has_gmtoff,
        [
            AC_TRY_COMPILE(
                [
                    #include <time.h>
                ],
                [
                    struct tm tm;
                    tm.tm_gmtoff++;
                ],
                [
                    wx_cv_struct_tm_has_gmtoff=yes
                ],
                wx_cv_struct_tm_has_gmtoff=no
            )
        ])
    fi

    if test "$wx_cv_struct_tm_has_gmtoff" = "yes"; then
        AC_DEFINE(WX_GMTOFF_IN_TM)
    fi

    dnl check for gettimeofday (SVr4, BSD 4.3) and ftime (V7, BSD 4.3) for the
    dnl function to be used for high resolution timers
    AC_CHECK_FUNCS(gettimeofday ftime, break)

    if test "$ac_cv_func_gettimeofday" = "yes"; then
        AC_CACHE_CHECK([whether gettimeofday takes two arguments],
                       wx_cv_func_gettimeofday_has_2_args,
        [
            dnl on some _really_ old systems it takes only 1 argument
            AC_LANG_SAVE
            AC_LANG_CPLUSPLUS

            AC_TRY_COMPILE(
                [
                    #include <sys/time.h>
                    #include <unistd.h>
                ],
                [
                    struct timeval tv;
                    gettimeofday(&tv, NULL);
                ],
                wx_cv_func_gettimeofday_has_2_args=yes,
                AC_TRY_COMPILE(
                    [
                        #include <sys/time.h>
                        #include <unistd.h>
                    ],
                    [
                        struct timeval tv;
                        gettimeofday(&tv);
                    ],
                    wx_cv_func_gettimeofday_has_2_args=no,
                    [
                        AC_MSG_WARN([failed to determine number of gettimeofday() arguments])
                        wx_cv_func_gettimeofday_has_2_args=unknown
                    ]
                )
            )
            AC_LANG_RESTORE
        ])

        if test "$wx_cv_func_gettimeofday_has_2_args" != "yes"; then
            AC_DEFINE(WX_GETTIMEOFDAY_NO_TZ)
        fi
    fi

    SAMPLES_SUBDIRS="$SAMPLES_SUBDIRS typetest"
fi

dnl ------------------------------------------------------------------------
dnl wxSocket
dnl ------------------------------------------------------------------------

dnl VZ: the GUI hooks wxSocket needs are not implemented yet in some toolkits
if test "$wxUSE_SOCKETS" = "yes"; then
    if test "$wxUSE_MGL" = "1"; then
        AC_MSG_WARN([wxSocket not yet supported under MGL... disabled])
        wxUSE_SOCKETS="no"
    fi
fi

if test "$wxUSE_SOCKETS" = "yes"; then
    dnl under MSW we always have sockets
    if test "$TOOLKIT" != "MSW"; then
        dnl under Solaris and OS/2, socket functions live in -lsocket
        AC_CHECK_FUNC(socket,,
            [
                AC_CHECK_LIB(socket, socket,
                    if test "$INET_LINK" != " -lsocket"; then
                        INET_LINK="$INET_LINK -lsocket"
                    fi,
                    [
                        AC_MSG_WARN([socket library not found - sockets will be disabled])
                        wxUSE_SOCKETS=no
                    ]
                )
            ]
        )
    fi
fi

if test "$wxUSE_SOCKETS" = "yes" ; then
    dnl this test may be appropriate if building under cygwin
    dnl right now I'm assuming it also uses the winsock stuff
    dnl like mingw does..  -- RL
    if test "$TOOLKIT" != "MSW"; then
        dnl determine the type of third argument for getsockname
        dnl This test needs to be done in C++ mode since gsocket.cpp now
        dnl is C++ code and pointer cast that are possible even without
        dnl warning in C still fail in C++.
        AC_LANG_PUSH(C++)
        AC_CACHE_CHECK([what is the type of the third argument of getsockname],
                       wx_cv_type_getsockname3,
            [
                AC_TRY_COMPILE(
                    [
                        #include <sys/types.h>
                        #include <sys/socket.h>
                    ],
                    [
                        socklen_t len;
                        getsockname(0, 0, &len);
                    ],
                    wx_cv_type_getsockname3=socklen_t,
                    [
                        dnl the compiler will compile the version with size_t
                        dnl even if the real type of the last parameter is int
                        dnl but it should give at least a warning about
                        dnl converting between incompatible pointer types, so
                        dnl try to use it to get the correct behaviour at
                        dnl least with gcc (otherwise we'd always use size_t)
                        CFLAGS_OLD="$CFLAGS"
                        if test "$GCC" = yes ; then
                            CFLAGS="$CFLAGS -Werror"
                        fi

                        AC_TRY_COMPILE(
                            [
                                #include <sys/types.h>
                                #include <sys/socket.h>
                            ],
                            [
                                size_t len;
                                getsockname(0, 0, &len);
                            ],
                            wx_cv_type_getsockname3=size_t,
                            AC_TRY_COMPILE(
                                [
                                    #include <sys/types.h>
                                    #include <sys/socket.h>
                                ],
                                [
                                    int len;
                                    getsockname(0, 0, &len);
                                ],
                                wx_cv_type_getsockname3=int,
                                wx_cv_type_getsockname3=unknown
                            )
                        )

                        CFLAGS="$CFLAGS_OLD"
                    ]
                )
            ])

        if test "$wx_cv_type_getsockname3" = "unknown"; then
            wxUSE_SOCKETS=no
            AC_MSG_WARN([Couldn't find socklen_t synonym for this system])
        else
            AC_DEFINE_UNQUOTED(SOCKLEN_T, $wx_cv_type_getsockname3)
        fi
        AC_LANG_POP
    fi
fi

if test "$wxUSE_SOCKETS" = "yes" ; then
    AC_DEFINE(wxUSE_SOCKETS)
    SAMPLES_SUBDIRS="$SAMPLES_SUBDIRS sockets"
fi

if test "$wxUSE_PROTOCOL" = "yes"; then
    if test "$wxUSE_SOCKETS" != "yes"; then
        AC_MSG_WARN(Protocol classes require sockets... disabled)
        wxUSE_PROTOCOL=no
    fi
fi

if test "$wxUSE_PROTOCOL" = "yes"; then
    AC_DEFINE(wxUSE_PROTOCOL)

    if test "$wxUSE_PROTOCOL_HTTP" = "yes"; then
        AC_DEFINE(wxUSE_PROTOCOL_HTTP)
    fi
    if test "$wxUSE_PROTOCOL_FTP" = "yes"; then
        AC_DEFINE(wxUSE_PROTOCOL_FTP)
    fi
    if test "$wxUSE_PROTOCOL_FILE" = "yes"; then
        AC_DEFINE(wxUSE_PROTOCOL_FILE)
    fi
else
    if test "$wxUSE_FS_INET" = "yes"; then
        AC_MSG_WARN([HTTP filesystem require protocol classes... disabled])   
        wxUSE_FS_INET="no"
    fi
fi

if test "$wxUSE_URL" = "yes"; then
    if test "$wxUSE_PROTOCOL" != "yes"; then
        AC_MSG_WARN(wxURL class requires wxProtocol... disabled)
        wxUSE_URL=no
    fi
    if test "$wxUSE_URL" = "yes"; then
        AC_DEFINE(wxUSE_URL)
    fi
fi

if test "$wxUSE_FS_INET" = "yes"; then
  AC_DEFINE(wxUSE_FS_INET)
fi

dnl ---------------------------------------------------------------------------
dnl Joystick support
dnl ---------------------------------------------------------------------------

if test "$wxUSE_GUI" = "yes"; then

    if test "$TOOLKIT" = "MAC" -o "$TOOLKIT" = "COCOA"; then
        AC_MSG_WARN([Joystick not yet supported under Mac OS X... disabled])
        wxUSE_JOYSTICK=no
     fi
    
     dnl under MSW we always have joystick support
    if test "$TOOLKIT" != "MSW"; then
        if test "$wxUSE_JOYSTICK" = "yes"; then
            dnl joystick support is only for Linux 2.1.x or greater
            AC_CHECK_HEADERS(linux/joystick.h)
            if test "$ac_cv_header_linux_joystick_h" != "yes"; then
                wxUSE_JOYSTICK=no
                AC_MSG_WARN(Joystick not supported by this system... disabled)
            fi
        fi
    fi

    if test "$wxUSE_JOYSTICK" = "yes"; then
        AC_DEFINE(wxUSE_JOYSTICK)
        SAMPLES_SUBDIRS="$SAMPLES_SUBDIRS joytest"
    fi
fi


dnl ---------------------------------------------------------------------------
dnl String stuff
dnl ---------------------------------------------------------------------------

if test "$wxUSE_FONTMAP" = "yes" ; then
  AC_DEFINE(wxUSE_FONTMAP)
fi

if test "$wxUSE_UNICODE" = "yes" ; then
  AC_DEFINE(wxUSE_UNICODE)

  if test "$USE_WIN32" != 1; then
      wxUSE_UNICODE_MSLU=no
  fi

  if test "$USE_WIN32" = 1 -a "$wxUSE_UNICODE_MSLU" = "yes"; then
        AC_CHECK_LIB(unicows,main,
            [
                AC_DEFINE(wxUSE_UNICODE_MSLU)
            ],
            [
                AC_MSG_WARN([Compiler doesn't support MSLU (libunicows.a), disabled.
                             Applications will only run on Windows NT/2000/XP!])
                wxUSE_UNICODE_MSLU=no
            ])
    fi
fi

if test "$wxUSE_wxUSE_EXPERIMENTAL_PRINTF" = "yes"; then
  AC_DEFINE(wxUSE_EXPERIMENTAL_PRINTF)
fi

dnl ----------------------------------------------------------------
dnl Register PostScript options for makefiles and setup.h
dnl ----------------------------------------------------------------

if test "$wxUSE_POSTSCRIPT" = "yes" ; then
  AC_DEFINE(wxUSE_POSTSCRIPT)
fi

AC_DEFINE(wxUSE_AFM_FOR_POSTSCRIPT)

AC_DEFINE(wxUSE_NORMALIZED_PS_FONTS)

dnl ---------------------------------------------------------------------------
dnl big GUI components: MDI, doc/view, printing, help, ...
dnl ---------------------------------------------------------------------------

if test "$wxUSE_CONSTRAINTS" = "yes"; then
    AC_DEFINE(wxUSE_CONSTRAINTS)
    SAMPLES_SUBDIRS="$SAMPLES_SUBDIRS layout"
fi

if test "$wxUSE_MDI" = "yes"; then
    AC_DEFINE(wxUSE_MDI)

    if test "$wxUSE_MDI_ARCHITECTURE" = "yes"; then
        AC_DEFINE(wxUSE_MDI_ARCHITECTURE)
        SAMPLES_SUBDIRS="$SAMPLES_SUBDIRS mdi"
    fi
fi

if test "$wxUSE_DOC_VIEW_ARCHITECTURE" = "yes" ; then
    AC_DEFINE(wxUSE_DOC_VIEW_ARCHITECTURE)
    SAMPLES_SUBDIRS="$SAMPLES_SUBDIRS docview"
    if test "$wxUSE_MDI_ARCHITECTURE" = "yes"; then
        SAMPLES_SUBDIRS="$SAMPLES_SUBDIRS docvwmdi"
    fi
fi

if test "$wxUSE_HELP" = "yes"; then
    AC_DEFINE(wxUSE_HELP)
    SAMPLES_SUBDIRS="$SAMPLES_SUBDIRS help"

    if test "$wxUSE_MSW" = 1; then
      if test "$wxUSE_MS_HTML_HELP" = "yes"; then
        AC_DEFINE(wxUSE_MS_HTML_HELP)
      fi
    fi

    if test "$wxUSE_WXHTML_HELP" = "yes"; then
      if test "$wxUSE_HTML" = "yes"; then
        AC_DEFINE(wxUSE_WXHTML_HELP)
      else
        AC_MSG_WARN(Cannot use wxHTML-based help without wxHTML so it won't be compiled)
        wxUSE_WXHTML_HELP=no
      fi
    fi
fi

if test "$wxUSE_PRINTING_ARCHITECTURE" = "yes" ; then
    if test "$wxUSE_CONSTRAINTS" != "yes"; then
        AC_MSG_WARN(Printing support cannot be used without constraints so it won't be compiled without it)
    else
        AC_DEFINE(wxUSE_PRINTING_ARCHITECTURE)
    fi
    SAMPLES_SUBDIRS="$SAMPLES_SUBDIRS printing"
fi

if test "$wxUSE_PROLOGIO" = "yes" -o  "$wxUSE_RESOURCES" = "yes"; then
    AC_MSG_ERROR([wxExpr and old-style resources are now available in contrib only])
fi

dnl ---------------------------------------------------------------------------
dnl IPC: IPC, Drag'n'Drop, Clipboard, ...
dnl ---------------------------------------------------------------------------

dnl check for ole headers and disable a few features requiring it if not
dnl present (earlier versions of mingw32 don't have ole2.h)
if test "$USE_WIN32" = 1 -a \( "$wxUSE_DATAOBJ"       = "yes" \
                          -o "$wxUSE_CLIPBOARD"     = "yes" \
                          -o "$wxUSE_OLE"     = "yes" \
                          -o "$wxUSE_DRAG_AND_DROP" = "yes" \) ; then
    AC_CHECK_HEADERS(ole2.h)

    if test "$ac_cv_header_ole2_h" = "yes" ; then
        if test "$GCC" = yes ; then
            AC_LANG_SAVE
            AC_LANG_CPLUSPLUS

            AC_MSG_CHECKING([if g++ requires -fvtable-thunks])
            AC_TRY_COMPILE([#include <windows.h>
                            #include <ole2.h>],
                           [],
                           [AC_MSG_RESULT(no)],
                           [AC_MSG_RESULT(yes)
                            CODE_GEN_FLAGS_CXX="$CODE_GEN_FLAGS_CXX -fvtable-thunks"])
            AC_LANG_RESTORE
            LIBS=" -lrpcrt4 -loleaut32 -lole32 -luuid$LIBS"
            if test "$wxUSE_OLE" = "yes" ; then
                AC_DEFINE(wxUSE_OLE)
            fi
        fi

        dnl for OLE clipboard and dnd
        if test "$wxUSE_DATAOBJ" = "yes" ; then
            AC_DEFINE(wxUSE_DATAOBJ)
        fi
    else
        AC_MSG_WARN([Some features disabled because OLE headers not found])

        wxUSE_CLIPBOARD=no
        wxUSE_DRAG_AND_DROP=no
        wxUSE_DATAOBJ=no
        wxUSE_OLE=no
    fi

    if test "$wxUSE_METAFILE" = "yes"; then
        AC_DEFINE(wxUSE_METAFILE)

        dnl this one should probably be made separately configurable
        AC_DEFINE(wxUSE_ENH_METAFILE)
    fi
fi

if test "$wxUSE_IPC" = "yes"; then
    if test "$wxUSE_SOCKETS" != "yes"; then
        AC_MSG_WARN(wxWidgets IPC classes require sockets... disabled)
    fi

    AC_DEFINE(wxUSE_IPC)
fi

if test "$wxUSE_CLIPBOARD" = "yes"; then
    if test "$wxUSE_MGL" = 1; then
        AC_MSG_WARN([Clipboard not yet supported under MGL... disabled])
        wxUSE_CLIPBOARD=no
    fi

    if test "$wxUSE_CLIPBOARD" = "yes"; then
        AC_DEFINE(wxUSE_CLIPBOARD)

        dnl required by clipboard code in configuration check
        AC_DEFINE(wxUSE_DATAOBJ)
    fi
fi

if test "$wxUSE_DRAG_AND_DROP" = "yes" ; then
    if test "$wxUSE_GTK" = 1; then
        if test "$WXGTK12" != 1; then
            AC_MSG_WARN([Drag and drop is only supported under GTK+ 1.2... disabled])
            wxUSE_DRAG_AND_DROP=no
        fi
    fi

    if test "$wxUSE_MOTIF" = 1; then
        AC_MSG_WARN([Drag and drop not yet supported under Motif... disabled])
        wxUSE_DRAG_AND_DROP=no
    fi

    if test "$wxUSE_X11" = 1; then
        AC_MSG_WARN([Drag and drop not yet supported under X11... disabled])
        wxUSE_DRAG_AND_DROP=no
    fi

    if test "$wxUSE_MGL" = 1; then
        AC_MSG_WARN([Drag and drop not yet supported under MGL... disabled])
        wxUSE_DRAG_AND_DROP=no
    fi

    if test "$wxUSE_DRAG_AND_DROP" = "yes"; then
        AC_DEFINE(wxUSE_DRAG_AND_DROP)
    fi

fi

if test "$wxUSE_DRAG_AND_DROP" = "yes" -o "$wxUSE_CLIPBOARD" = "yes"; then
    SAMPLES_SUBDIRS="$SAMPLES_SUBDIRS dnd"
fi

if test "$wxUSE_SPLINES" = "yes" ; then
  AC_DEFINE(wxUSE_SPLINES)
fi

dnl ---------------------------------------------------------------------------
dnl GUI controls
dnl ---------------------------------------------------------------------------

USES_CONTROLS=0
if test "$wxUSE_CONTROLS" = "yes"; then
  USES_CONTROLS=1
fi

if test "$wxUSE_ACCEL" = "yes"; then
  AC_DEFINE(wxUSE_ACCEL)
  USES_CONTROLS=1
fi

if test "$wxUSE_BUTTON" = "yes"; then
  AC_DEFINE(wxUSE_BUTTON)
  USES_CONTROLS=1
fi

if test "$wxUSE_BMPBUTTON" = "yes"; then
  AC_DEFINE(wxUSE_BMPBUTTON)
  USES_CONTROLS=1
fi

if test "$wxUSE_CALCTRL" = "yes"; then
  AC_DEFINE(wxUSE_CALENDARCTRL)
  USES_CONTROLS=1
  SAMPLES_SUBDIRS="$SAMPLES_SUBDIRS calendar"
fi

if test "$wxUSE_CARET" = "yes"; then
  AC_DEFINE(wxUSE_CARET)
  USES_CONTROLS=1
  SAMPLES_SUBDIRS="$SAMPLES_SUBDIRS caret"
fi

if test "$wxUSE_COMBOBOX" = "yes"; then
  AC_DEFINE(wxUSE_COMBOBOX)
  USES_CONTROLS=1
fi

if test "$wxUSE_CHOICE" = "yes"; then
  AC_DEFINE(wxUSE_CHOICE)
  USES_CONTROLS=1
fi

if test "$wxUSE_CHOICEBOOK" = "yes"; then
    AC_DEFINE(wxUSE_CHOICEBOOK)
    USES_CONTROLS=1
fi

if test "$wxUSE_CHECKBOX" = "yes"; then
  AC_DEFINE(wxUSE_CHECKBOX)
  USES_CONTROLS=1
fi

if test "$wxUSE_CHECKLST" = "yes"; then
  AC_DEFINE(wxUSE_CHECKLISTBOX)
  USES_CONTROLS=1
  SAMPLES_SUBDIRS="$SAMPLES_SUBDIRS checklst"
fi

if test "$wxUSE_DISPLAY" = "yes"; then
    AC_DEFINE(wxUSE_DISPLAY)
    SAMPLES_SUBDIRS="$SAMPLES_SUBDIRS display"
fi

if test "$wxUSE_GAUGE" = "yes"; then
  AC_DEFINE(wxUSE_GAUGE)
  USES_CONTROLS=1
fi

if test "$wxUSE_GRID" = "yes"; then
  AC_DEFINE(wxUSE_GRID)
  USES_CONTROLS=1
  SAMPLES_SUBDIRS="$SAMPLES_SUBDIRS grid"
fi

if test "$wxUSE_IMAGLIST" = "yes"; then
    AC_DEFINE(wxUSE_IMAGLIST)
fi

if test "$wxUSE_LISTBOOK" = "yes"; then
    AC_DEFINE(wxUSE_LISTBOOK)
    USES_CONTROLS=1
fi

if test "$wxUSE_LISTBOX" = "yes"; then
    AC_DEFINE(wxUSE_LISTBOX)
    USES_CONTROLS=1
fi

if test "$wxUSE_LISTCTRL" = "yes"; then
    if test "$wxUSE_IMAGLIST" = "yes"; then
        AC_DEFINE(wxUSE_LISTCTRL)
        USES_CONTROLS=1
        SAMPLES_SUBDIRS="$SAMPLES_SUBDIRS listctrl"
    else
        AC_MSG_WARN([wxListCtrl requires wxImageList and won't be compiled without it])
    fi
fi

if test "$wxUSE_NOTEBOOK" = "yes"; then
    AC_DEFINE(wxUSE_NOTEBOOK)
    USES_CONTROLS=1
    SAMPLES_SUBDIRS="$SAMPLES_SUBDIRS notebook"
fi

dnl if test "$wxUSE_PROPSHEET" = "yes"; then
dnl     AC_DEFINE(wxUSE_PROPSHEET)
dnl     USES_CONTROLS=1
dnl     SAMPLES_SUBDIRS="$SAMPLES_SUBDIRS proplist"
dnl fi

if test "$wxUSE_RADIOBOX" = "yes"; then
    AC_DEFINE(wxUSE_RADIOBOX)
    USES_CONTROLS=1
fi

if test "$wxUSE_RADIOBTN" = "yes"; then
    AC_DEFINE(wxUSE_RADIOBTN)
    USES_CONTROLS=1
fi

if test "$wxUSE_SASH" = "yes"; then
    AC_DEFINE(wxUSE_SASH)
    SAMPLES_SUBDIRS="$SAMPLES_SUBDIRS sashtest"
fi

if test "$wxUSE_SCROLLBAR" = "yes"; then
    AC_DEFINE(wxUSE_SCROLLBAR)
    USES_CONTROLS=1
    SAMPLES_SUBDIRS="$SAMPLES_SUBDIRS scroll scrollsub"
fi

if test "$wxUSE_SLIDER" = "yes"; then
    AC_DEFINE(wxUSE_SLIDER)
    USES_CONTROLS=1
fi

if test "$wxUSE_SPINBTN" = "yes"; then
    AC_DEFINE(wxUSE_SPINBTN)
    USES_CONTROLS=1
fi

if test "$wxUSE_SPINCTRL" = "yes"; then
    AC_DEFINE(wxUSE_SPINCTRL)
    USES_CONTROLS=1
fi

if test "$wxUSE_SPLITTER" = "yes"; then
    AC_DEFINE(wxUSE_SPLITTER)
    SAMPLES_SUBDIRS="$SAMPLES_SUBDIRS splitter"
fi

if test "$wxUSE_STATBMP" = "yes"; then
    AC_DEFINE(wxUSE_STATBMP)
    USES_CONTROLS=1
fi

if test "$wxUSE_STATBOX" = "yes"; then
    AC_DEFINE(wxUSE_STATBOX)
    USES_CONTROLS=1
fi

if test "$wxUSE_STATTEXT" = "yes"; then
    AC_DEFINE(wxUSE_STATTEXT)
    USES_CONTROLS=1
fi

if test "$wxUSE_STATLINE" = "yes"; then
    AC_DEFINE(wxUSE_STATLINE)
    USES_CONTROLS=1
fi

if test "$wxUSE_STATUSBAR" = "yes"; then
    AC_DEFINE(wxUSE_STATUSBAR)
    USES_CONTROLS=1

    SAMPLES_SUBDIRS="$SAMPLES_SUBDIRS statbar"
fi

if test "$wxUSE_TABDIALOG" = "yes"; then
    AC_DEFINE(wxUSE_TAB_DIALOG)
    SAMPLES_SUBDIRS="$SAMPLES_SUBDIRS tab"
fi

if test "$wxUSE_TEXTCTRL" = "yes"; then
    AC_DEFINE(wxUSE_TEXTCTRL)
    USES_CONTROLS=1
    SAMPLES_SUBDIRS="$SAMPLES_SUBDIRS text"
fi

if test "$wxUSE_TOGGLEBTN" = "yes"; then
    if test "$wxUSE_COCOA" = 1 ; then
        AC_MSG_WARN([Toggle button not yet supported under Mac OS X... disabled])
        wxUSE_TOGGLEBTN=no
    else
        if test "$wxUSE_PM" = 1; then
            AC_MSG_WARN([Toggle button not yet supported under PM... disabled])
            wxUSE_TOGGLEBTN=no
        fi
    fi
    if test "$wxUSE_UNIVERSAL" = "yes"; then
        AC_MSG_WARN([Toggle button not yet supported under wxUniversal... disabled])
        wxUSE_TOGGLEBTN=no
    fi

    if test "$wxUSE_TOGGLEBTN" = "yes"; then
        AC_DEFINE(wxUSE_TOGGLEBTN)
        USES_CONTROLS=1
    fi
fi

if test "$wxUSE_TOOLBAR_SIMPLE" = "yes"; then
    AC_DEFINE(wxUSE_TOOLBAR_SIMPLE)
    wxUSE_TOOLBAR="yes"
    USES_CONTROLS=1
fi

if test "$wxUSE_TOOLBAR" = "yes"; then
    AC_DEFINE(wxUSE_TOOLBAR)
    USES_CONTROLS=1

    dnl if wxUSE_TOOLBAR and !wxUSE_TOOLBAR_SIMPLE => wxUSE_TOOLBAR_NATIVE
    if test "$wxUSE_TOOLBAR_SIMPLE" != "yes"; then
        wxUSE_TOOLBAR_NATIVE="yes"
    fi

    if test "$wxUSE_TOOLBAR_NATIVE" = "yes"; then
        AC_DEFINE(wxUSE_TOOLBAR_NATIVE)
        USES_CONTROLS=1
    fi

    SAMPLES_SUBDIRS="$SAMPLES_SUBDIRS toolbar"
fi

if test "$wxUSE_TOOLTIPS" = "yes"; then
    if test "$wxUSE_MOTIF" = 1; then
        AC_MSG_WARN([wxTooltip not supported yet under Motif... disabled])
    else
        if test "$wxUSE_UNIVERSAL" = "yes"; then
            AC_MSG_WARN([wxTooltip not supported yet in wxUniversal... disabled])
        else
            AC_DEFINE(wxUSE_TOOLTIPS)
        fi
    fi
fi

if test "$wxUSE_TREECTRL" = "yes"; then
    if test "$wxUSE_IMAGLIST" = "yes"; then
        AC_DEFINE(wxUSE_TREECTRL)
        USES_CONTROLS=1
        SAMPLES_SUBDIRS="$SAMPLES_SUBDIRS treectrl"
    else
        AC_MSG_WARN([wxTreeCtrl requires wxImageList and won't be compiled without it])
    fi
fi

if test "$wxUSE_POPUPWIN" = "yes"; then
    if test "$wxUSE_MAC" = 1 -o "$wxUSE_COCOA" = 1 ; then
        AC_MSG_WARN([Popup window not yet supported under Mac OS X... disabled])
    else
        if test "$wxUSE_PM" = 1; then
            AC_MSG_WARN([wxPopupWindow not yet supported under PM... disabled])
        else
            AC_DEFINE(wxUSE_POPUPWIN)

            USES_CONTROLS=1
        fi
    fi
fi

if test "$wxUSE_TIPWINDOW" = "yes"; then
    if test "$wxUSE_PM" = 1; then
        AC_MSG_WARN([wxTipWindow not yet supported under PM... disabled])
    else
        AC_DEFINE(wxUSE_TIPWINDOW)
    fi
fi

if test "$USES_CONTROLS" = 1; then
    AC_DEFINE(wxUSE_CONTROLS)
fi

dnl ---------------------------------------------------------------------------
dnl misc options
dnl ---------------------------------------------------------------------------

dnl please keep the settings below in alphabetical order
if test "$wxUSE_ACCESSIBILITY" = "yes"; then
    AC_DEFINE(wxUSE_ACCESSIBILITY)
    SAMPLES_SUBDIRS="$SAMPLES_SUBDIRS access"
fi

if test "$wxUSE_DRAGIMAGE" = "yes"; then
    AC_DEFINE(wxUSE_DRAGIMAGE)
    SAMPLES_SUBDIRS="$SAMPLES_SUBDIRS dragimag"
fi

if test "$wxUSE_EXCEPTIONS" = "yes"; then
    if test "$wxUSE_NO_EXCEPTIONS" = "yes" ; then
        AC_MSG_WARN([--enable-exceptions can't be used with --enable-no_exceptions])
    else
        AC_DEFINE(wxUSE_EXCEPTIONS)
        SAMPLES_SUBDIRS="$SAMPLES_SUBDIRS except"
    fi
fi

USE_HTML=0
if test "$wxUSE_HTML" = "yes"; then
    AC_DEFINE(wxUSE_HTML)
    USE_HTML=1
    SAMPLES_SUBDIRS="$SAMPLES_SUBDIRS html html/about html/help html/helpview html/printing html/test html/virtual html/widget html/zip htlbox"
    if test "$wxUSE_MAC" = 1; then
        SAMPLES_SUBDIRS="$SAMPLES_SUBDIRS html/htmlctrl"
    fi
fi
if test "$wxUSE_WEBKIT" = "yes"; then
    if test "$wxUSE_MAC" = 1; then
        old_CPPFLAGS="$CPPFLAGS"
        CPPFLAGS="$CPPFLAGS -x objective-c++"
        AC_CHECK_HEADER([WebKit/HIWebView.h],
                        [
                           AC_DEFINE(wxUSE_WEBKIT)
                           WEBKIT_LINK="-framework WebKit"
                        ],
                        [
                           AC_MSG_WARN([WebKit headers not found; disabling wxWebKit])
                           wxUSE_WEBKIT=no
                        ],
                        [#include <Carbon/Carbon.h>])
        CPPFLAGS="$old_CPPFLAGS"
    elif test "$wxUSE_COCOA" = 1; then
        AC_DEFINE(wxUSE_WEBKIT)
    else
        wxUSE_WEBKIT=no
    fi
fi

USE_XRC=0
if test "$wxUSE_XRC" = "yes"; then
    if test "$wxUSE_XML" != "yes"; then
        AC_MSG_WARN([XML library not built, XRC resources disabled])
        wxUSE_XRC=no
    else
        AC_DEFINE(wxUSE_XRC)
        USE_XRC=1
        SAMPLES_SUBDIRS="$SAMPLES_SUBDIRS xrc"
    fi
fi

if test "$wxUSE_MENUS" = "yes"; then
    AC_DEFINE(wxUSE_MENUS)
    SAMPLES_SUBDIRS="$SAMPLES_SUBDIRS menu"
fi

if test "$wxUSE_METAFILE" = "yes"; then
    AC_DEFINE(wxUSE_METAFILE)
fi

if test "$wxUSE_MIMETYPE" = "yes"; then
    AC_DEFINE(wxUSE_MIMETYPE)
fi

if test "$wxUSE_MINIFRAME" = "yes"; then
    AC_DEFINE(wxUSE_MINIFRAME)
    SAMPLES_SUBDIRS="$SAMPLES_SUBDIRS minifram"
fi

if test "$wxUSE_SYSTEM_OPTIONS" = "yes"; then
    AC_DEFINE(wxUSE_SYSTEM_OPTIONS)
    if test "$TOOLKIT" = "MSW" -o "$TOOLKIT" = "GTK" -o "$TOOLKIT" = "X11" -o \
        "$TOOLKIT" = "MOTIF" -o "$TOOLKIT" = "COCOA"; then
        SAMPLES_SUBDIRS="$SAMPLES_SUBDIRS taskbar"
    fi
fi


if test "$wxUSE_VALIDATORS" = "yes"; then
    AC_DEFINE(wxUSE_VALIDATORS)
    SAMPLES_SUBDIRS="$SAMPLES_SUBDIRS validate"
fi

if test "$wxUSE_PALETTE" = "yes" ; then
  AC_DEFINE(wxUSE_PALETTE)
fi

if test "$wxUSE_UNICODE" = "yes" -a "$wxUSE_UNICODE_MSLU" = "yes" ; then
  dnl Must be done this late because -lunicows must be before all the other libs
  LIBS=" -lunicows $LIBS"
fi

dnl ---------------------------------------------------------------------------
dnl wxImage options
dnl ---------------------------------------------------------------------------

if test "$wxUSE_IMAGE" = "yes" ; then
    AC_DEFINE(wxUSE_IMAGE)

    if test "$wxUSE_GIF" = "yes" ; then
      AC_DEFINE(wxUSE_GIF)
    fi

    if test "$wxUSE_PCX" = "yes" ; then
      AC_DEFINE(wxUSE_PCX)
    fi

    if test "$wxUSE_IFF" = "yes" ; then
      AC_DEFINE(wxUSE_IFF)
    fi

    if test "$wxUSE_PNM" = "yes" ; then
      AC_DEFINE(wxUSE_PNM)
    fi

    if test "$wxUSE_XPM" = "yes" ; then
      AC_DEFINE(wxUSE_XPM)
    fi

    if test "$wxUSE_ICO_CUR" = "yes" ; then
      AC_DEFINE(wxUSE_ICO_CUR)
    fi
fi

dnl ---------------------------------------------------------------------------
dnl common dialogs
dnl ---------------------------------------------------------------------------

if test "$wxUSE_CHOICEDLG" = "yes"; then
    AC_DEFINE(wxUSE_CHOICEDLG)
fi

if test "$wxUSE_COLOURDLG" = "yes"; then
    AC_DEFINE(wxUSE_COLOURDLG)
fi

if test "$wxUSE_FILEDLG" = "yes"; then
    AC_DEFINE(wxUSE_FILEDLG)
fi

if test "$wxUSE_FINDREPLDLG" = "yes"; then
    AC_DEFINE(wxUSE_FINDREPLDLG)
fi

if test "$wxUSE_FONTDLG" = "yes"; then
    AC_DEFINE(wxUSE_FONTDLG)
fi

if test "$wxUSE_DIRDLG" = "yes"; then
    if test "$wxUSE_TREECTRL" != "yes"; then
        AC_MSG_WARN(wxDirDialog requires wxTreeCtrl so it won't be compiled without it)
    else
        AC_DEFINE(wxUSE_DIRDLG)
    fi
fi

if test "$wxUSE_MSGDLG" = "yes"; then
    AC_DEFINE(wxUSE_MSGDLG)
fi

if test "$wxUSE_NUMBERDLG" = "yes"; then
    AC_DEFINE(wxUSE_NUMBERDLG)
fi

if test "$wxUSE_PROGRESSDLG" = "yes"; then
    if test "$wxUSE_CONSTRAINTS" != "yes"; then
       AC_MSG_WARN(wxProgressDialog requires constraints so it won't be compiled without them)
    else
        AC_DEFINE(wxUSE_PROGRESSDLG)
    fi
fi

if test "$wxUSE_SPLASH" = "yes"; then
    AC_DEFINE(wxUSE_SPLASH)
    SAMPLES_SUBDIRS="$SAMPLES_SUBDIRS splash"
fi

if test "$wxUSE_STARTUP_TIPS" = "yes"; then
    if test "$wxUSE_CONSTRAINTS" != "yes"; then
       AC_MSG_WARN(Startup tips requires constraints and won't be compiled without them)
    else
        AC_DEFINE(wxUSE_STARTUP_TIPS)
    fi
fi

if test "$wxUSE_TEXTDLG" = "yes"; then
    AC_DEFINE(wxUSE_TEXTDLG)
fi

if test "$wxUSE_WIZARDDLG" = "yes"; then
    AC_DEFINE(wxUSE_WIZARDDLG)
    SAMPLES_SUBDIRS="$SAMPLES_SUBDIRS wizard"
fi

dnl ---------------------------------------------------------------------------
dnl get the string with OS info - used by wxGetOsDescription() on MacOS X
dnl ---------------------------------------------------------------------------

if test "$cross_compiling" = "yes"; then
    dnl Use best guess from host as we can't use uname when cross compiling
    OSINFO="\"$host\""
else
    dnl attualy work out OS version
    OSINFO=`uname -s -r -m`
    OSINFO="\"$OSINFO\""
fi

AC_DEFINE_UNQUOTED(WXWIN_OS_DESCRIPTION, $OSINFO)

dnl ---------------------------------------------------------------------------
dnl define the variable containing the installation prefix (used in dcpsg.cpp)
dnl ---------------------------------------------------------------------------

if test "x$prefix" != "xNONE"; then
    wxPREFIX=$prefix
else
    wxPREFIX=$ac_default_prefix
fi

AC_DEFINE_UNQUOTED(wxINSTALL_PREFIX, "$wxPREFIX")


dnl ---------------------------------------------------------------------------
dnl define variables with all built libraries for wx-config
dnl ---------------------------------------------------------------------------

CORE_BASE_LIBS="net base"
CORE_GUI_LIBS="adv core"

if test "$wxUSE_XML" = "yes" ; then
    CORE_BASE_LIBS="xml $CORE_BASE_LIBS"
fi
if test "$wxUSE_ODBC" != "no" ; then
    CORE_BASE_LIBS="odbc $CORE_BASE_LIBS"
    CORE_GUI_LIBS="dbgrid $CORE_GUI_LIBS"
fi
if test "$wxUSE_HTML" = "yes" ; then
    CORE_GUI_LIBS="html $CORE_GUI_LIBS"
fi
if test "$wxUSE_XRC" = "yes" ; then
    CORE_GUI_LIBS="xrc $CORE_GUI_LIBS"
fi

if test "$wxUSE_GUI" != "yes"; then
    CORE_GUI_LIBS=""
fi

AC_SUBST(CORE_BASE_LIBS)
AC_SUBST(CORE_GUI_LIBS)


dnl ---------------------------------------------------------------------------
dnl Output the makefiles and such from the results found above
dnl ---------------------------------------------------------------------------

dnl all additional libraries (except wxWidgets itself) we link with

dnl note that we always link with -lm except for Mac OS X
dnl extended.c uses floor() and is always linked in

if test "$wxUSE_MAC" = 1 ; then
    if test "$wxUSE_SOUND" = "yes" || test "$wxUSE_MEDIACTRL" = "yes"; then
       LDFLAGS="$LDFLAGS -framework QuickTime"
    fi
    if test "$USE_DARWIN" = 1; then
       LDFLAGS="$LDFLAGS -framework IOKit"
    fi
    LDFLAGS="$LDFLAGS -framework Carbon -framework Cocoa -framework System"
fi
if test "$wxUSE_COCOA" = 1 ; then
    LDFLAGS="$LDFLAGS -framework Cocoa"
fi

dnl FIXME: should this be covered by the conditional above
dnl given the -lm comment there?  Or should that comment (and
dnl this one) be removed..   [ 7 Nov 2001 ]

LIBS="$ZLIB_LINK $POSIX4_LINK $INET_LINK $WCHAR_LINK $DL_LINK $LIBS"

dnl Only add the -lm library if floating point functions cannot be used
dnl without it.  This check is important on cygwin because of the bizarre
dnl way that they have organized functions into libraries.  On cygwin, both
dnl libc.a and libm.a are symbolic links to a single lib libcygwin.a.  This
dnl means that
dnl   1) linking with -lm is not necessary, and
dnl   2) linking with -lm is dangerous if the order of libraries is wrong
dnl In particular, if you compile any program with -mno-cygwin and link with
dnl -lm, it will crash instantly when it is run.  This happens because the
dnl linker incorrectly links the Cygwin libm.a (==libcygwin.a), which replaces
dnl the ___main function instead of allowing it to be defined by
dnl /usr/lib/mingw/libmingw32.a as it should be.
dnl
dnl On MacOS X, this test will find that -lm is unnecessary and leave it out.
dnl
dnl Just check a few floating point functions. If they are all found without
dnl -lm, then we must not need -lm.
have_cos=0
have_floor=0
AC_CHECK_FUNCS(cos, have_cos=1)
AC_CHECK_FUNCS(floor, have_floor=1)
AC_MSG_CHECKING(if floating point functions link without -lm)
if test "$have_cos" = 1 -a "$have_floor" = 1; then
    AC_MSG_RESULT(yes)
else
    AC_MSG_RESULT(no)
    LIBS="$LIBS -lm"
    # use different functions to avoid configure caching
    have_sin=0
    have_ceil=0
    AC_CHECK_FUNCS(sin, have_sin=1)
    AC_CHECK_FUNCS(ceil, have_ceil=1)
    AC_MSG_CHECKING(if floating point functions link with -lm)
    if test "$have_sin" = 1 -a "$have_ceil" = 1; then
        AC_MSG_RESULT(yes)
    else
        AC_MSG_RESULT(no)
        # not sure we should warn the user, crash, etc.
    fi
fi

if test "$wxUSE_GUI" = "yes"; then

    dnl TODO add checks that these samples will really compile (i.e. all the
    dnl      library features they need are present)

    dnl TODO some samples are never built so far:
    dnl      ipc, mfc, nativdlg, oleauto, ownerdrw
    SAMPLES_SUBDIRS="$SAMPLES_SUBDIRS artprov controls dialogs \
                     drawing dynamic erase event exec font image \
                     minimal propsize rotate shaped widgets render"

    if test "$wxUSE_MONOLITHIC" != "yes"; then
        SAMPLES_SUBDIRS="$SAMPLES_SUBDIRS console"
    fi
else
    SAMPLES_SUBDIRS="console"
fi


dnl all -I options we must pass to the compiler
dnl
dnl note that the order is somewhat important: wxWidgets headers should
dnl come first and the one with setup.h should be before $(top_srcdir)/include
dnl in case the latter contains setup.h used by non-autoconf makefiles (e.g.
dnl CodeWarrior):
INCLUDES="-I\${top_builddir}lib/wx/include/${TOOLCHAIN_FULLNAME} \
-I\${top_srcdir}/include $TOOLKIT_INCLUDE"

dnl C/C++ compiler options used to compile wxWidgets
if test "$GXX" = yes ; then
    dnl CXXWARNINGS="-Wall -W -Wcast-qual -Werror"
    CXXWARNINGS="-Wall"
    dnl should enable this one day...
    dnl CXXWARNINGS="-Wall -Werror"
fi

EXTRA_CFLAGS="$WXDEBUG $WXODBCFLAG $PROFILE $OPTIMISE"

dnl remove the extra white space from the cc/c++/ld options
CPPFLAGS=`echo $WXDEBUG_DEFINE $INCLUDES $CPPFLAGS`
CFLAGS=`echo $CODE_GEN_FLAGS $EXTRA_CFLAGS $CFLAGS $CXXWARNINGS`
CXXFLAGS=`echo $CODE_GEN_FLAGS $CODE_GEN_FLAGS_CXX $EXTRA_CFLAGS $CXXFLAGS $CXXWARNINGS`
    

LIBS=`echo $LIBS`
EXTRALIBS="$LDFLAGS $LDFLAGS_VERSIONING $LIBS $DMALLOC_LIBS"
EXTRALIBS_XML="$EXPAT_LINK"
EXTRALIBS_HTML="$MSPACK_LINK"
EXTRALIBS_ODBC="$ODBC_LINK"
if test "$wxUSE_GUI" = "yes"; then
    EXTRALIBS_GUI=`echo $GUI_TK_LIBRARY $PNG_LINK $JPEG_LINK $TIFF_LINK $WEBKIT_LINK`
fi
if test "$wxUSE_OPENGL" = "yes"; then
    EXTRALIBS_OPENGL="$LDFLAGS_GL $OPENGL_LIBS"
fi
    
LDFLAGS="$LDFLAGS $PROFILE"

dnl wxGTK does not need TOOLKIT includes in wx-config
if test "$wxUSE_GTK" = 1; then
    WXCONFIG_INCLUDE=
else
    WXCONFIG_INCLUDE="$TOOLKIT_INCLUDE"
fi

WXCONFIG_EXTRALIBS="$LIBS"

dnl wx-config must output builtin 3rd party libs in --libs in static build:
if test "$wxUSE_REGEX" = "builtin" ; then
    wxconfig_3rdparty="$wxconfig_3rdparty regex${lib_unicode_suffix}"
fi
if test "$wxUSE_EXPAT" = "builtin" ; then
    wxconfig_3rdparty="$wxconfig_3rdparty expat"
fi
if test "$wxUSE_ODBC" = "builtin" ; then
    wxconfig_3rdparty="$wxconfig_3rdparty odbc"
fi
if test "$wxUSE_LIBTIFF" = "builtin" ; then
    wxconfig_3rdparty="$wxconfig_3rdparty tiff"
fi
if test "$wxUSE_LIBJPEG" = "builtin" ; then
    wxconfig_3rdparty="$wxconfig_3rdparty jpeg"
fi
if test "$wxUSE_LIBPNG" = "builtin" ; then
    wxconfig_3rdparty="$wxconfig_3rdparty png"
fi
if test "$wxUSE_ZLIB" = "builtin" ; then
    wxconfig_3rdparty="$wxconfig_3rdparty zlib"
fi
if test "$wxUSE_ODBC" = "builtin" ; then
    wxconfig_3rdparty="$wxconfig_3rdparty odbc"
fi

for i in $wxconfig_3rdparty ; do
    WXCONFIG_EXTRALIBS="$WXCONFIG_EXTRALIBS -lwx${i}${lib_debug_suffix}${WX_LIB_FLAVOUR}-${WX_RELEASE}${HOST_SUFFIX}"
done


if test "x$wxUSE_UNIVERSAL" = "xyes" ; then
    WXUNIV=1
else
    WXUNIV=0
fi

AC_SUBST(wxUSE_ZLIB)
AC_SUBST(wxUSE_REGEX)
AC_SUBST(wxUSE_EXPAT)
AC_SUBST(wxUSE_ODBC)
AC_SUBST(wxUSE_LIBJPEG)
AC_SUBST(wxUSE_LIBPNG)
AC_SUBST(wxUSE_LIBTIFF)

if test $wxUSE_MONOLITHIC = "yes" ; then
    MONOLITHIC=1
else
    MONOLITHIC=0
fi

if test $wxUSE_PLUGINS = "yes" ; then
    USE_PLUGINS=1
else
    USE_PLUGINS=0
fi

if test $wxUSE_ODBC != "no" ; then
    USE_ODBC=1
else
    USE_ODBC=0
fi

if test $wxUSE_OFFICIAL_BUILD = "yes" ; then
    OFFICIAL_BUILD=1
else
    OFFICIAL_BUILD=0
fi

AC_SUBST(VENDOR)
AC_SUBST(OFFICIAL_BUILD)
AC_SUBST(WX_FLAVOUR)
AC_SUBST(WX_LIB_FLAVOUR)

AC_SUBST(WXUNIV)
AC_SUBST(MONOLITHIC)
AC_SUBST(USE_PLUGINS)
AC_SUBST(LIBS)
AC_SUBST(EXTRALIBS)
AC_SUBST(EXTRALIBS_XML)
AC_SUBST(EXTRALIBS_HTML)
AC_SUBST(EXTRALIBS_ODBC)
AC_SUBST(EXTRALIBS_GUI)
AC_SUBST(EXTRALIBS_OPENGL)
AC_SUBST(EXTRALIBS_SDL)
AC_SUBST(WITH_PLUGIN_SDL)
AC_SUBST(EXTRALIBS_GNOMEPRINT)
AC_SUBST(UNICODE)
AC_SUBST(BUILD)
AC_SUBST(DEBUG_INFO)
AC_SUBST(DEBUG_FLAG)
TOOLKIT_LOWERCASE=`echo $TOOLKIT | tr "[[A-Z]]" "[[a-z]]"`
AC_SUBST(TOOLKIT_LOWERCASE)
AC_SUBST(TOOLKIT_VERSION)
AC_SUBST(SAMPLES_RPATH_FLAG)
AC_SUBST(SAMPLES_RPATH_POSTLINK)
AC_SUBST(HOST_SUFFIX)
AC_SUBST(CPPUNIT_CFLAGS)
AC_SUBST(CPPUNIT_LIBS)

case "$TOOLKIT" in
    GTK)
        TOOLKIT_USER="GTK+"
        if test "$WXGTK20" = 1; then
            TOOLKIT_USER="$TOOLKIT_USER 2"
        fi
        ;;

    *)
        TOOLKIT_USER=$TOOLKIT_LOWERCASE
        ;;
esac

AC_BAKEFILE

if test "$wxUSE_SHARED" = "yes"; then

    dnl We get the shared build linker from bakefile, since it
    dnl moved all the logic for this out of this file and into
    dnl its own macro.  But it can't decide on whether to return
    dnl us $(VAR), ${VAR}, or the present expansion of VAR.
    dnl So normalise and expand everything here now, because its
    dnl not going to change inside wx-config anyway.
    sanitised_bakefile_mess=`echo "$SHARED_LD_CXX" | tr -d '()'`
    EXE_LINKER=`eval echo "$sanitised_bakefile_mess"`

else

    dnl No bakefile support for static builds, but this should be ok for most.
    EXE_LINKER="$CXX -o"

fi

GCC_PRAGMA_FLAGS=""
dnl Find out if we have to define NO_GCC_PRAGMA and WX_PRECOMP:
if test $GCC_PCH = 1 ; then
    GCC_PRAGMA_FLAGS="-DWX_PRECOMP -DNO_GCC_PRAGMA"
else
    case "${host}" in
        powerpc-*-darwin* )
            dnl Some Apple's GCC version are broken and can't handle the
            dnl pragmas:
            GCC_PRAGMA_FLAGS="-DNO_GCC_PRAGMA"
            ;;
        *-*-mingw32* )
            dnl MinGW GCC versions >= 3.2 have problems with
            dnl static member of classes derived from templates
            dnl in combination with #pragma interface/implementation
            dnl (the test case uses 4 files)
            dnl ... and with exceptions handling (undefined symbols needed
            dnl to correctly calls dtors when unwinding) as well
            if test "$wxUSE_STL" = "yes" -o \
                    "$wxUSE_NO_EXCEPTIONS" != "yes" -o \
                    "$wxUSE_NO_RTTI" != "yes"; then
               AC_MSG_CHECKING([if this MinGW version needs -DNO_GCC_PRAGMA])
               AC_TRY_COMPILE([],
                              [#if !(__GNUC__ > 3 || (__GNUC__ == 3 && __GNUC_MINOR__ >= 2))
                               #error "Not GCC 3.2 or greater"
                               #endif
                               ],
                              [GCC_PRAGMA_FLAGS="-DNO_GCC_PRAGMA"
                               AC_MSG_RESULT([yes])],
                              [AC_MSG_RESULT([no])])
            fi
            ;;
    esac
fi
CPPFLAGS="$GCC_PRAGMA_FLAGS $CPPFLAGS"


dnl for convenience, sort the samples in alphabetical order
dnl
dnl FIXME For some mysterious reasons, sometimes the directories are duplicated
dnl       in this list - hence uniq. But normally, this shouldn't be needed!
dnl
dnl       Unfortunately, there is a bug in OS/2's tr, such that
dnl       tr ' ' '\n' introduces DOS-like line breaks, whereas tr '\n' ' '
dnl       only removes the Unix-like part of the introduced line break.
SAMPLES_SUBDIRS="`echo $SAMPLES_SUBDIRS | tr -s ' ' | tr ' ' '\n' | sort | uniq | tr '\n' ' '| tr -d '\r'`"

dnl subtle bakefile goop.
dnl Used in wx-config now too, as its STATIC_FLAG with different makeup.
dnl I wish we would have called it something less likely to clash with
dnl things though.
AC_SUBST(SHARED)

dnl global options
AC_SUBST(WX_LIBRARY_BASENAME_NOGUI)
AC_SUBST(WX_LIBRARY_BASENAME_GUI)

dnl debugging options
AC_SUBST(WXDEBUG_DEFINE)

dnl toolkit options
AC_SUBST(USE_GUI)
AC_SUBST(AFMINSTALL)
AC_SUBST(WIN32INSTALL)
AC_SUBST(TOOLKIT)
AC_SUBST(TOOLKIT_DIR)
AC_SUBST(TOOLCHAIN_NAME)
AC_SUBST(TOOLCHAIN_FULLNAME)
AC_SUBST(TOOLCHAIN_DEFS)
AC_SUBST(TOOLCHAIN_DLL_DEFS)

dnl wx-config options
AC_SUBST(host_alias)
AC_SUBST(cross_compiling)
AC_SUBST(WIDGET_SET)
AC_SUBST(WX_RELEASE)
AC_SUBST(WX_VERSION)
AC_SUBST(WX_SUBVERSION)
AC_SUBST(WX_CHARTYPE)
AC_SUBST(WX_DEBUGTYPE)
AC_SUBST(WXCONFIG_EXTRALIBS)
AC_SUBST(WXCONFIG_INCLUDE)
AC_SUBST(WXCONFIG_RPATH)
AC_SUBST(WXCONFIG_LDFLAGS_GUI)
AC_SUBST(WX_LARGEFILE_FLAGS)
AC_SUBST(GCC_PRAGMA_FLAGS)
AC_SUBST(CODE_GEN_FLAGS)
AC_SUBST(CODE_GEN_FLAGS_CXX)
AC_SUBST(EXE_LINKER)

dnl distribution vars
AC_SUBST(GUIDIST)
AC_SUBST(DISTDIR)

dnl additional subdirectories where we will build
AC_SUBST(SAMPLES_SUBDIRS)

dnl additional libraries and linker settings
AC_SUBST(LDFLAGS)
AC_SUBST(LDFLAGS_GL)
AC_SUBST(OPENGL_LIBS)
AC_SUBST(DMALLOC_LIBS)
AC_SUBST(WX_VERSION_TAG)

dnl additional resurces settings
AC_SUBST(RESCOMP)
AC_SUBST(RESFLAGS)
AC_SUBST(RESPROGRAMOBJ)
AC_SUBST(WX_RESOURCES_MACOSX_ASCII)
AC_SUBST(WX_RESOURCES_MACOSX_DATA)

dnl additional for Mac OS X
AC_SUBST(DEREZ)
AC_SUBST(LIBWXMACRES)
AC_SUBST(MACRESCOMP)
AC_SUBST(MACSETFILE)
AC_SUBST(MACRESWXCONFIG)

dnl other tools
AC_SUBST(GCC)
AC_SUBST(DLLTOOL)


dnl MAKE_SET will be replaced with "MAKE=..." or nothing if make sets MAKE
dnl itself (this macro is required if SUBDIRS variable is used in Makefile.am
dnl - and we do use it)
AC_PROG_MAKE_SET


AC_CONFIG_HEADERS([lib/wx/include/${TOOLCHAIN_FULLNAME}/wx/setup.h:setup.h.in])

AC_CONFIG_FILES([ lib/wx/config/${TOOLCHAIN_FULLNAME}:wx-config.in ],
                [ chmod +x lib/wx/config/${TOOLCHAIN_FULLNAME} ],
                [ TOOLCHAIN_FULLNAME="${TOOLCHAIN_FULLNAME}" ])

AC_CONFIG_FILES([ lib/wx/config/inplace-${TOOLCHAIN_FULLNAME}:wx-config-inplace.in ],
                [ chmod +x lib/wx/config/inplace-${TOOLCHAIN_FULLNAME} ],
                [ TOOLCHAIN_FULLNAME="${TOOLCHAIN_FULLNAME}" ])

AC_CONFIG_FILES([ version-script Makefile ])

AC_CONFIG_COMMANDS([ wx-config
                   ],
                   [ rm -f wx-config
                     ${LN_S} lib/wx/config/inplace-${TOOLCHAIN_FULLNAME} wx-config
                   ],
                   [ TOOLCHAIN_FULLNAME="${TOOLCHAIN_FULLNAME}"
                     LN_S="${LN_S}"
                   ])

dnl This would give us us build dir that in every significant way
dnl resembles an installed wx in prefix=$builddir.  It is troublesome
dnl though in this form because AC_CONFIG_LINKS will fail for directories
dnl on platforms that do not have symlinks.
dnl AC_CONFIG_LINKS([ include/wx-$WX_RELEASE$WX_FLAVOUR:include ])
dnl AC_CONFIG_LINKS([ contrib/include ])


dnl Configure samples, contrib etc. directories, but only if they are present:
if test "$wxUSE_GUI" = "yes"; then
    SUBDIRS="samples demos utils contrib"
else dnl we build wxBase only
    dnl there are no wxBase programs in contrib nor demos
    SUBDIRS="samples utils"
fi
dnl Add tests to the list of subdirs if cppunit 1.8.0+ is detected
AM_PATH_CPPUNIT(1.8.0, [SUBDIRS="$SUBDIRS tests"])

for subdir in `echo $SUBDIRS`; do
    if test -d ${srcdir}/${subdir} ; then
        if test "$wxUSE_GUI" = "yes"; then
            if test ${subdir} = "samples"; then
                dnl only take those samples which compile in the current
                dnl configuration and which exist
                makefiles="samples/Makefile.in $makefiles"
                for sample in `echo $SAMPLES_SUBDIRS`; do
                    if test -d $srcdir/samples/$sample; then
                        makefiles="samples/$sample/Makefile.in $makefiles"
                    fi
                done
            else dnl assume that everything compiles for utils &c
                makefiles=`(cd $srcdir ; find $subdir -name Makefile.in)`
            fi
        else dnl we build wxBase only
            dnl don't take all samples/utils, just those which build with
            dnl wxBase
            if test ${subdir} = "samples"; then
                makefiles="samples/Makefile.in samples/console/Makefile.in"
            elif test ${subdir} = "utils"; then
                makefiles=""
                for util in HelpGen tex2rtf ; do
                    if test -d $srcdir/utils/$util ; then
                        makefiles="utils/$util/Makefile.in \
                                   utils/$util/src/Makefile.in \
                                   $makefiles"
                    fi
                done
            else dnl assume that everything compiles for tests
                makefiles=`(cd $srcdir ; find $subdir -name Makefile.in)`
            fi
        fi

        for mkin in $makefiles ; do
            mk=`echo $mkin | sed 's/Makefile\.in/Makefile/g'`
            AC_CONFIG_FILES([$mk])
        done
    fi
done


AC_OUTPUT

dnl report how we have been configured
echo
echo "Configured wxWidgets ${WX_VERSION} for \`${host}'"
echo ""
echo "  Which GUI toolkit should wxWidgets use?                 ${TOOLKIT_USER:-base only}"
echo "  Should wxWidgets be compiled into single library?       ${wxUSE_MONOLITHIC:-yes}"

echo "  Should wxWidgets be compiled in debug mode?             ${wxUSE_DEBUG:-no}"
echo "  Should wxWidgets be linked as a shared library?         ${wxUSE_SHARED:-no}"
echo "  Should wxWidgets be compiled in Unicode mode?           ${wxUSE_UNICODE:-no}"

echo "  What level of wxWidgets compatibility should be enabled?"
echo "                                       wxWidgets 2.2      ${WXWIN_COMPATIBILITY_2_2:-no}"
echo "                                       wxWidgets 2.4      ${WXWIN_COMPATIBILITY_2_4:-yes}"

echo "  Which libraries should wxWidgets use?"
echo "                                       jpeg               ${wxUSE_LIBJPEG-none}"
echo "                                       png                ${wxUSE_LIBPNG-none}"
echo "                                       regex              ${wxUSE_REGEX}"
echo "                                       tiff               ${wxUSE_LIBTIFF-none}"
if test "$wxUSE_X11" = 1 -o "$wxUSE_MOTIF" = 1; then
echo "                                       xpm                ${wxUSE_LIBXPM-none}"
fi
echo "                                       zlib               ${wxUSE_ZLIB}"
echo "                                       odbc               ${wxUSE_ODBC}"
echo "                                       expat              ${wxUSE_EXPAT}"
echo "                                       libmspack          ${wxUSE_LIBMSPACK}"
echo "                                       sdl                ${wxUSE_LIBSDL}"
echo "                                       gnomeprint         ${wxUSE_LIBGNOMEPRINT}"

echo ""

dnl vi: set et sts=4 sw=4 com=\:dnl:
